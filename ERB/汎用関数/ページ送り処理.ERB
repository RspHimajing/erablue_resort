;=========================================================
;	・ページ送り処理の共通化
;	キャラ配列フィルタ処理と併用することで、最大表示数を個別に指定する必要がなくなります。
;=========================================================

;引数	ボタン番号:0 前ページ, 1 最初のページ, 2 ページ指定, 3 次ページ, 4 最終ページ, 5 ‐５, 6 ＋５, 7 表示数変更
;			1～7は省略可能。1を省略すると前ページ・現在ページ数・次ページの1行のみの表示になる
;			2、3を省略するとボタン:0からそれぞれ+1、+2された数字を使用する
;			4～7を省略するとボタン:1から+1～+4された数字を使用する
;		人員以外のリストに使用したい場合は項目名に単位数を指定すること
@ページ送り共通表示(現在ページ, 項目表示数, 最大表示数, ボタン番号:0, ボタン番号:1, 項目名 = "人", ボタン番号:7, ボタン番号:2, ボタン番号:3, ボタン番号:4, ボタン番号:5, ボタン番号:6)
#DIM 現在ページ
#DIM DYNAMIC ボタン番号, 8
#DIMS 項目名
#DIM 最大ページ数

;前のページのボタン番号がない場合は何もしない
SIF !ボタン番号:0
	RETURN

;最大表示数から最大ページ数を算出
最大ページ数 = MAX(最大表示数 / 項目表示数 - (最大表示数%項目表示数 ? 0# 1), 0)

SIF !ボタン番号:2 || ボタン番号:2 == ボタン番号:0
	ボタン番号:2 = ボタン番号:0 + 1

SIF !ボタン番号:3 || ボタン番号:3 == ボタン番号:0
	ボタン番号:3 = ボタン番号:0 + 2

IF 現在ページ > 0
	PRINTBUTTON @"[{ボタン番号:0, 4}] 前のページへ", ボタン番号:0
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM [{ボタン番号:0, 4}] 前のページへ
	RESETCOLOR
ENDIF
PRINTS " " * 22
PRINTBUTTON @"{1+現在ページ, 4, RIGHT}/{1+最大ページ数, 4, RIGHT}", ボタン番号:2
PRINTS " " * 25
IF 現在ページ < 最大ページ数
	PRINTBUTTON @"[{ボタン番号:3, 4}] 次のページへ", ボタン番号:3
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM [{ボタン番号:3, 4}] 次のページへ
	RESETCOLOR
ENDIF
PRINTL
;最初のページのボタン番号がない場合は1行で終了
IF ボタン番号:1
	WHILE GROUPMATCH(ボタン番号:1, ボタン番号:0, ボタン番号:2, ボタン番号:3)
		ボタン番号:1 ++
	WEND
	FOR LOCAL, 1, 5
		SIF !ボタン番号:(LOCAL+3) || ボタン番号:(LOCAL+3) == ボタン番号:1
			ボタン番号:(LOCAL+3) = ボタン番号:1 + LOCAL
	NEXT
	IF 現在ページ > 0
		PRINTBUTTON "[|≪] ", ボタン番号:1
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAINFORM [|≪] 
		RESETCOLOR
	ENDIF
	IF 現在ページ >= 5
		PRINTBUTTON "[－５]", ボタン番号:5
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAINFORM [－５]
		RESETCOLOR
	ENDIF
	PRINTS " " * (30 - STRLENS(項目名))
	IF ボタン番号:7 > 0
		PRINTBUTTON @"{項目表示数, 3, RIGHT}%項目名%/ページ", ボタン番号:7
	ELSE
		PRINTPLAINFORM {項目表示数, 3, RIGHT}%項目名%/ページ
	ENDIF
	PRINTS " " * 30
	IF 現在ページ <= 最大ページ数 - 5
		PRINTBUTTON "[＋５] ", ボタン番号:6
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAINFORM [＋５] 
		RESETCOLOR
	ENDIF
	IF 現在ページ < 最大ページ数
		PRINTBUTTON "[≫|]", ボタン番号:4
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAINFORM [≫|]
		RESETCOLOR
	ENDIF
	PRINTL
ENDIF

@ページ送り共通実行(ARG, 現在ページ, 項目表示数, 最大表示数, ボタン番号:0, ボタン番号:1, 項目名 = "人", ボタン番号:7, ボタン番号:2, ボタン番号:3, ボタン番号:4, ボタン番号:5, ボタン番号:6)
#DIM REF 現在ページ
#DIM DYNAMIC ボタン番号, 8
#DIMS 項目名
#DIM 最大ページ数

;最大表示数から最大ページ数を算出
最大ページ数 = MAX(最大表示数 / 項目表示数 - (最大表示数%項目表示数 ? 0# 1), 0)

SIF !ボタン番号:2 || ボタン番号:2 == ボタン番号:0
	ボタン番号:2 = ボタン番号:0 + 1

SIF !ボタン番号:3 || ボタン番号:3 == ボタン番号:0
	ボタン番号:3 = ボタン番号:0 + 2

IF ARG == ボタン番号:0
		現在ページ --
ELSEIF ARG == ボタン番号:2
		PRINTFORML 表示したいページ数を入力してください（1～{最大ページ数+1}）
		SETTEXTBOX TOSTR(現在ページ+1)
		INPUT
		IF INRANGE(RESULT, 1, 最大ページ数)
			現在ページ = RESULT-1
		ELSEIF RESULT < 1
			現在ページ = 0
		ELSE
			現在ページ = 最大ページ数
		ENDIF
ELSEIF ARG == ボタン番号:3
		現在ページ ++
ELSEIF ボタン番号:1
	WHILE GROUPMATCH(ボタン番号:1, ボタン番号:0, ボタン番号:2, ボタン番号:3)
		ボタン番号:1 ++
	WEND
	FOR LOCAL, 1, 5
		SIF !ボタン番号:(LOCAL+3) || ボタン番号:(LOCAL+3) == ボタン番号:1
			ボタン番号:(LOCAL+3) = ボタン番号:1 + LOCAL
	NEXT
	SELECTCASE ARG
		CASE ボタン番号:1
			現在ページ = 0
		CASE ボタン番号:4
			現在ページ = 最大ページ数
		CASE ボタン番号:5
			現在ページ -= 5
		CASE ボタン番号:6
			現在ページ += 5
		CASE ボタン番号:7
			SIF ボタン番号 < 0
				RETURN 0

			PRINTFORML 表示したい%項目名%数を入力してください（2～100）
			SETTEXTBOX TOSTR(項目表示数)
			INPUT
			IF INRANGE(RESULT, 2, 100)
				項目表示数 = RESULT
			ELSEIF RESULT < 2
				項目表示数 = 2
			ELSE
				項目表示数 = 100
			ENDIF
			現在ページ = LIMIT(現在ページ, 0, MAX(最大表示数 / 項目表示数 - (最大表示数%項目表示数 ? 0# 1), 0))
	ENDSELECT
ENDIF

[SKIPSTART]
;ページめくり機能_共通表示処理
CALL ページ送り共通表示(現在ページ, 項目表示数, 最大表示数, 9000, 9003)
;ページめくり機能_共通実行処理
	CASE 9000 TO 9007
		CALL ページ送り共通実行(RESULT, 現在ページ, 項目表示数, 最大表示数, 9000, 9003)
		GOTO 表示_LOOP
[SKIPEND]