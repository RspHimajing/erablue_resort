

;-------------------------------------------------
;下着についてランダム設定する
;一度設定したものは一日を通して有効
;-------------------------------------------------
@今日の下着文字列生成(対象キャラ)
#FUNCTIONS
#DIM 対象キャラ
#DIM ぱんつネーム

;既に今日のぱんつが決定されてるならそれを返す
IF 今日の服装:対象キャラ:下着 != ""
	RETURNF 今日の服装:対象キャラ:下着
ENDIF

;まず口上での個別設定を確認
IF EXISTFUNCTION(@"固有ぱんつ設定_{NO:対象キャラ}")
	RESULTS = 
	CALLFORMF 固有ぱんつ設定_{NO:対象キャラ}(対象キャラ)
	IF RESULTS != ""
		今日の服装:対象キャラ:下着 = %RESULTS%
		RETURNF 今日の服装:対象キャラ:下着
	ENDIF
ENDIF

;口上設定がないなら素質ごとに分岐
;色設定
STRDATA LOCALS:0
	DATA 白の
	DATA ピンクの
	DATA 黒の
	DATA 赤い
	DATA パステルカラーの
	DATA 青の
ENDDATA
IF TEQUIP:対象キャラ:下半身下着あり == 3
	今日の服装:対象キャラ:下着 = 白いふんどし
	今日の服装:対象キャラ:下着の色 = 白の
ELSEIF TEQUIP:対象キャラ:下半身下着あり == 2
	今日の服装:対象キャラ:下着 = %LOCALS:0%前張り
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ELSEIF TEQUIP:対象キャラ:下半身下着あり == 4
	今日の服装:対象キャラ:下着 = %LOCALS:0%リボン
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ELSEIF TEQUIP:対象キャラ:下半身下着あり == -1
	今日の服装:対象キャラ:下着 = %LOCALS:0%穴が空いた下着
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ELSEIF TEQUIP:対象キャラ:下半身下着あり == -2
	今日の服装:対象キャラ:下着 = %LOCALS:0%宝石が煌めく、%TEXTR("クリトリス/ラビア/クリとラビア")%を貫くピアス
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ELSEIF TEQUIP:対象キャラ:下半身下着あり == -3
	今日の服装:対象キャラ:下着 = クリトリスからぶら下がる、%LOCALS:0%台紙に
	IF !RAND:3
		今日の服装:対象キャラ:下着 += TEXTR("卑猥な言葉/常軌を逸した役職名/膣穴の具合")
	ELSEIF !RAND:999
		;お好きな方はもっと確率あげてください
		今日の服装:対象キャラ:下着 += TEXTR("「use free」/「big cock only」/「ご自由にお使い下さい♥」/「短小ちんぽお断り」/「ドラフ用ペニスケース」") + "の文字"
	ELSE
		今日の服装:対象キャラ:下着 += TEXTR("「オーナー専用肉便器」/「オーナー専用生オナホ」/「オーナー専用孕み袋」/「オーナー専用コキ捨て穴」/「お触り厳禁」") + "の文字"
	ENDIF
	今日の服装:対象キャラ:下着 += "と名前が併記された名札"
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ELSEIF 知識素質:対象キャラ:性知識 > 1 && BASE:対象キャラ:性欲 > 1500
	STRDATA LOCALS:1
		DATA 穴が空いた下着
		DATA Ｔバック
		DATA 紐パン
		DATA 透けた下着
	ENDDATA
	今日の服装:対象キャラ:下着 = %LOCALS:0%%LOCALS:1%
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ELSEIF TALENT:対象キャラ:精神的余裕 < 0 || TALENT:対象キャラ:年齢層 == 年齢層_幼い
	STRDATA LOCALS:1
		DATA こどもぱんつ
		DATA 可愛い下着
		DATA シンプルな下着
		DATA 縞パン
	ENDDATA
	今日の服装:対象キャラ:下着 = %LOCALS:0%%LOCALS:1%
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ELSE
	STRDATA LOCALS:1
		DATA 清楚な下着
		DATA 可愛い下着
		DATA シンプルな下着
		DATA セクシーな下着
		DATA 際どい下着
	ENDDATA
	今日の服装:対象キャラ:下着 = %LOCALS:0%%LOCALS:1%
	今日の服装:対象キャラ:下着の色 = %LOCALS:0%
ENDIF

RETURNF 今日の服装:対象キャラ:下着
