
;----------------------------------------------------
;キャラ固有コマンド処理
;----------------------------------------------------
;コマンド名
;----------------------------------------------------
@CHARACOMNAME370_25
#FUNCTION
LOCAL = GETCHARA(25)
TSTR:コマンド名受渡 = 肉体再錬成
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_370_25
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_370_25
LOCALS = <br>■肉体再錬成<br>
LOCALS += "カリオストロに頼んで、ZPを使うことなく肉体の外見を変化させて錬成しなおしてもらう"
LOCALS += "<br>経過時間：60分"
TSTR:ツールチップ受渡 = %LOCALS%

;-------------------------------------------------
;実行処理
;-------------------------------------------------
@CHARACOM370_25
#DIM 変更フラグ, 7
#DIM 変更後数値, 4
#DIM CONST 変更箇所 = 161, 205, 163, 210, 2, 200, 162
#DIM L_COUNT
#DIM CONST 全長 = 0
#DIM CONST 胸囲 = 1
#DIM CONST 腹囲 = 2
#DIM CONST 臀囲 = 3
#DIM CONST 雌雄 = 4
#DIM CONST 人種 = 5 
#DIM CONST 竿長 = 6
#DIMS CONST 変更フラグ名 = "全長", "胸囲", "腹囲","臀囲","雌雄","人種","竿長"

DEBUGCLEAR
LOCAL = GETCHARA(25)
VARSET 変更フラグ, -5
VARSET 変更後数値, 0

PRINTL
L_COUNT = LINECOUNT

$LOOP_START
CLEARLINE LINECOUNT - L_COUNT
PRINT 「
FONTITALIC
PRINT で、どんな設計にするんだ？
FONTSTYLE 0
PRINTL 」
PRINTL どの素質を変化させますか？
DRAWLINE

PRINT 性別：　
FOR LOCAL , 1, 4
	SIF 素質切り替えOPTION記憶:ふたなり_女性 && LOCAL == 3
		CONTINUE
	IF TALENT:性別 == LOCAL && (変更フラグ:雌雄 == LOCAL || 変更フラグ:雌雄 == -5)
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL, 2}]%GET_TALENTNAME(変更箇所:雌雄, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%
		RESETCOLOR
	ELSEIF 変更フラグ:雌雄 == LOCAL
		SETCOLOR 0xFFFFCC
		PRINTPLAINFORM [{LOCAL, 2}]%GET_TALENTNAME(変更箇所:雌雄, LOCAL, 0,TARGET)+"（予定）", 23, LEFT%
		RESETCOLOR
	ELSEIF TALENT:性別 == LOCAL
		PRINTBUTTON @"[{LOCAL, 2}]%GET_TALENTNAME(変更箇所:雌雄, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%", LOCAL
	ELSE
		PRINTBUTTON @"[{LOCAL, 2}]%GET_TALENTNAME(変更箇所:雌雄, LOCAL, 0,TARGET), 23, LEFT%", LOCAL
	ENDIF
NEXT
PRINTL 

PRINT 種族：　
FOR LOCAL , 1, 7
	IF TALENT:種族 == LOCAL && (変更フラグ:人種 == LOCAL || 変更フラグ:人種 == -5)
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL+10}]%GET_TALENTNAME(変更箇所:人種, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%
		RESETCOLOR
	ELSEIF 変更フラグ:人種 == LOCAL
		SETCOLOR 0xFFFFCC
		PRINTPLAINFORM [{LOCAL+10}]%GET_TALENTNAME(変更箇所:人種, LOCAL, 0,TARGET)+"（予定）", 23, LEFT%
		RESETCOLOR
	ELSEIF TALENT:種族 == LOCAL
		PRINTBUTTON @"[{LOCAL+10}]%GET_TALENTNAME(変更箇所:人種, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%", LOCAL+10
	ELSE
		PRINTBUTTON @"[{LOCAL+10}]%GET_TALENTNAME(変更箇所:人種, LOCAL, 0,TARGET), 23, LEFT%", LOCAL+10
	ENDIF
NEXT
PRINTL 

PRINT 体格：　
FOR LOCAL, -3, 3
	IF TALENT:体格 == LOCAL && (変更フラグ:全長 == LOCAL || 変更フラグ:全長 == -5)
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL+44}]%GET_TALENTNAME(変更箇所:全長, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%
		RESETCOLOR
	ELSEIF 変更フラグ:全長 == LOCAL
		SETCOLOR 0xFFFFCC
		PRINTPLAINFORM [{LOCAL+44}]%GET_TALENTNAME(変更箇所:全長, LOCAL, 0,TARGET)+"（予定）", 23, LEFT%
		RESETCOLOR
	ELSEIF TALENT:体格 == LOCAL
		PRINTBUTTON @"[{LOCAL+44}]%GET_TALENTNAME(変更箇所:全長, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%", LOCAL+44
	ELSE
		PRINTBUTTON @"[{LOCAL+44}]%GET_TALENTNAME(変更箇所:全長, LOCAL, 0,TARGET), 23, LEFT%", LOCAL+44
	ENDIF
NEXT
PRINTL

PRINT 体型：　
FOR LOCAL, -3, 4
	IF TALENT:体型 == LOCAL && (変更フラグ:腹囲 == LOCAL || 変更フラグ:腹囲 == -5)
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL+54}]%GET_TALENTNAME(変更箇所:腹囲, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%
		RESETCOLOR
	ELSEIF 変更フラグ:腹囲 == LOCAL
		SETCOLOR 0xFFFFCC
		PRINTPLAINFORM [{LOCAL+54}]%GET_TALENTNAME(変更箇所:腹囲, LOCAL, 0,TARGET)+"（予定）", 23, LEFT%
		RESETCOLOR
	ELSEIF TALENT:体型 == LOCAL
		PRINTBUTTON @"[{LOCAL+54}]%GET_TALENTNAME(変更箇所:腹囲, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%", LOCAL+54
	ELSE
		PRINTBUTTON @"[{LOCAL+54}]%GET_TALENTNAME(変更箇所:腹囲, LOCAL, 0,TARGET), 23, LEFT%", LOCAL+54
	ENDIF
NEXT
PRINTL

PRINT 下半身：
FOR LOCAL, -1, 2
	IF TALENT:下半身 == LOCAL && (変更フラグ:臀囲 == LOCAL || 変更フラグ:臀囲 == -5)
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL+62}]%GET_TALENTNAME(変更箇所:臀囲, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%
		RESETCOLOR
	ELSEIF 変更フラグ:臀囲 == LOCAL
		SETCOLOR 0xFFFFCC
		PRINTPLAINFORM [{LOCAL+62}]%GET_TALENTNAME(変更箇所:臀囲, LOCAL, 0,TARGET)+"（予定）", 23, LEFT%
		RESETCOLOR
	ELSEIF TALENT:下半身 == LOCAL
		PRINTBUTTON @"[{LOCAL+62}]%GET_TALENTNAME(変更箇所:臀囲, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%", LOCAL+62
	ELSE
		PRINTBUTTON @"[{LOCAL+62}]%GET_TALENTNAME(変更箇所:臀囲, LOCAL, 0,TARGET), 23, LEFT%", LOCAL+62
	ENDIF
NEXT
PRINTL

IF GETBIT(TALENT:性別, 0) || (変更フラグ:雌雄 > -1 && GETBIT(変更フラグ:雌雄, 0))
	PRINTL 乳の大きさ：
	PRINT 　　　　
	FOR LOCAL, -2, 4
		IF TALENT:バストサイズ == LOCAL && (変更フラグ:胸囲 == LOCAL || 変更フラグ:胸囲 == -5)
			SETCOLOR 0x666666
			PRINTPLAINFORM [{LOCAL+23}]%GET_TALENTNAME(変更箇所:胸囲, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%
			RESETCOLOR
		ELSEIF 変更フラグ:胸囲 == LOCAL
			SETCOLOR 0xFFFFCC
			PRINTPLAINFORM [{LOCAL+23}]%GET_TALENTNAME(変更箇所:胸囲, LOCAL, 0,TARGET)+"（予定）", 23, LEFT%
			RESETCOLOR
		ELSEIF TALENT:バストサイズ == LOCAL
			PRINTBUTTON @"[{LOCAL+23}]%GET_TALENTNAME(変更箇所:胸囲, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%", LOCAL+23
		ELSE
			PRINTBUTTON @"[{LOCAL+23}]%GET_TALENTNAME(変更箇所:胸囲, LOCAL, 0,TARGET), 23, LEFT%", LOCAL+23
		ENDIF
	NEXT
ENDIF
PRINTL 

IF GETBIT(TALENT:性別, 1) || (変更フラグ:雌雄 > -1 && GETBIT(変更フラグ:雌雄, 1))
	PRINTL 男性器の大きさ：
	PRINT 　　　　
	FOR LOCAL , -2, 3
		IF TALENT:男性器サイズ == LOCAL && (変更フラグ:竿長 == LOCAL || 変更フラグ:竿長 == -5)
			SETCOLOR 0x666666
			PRINTPLAINFORM [{LOCAL+33}]%GET_TALENTNAME(変更箇所:竿長, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%
			RESETCOLOR
		ELSEIF 変更フラグ:竿長 == LOCAL
			SETCOLOR 0xFFFFCC
			PRINTPLAINFORM [{LOCAL+33}]%GET_TALENTNAME(変更箇所:竿長, LOCAL, 0,TARGET)+"（予定）", 23, LEFT%
			RESETCOLOR
		ELSEIF TALENT:男性器サイズ == LOCAL
			PRINTBUTTON @"[{LOCAL+33}]%GET_TALENTNAME(変更箇所:竿長, LOCAL, 0,TARGET)+"（現在）", 23, LEFT%", LOCAL+33
		ELSE
			PRINTBUTTON @"[{LOCAL+33}]%GET_TALENTNAME(変更箇所:竿長, LOCAL, 0,TARGET), 23, LEFT%", LOCAL+33
		ENDIF
	NEXT
ENDIF
PRINTL 

PRINTBUTTONC "[900] 変更キャンセル", 900
PRINTBUTTONC "[910] 素の状態に戻す", 910
PRINTBUTTONC "[999] 終了する", 999
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 1 TO 9
		;性別
		変更フラグ:雌雄 = RESULT
		GOTO LOOP_START
	CASE 10 TO 19
		;種族
		変更フラグ:人種 = RESULT - 10
		GOTO LOOP_START
	CASE 20 TO 29
		;バスト
		IF RESULT == 27
		ELSEIF RESULT < 27
			変更フラグ:胸囲 = RESULT - 23
		ENDIF
		GOTO LOOP_START
	CASE 30 TO 39
		;男性器
		変更フラグ:竿長 = RESULT - 33
		GOTO LOOP_START
	CASE 40 TO 49
		;体格・身長
		IF RESULT == 47
		ELSEIF RESULT < 47
			変更フラグ:全長 = RESULT - 44
		ENDIF
				GOTO LOOP_START
	CASE 50 TO 59
		;体型・ウエスト
		IF RESULT == 58
		ELSEIF RESULT < 58
			変更フラグ:腹囲 = RESULT - 54
		ENDIF
		GOTO LOOP_START
	CASE 60 TO 69
		;下半身・ヒップ
		IF RESULT == 64
		ELSEIF RESULT < 64
			変更フラグ:臀囲 = RESULT - 62
		ENDIF
		GOTO LOOP_START
	CASE 900
		;変更箇所をキャンセルする
		VARSET 変更フラグ, -5
		VARSET 変更後数値, 0
		GOTO LOOP_START
	CASE 910
		;デフォルトの状態に戻す
		変更フラグ:人種 = 種族_ヒューマン
		変更フラグ:雌雄 = 性別_女性
		変更フラグ:全長 = 体格_小柄
		変更フラグ:胸囲 = バストサイズ_貧乳
		変更フラグ:腹囲 = 体型_普通
		変更フラグ:臀囲 = 下半身_普通
		GOTO LOOP_START
	CASE 999
		;変更点なしなら未実行のまま戻る
		SIF MAXARRAY(変更フラグ) < -4
			RETURN -1
		LOCAL:1 = 0
		FOR LOCAL, 0, VARSIZE("変更フラグ")
			IF TALENT:(変更箇所:LOCAL) == 変更フラグ:LOCAL || 変更フラグ:LOCAL == -5
				LOCAL:1 ++
			ELSE
				SIF LOCAL == 2 && !(GETBIT(TALENT:性別, 0) || (変更フラグ:雌雄 > -1 && GETBIT(変更フラグ:雌雄, 0)))
					LOCAL:1 ++
				SIF LOCAL == 3 && !(GETBIT(TALENT:性別, 1) || (変更フラグ:雌雄 > -1 && GETBIT(変更フラグ:雌雄, 1)))
					LOCAL:1 ++
			ENDIF
		NEXT
		SIF LOCAL:1 == VARSIZE("変更フラグ")
			RETURN -1
	CASEELSE
		GOTO LOOP_START
ENDSELECT

PRINTL
PRINT 「
FONTITALIC
PRINT これでいいか？
FONTSTYLE 0
PRINTL 」

PRINTFORM 性別【%GET_TALENTNAME(2, (変更フラグ:雌雄 > -5 ? 変更フラグ:雌雄 # TALENT:性別), 0,TARGET)%】　
PRINTFORM 種族【%GET_TALENTNAME(変更箇所:人種, (変更フラグ:人種 > -5 ? 変更フラグ:人種 # TALENT:種族), 0,TARGET)%】　
PRINTFORM 体格【%GET_TALENTNAME(変更箇所:全長, (変更フラグ:全長 > -5 ? 変更フラグ:全長 # TALENT:体格), 0,TARGET)%】　
PRINTFORM 体型【%GET_TALENTNAME(変更箇所:腹囲, (変更フラグ:腹囲 > -5 ? 変更フラグ:腹囲 # TALENT:体型), 0,TARGET)%】　
PRINTFORM 下半身【%GET_TALENTNAME(変更箇所:臀囲, (変更フラグ:臀囲 > -5 ? 変更フラグ:臀囲 # TALENT:下半身), 0,TARGET)%】

SIF GETBIT(TALENT:性別, 0) || (変更フラグ:雌雄 > -1 && GETBIT(変更フラグ:雌雄, 0))
	PRINTFORM 乳の大きさ【%GET_TALENTNAME(変更箇所:胸囲, (変更フラグ:胸囲 > -5 ? 変更フラグ:胸囲# TALENT:バストサイズ), 0,TARGET)%】
SIF GETBIT(TALENT:性別, 1) || (変更フラグ:雌雄 > -1 && GETBIT(変更フラグ:雌雄, 1))
	PRINTFORM 男性器の大きさ【%GET_TALENTNAME(変更箇所:竿長, (変更フラグ:竿長 > -5 ? 変更フラグ:竿長# TALENT:男性器サイズ), 0,TARGET)%】
PRINTL

DEBUGPRINTFORM 処理前
FOR LOCAL, 0, VARSIZE("変更フラグ")
	DEBUGPRINTFORM  : %変更フラグ名:LOCAL% = {変更フラグ:LOCAL}
NEXT
DEBUGPRINTL

CALL CHOICE(LOCALS, "はい", "いいえ", "再錬成を中止する")
SIF RESULT == 1
	GOTO LOOP_START

IF RESULT == 2
	PRINT 「
	FONTITALIC
	PRINT そうか…… なら、もう少しこの姿での『可愛い』を追求するか
	FONTSTYLE 0
	PRINTL 」
	RETURN -1
ENDIF

FOR LOCAL, 0, 7
	IF 変更フラグ:LOCAL != -5 && TALENT:(変更箇所:LOCAL) != 変更フラグ:LOCAL
		TALENT:(変更箇所:LOCAL) = 変更フラグ:LOCAL
		IF LOCAL == 人種
			TALENT:ハーヴィン耳 = 0
			TALENT:吸血鬼 = 0
			TALENT:発情期あり = 0
			TALENT:尻尾あり = 0
			TALENT:エルーン耳 = 0
			TALENT:角あり = 0
			CSTR:特殊種族 = 
			IF TALENT:(変更箇所:LOCAL) == 種族_エルーン
				CALL CHOICE("「エルーンかぁ……　ね、尻尾は欲しい？」", "はい", "いいえ")
				TALENT:尻尾あり = !RESULT
				PRINTL 「おっけー♪」
			ELSEIF TALENT:(変更箇所:LOCAL) == 種族_その他
				$INPUT_EXRACE
				LOCAL:1 = LINECOUNT
				CALL CHOICE("「サンプル数の少ない種族ね。どれにしよっか？」", "クリスタリア", "ヴァンパイア", "ワーウルフ", "その他")
				SELECTCASE RESULT
					CASE 0
						;クリスタリア
						CSTR:特殊種族 = クリスタリア
					CASE 1
						;ヴァンパイア
						CSTR:特殊種族 = ヴァンパイア
					CASE 2
						;ワーウルフ
						CSTR:特殊種族 = ワーウルフ
					CASE 3
						;自由選択（名前のみ）
						PRINT 「
						FONTITALIC
						PRINT 言っとくが、さすがのオレ様もレアすぎる種族は殆ど再現出来ないぞ？
						FONTSTYLE 0
						PRINTL 」
						PRINTL 追加種族名を任意で入力してください
						INPUTS
						IF RESULTS != ""
							CSTR:特殊種族 = %RESULTS%
						ELSE
							CLEARLINE LINECOUNT - LOCAL:1
							GOTO INPUT_EXRACE
						ENDIF
				ENDSELECT
				CALL CHOICE(@"「いわゆる亜人の中でも【%CSTR:特殊種族%】に併せる…これでいい？」", "ＯＫ！", "選び直す")
				IF RESULT == 1
					CLEARLINE LINECOUNT - LOCAL:1
					GOTO INPUT_EXRACE
				ENDIF
			ENDIF
			;種族ごとに特徴を設定し直す
			SELECTCASE TALENT:(変更箇所:人種)
				CASE 種族_ヒューマン
					;特徴なし
				CASE 種族_エルーン
					TALENT:エルーン耳 = 1
				CASE 種族_ドラフ
					TALENT:角あり = 1
				CASE 種族_ハーヴィン
					TALENT:ハーヴィン耳 = 1
				CASE 種族_星晶獣
					;特徴なし
				CASE 種族_その他
					SELECTCASE CSTR:特殊種族
						CASE "クリスタリア"
							;クリスタリア
							TALENT:ハーヴィン耳 = 1
						CASE "ヴァンパイア"
							;ヴァンパイア
							TALENT:吸血鬼 = 1
						CASE "ワーウルフ"
							;ワーウルフ
							TALENT:ハーヴィン耳 = 0
							TALENT:発情期あり = 1
							TALENT:尻尾あり = 1
							TALENT:エルーン耳 = 1
						CASEELSE
							;特徴なし
					ENDSELECT
			ENDSELECT
		ENDIF
	ENDIF
NEXT

;素質を元に再計算させるため、一旦0にする
BASE:身長 = 0
BASE:バスト = 0
BASE:ウエスト = 0
BASE:ヒップ = 0
CALL SET_BODYSIZE(TARGET, 0)

IF GROUPMATCH(TALENT:性別, 性別_女性, 性別_ふたなり) && TALENT:種族 == 種族_ヒューマン && TALENT:体格 >= 体格_普通 && TALENT:バストサイズ >= バストサイズ_巨乳
	;肉体条件を満たした場合、大人スタイル
	IF CSTR:スタイル管理文字列 != "[天才美人錬金術師]カリオストロ"
		;スタイル変更していない場合、変更させる
		CSTR:スタイル管理文字列 '= "[天才美人錬金術師]カリオストロ"
		CALL スタイル変更_25(TARGET, "変更後処理", 1)
	ENDIF
	IF GETBIT(口上フラグ_25:1, 0)
		CALL 口上変数初期化
		KSTR:(K++) = IMAGEPATH_%CSTR:画像フォルダ%/短冊_デフォルト
		CALL 口上変数表示(, 0)
		PRINTL 「大人っぽいカリオストロが好みなの？ しょうがないなぁ♪」
	ELSE
		SETBIT 口上フラグ_25:1, 0
		PRINT 「
		FONTITALIC
		PRINT なるほど、つまり……
		FONTSTYLE 0
		PRINT こういうのがお好み、ってわけね
		PRINT_IMG "えっちハート"
		PRINTL 」
		PRINTFORMW そう言うと、%CALLNAME:TARGET%は面影を残したまま、大人の魅力を蓄えた姿へと変貌した
		CALL 口上変数初期化
		KSTR:(K++) = IMAGEPATH_%CSTR:画像フォルダ%/短冊_デフォルト
		CALL 口上変数表示(, 0)
		PRINTL
		PRINTFORML 「どう？　これがカリオストロの新しい側面、年長者としての慈愛と包容力の体現だよ☆
		PRINTFORML 　衣装もキュートかつエレガントを基調として、少しコケティッシュな面も取り入れてみたけど……
		PRINTFORM 　（ニンマリ）%CSTR:二人称%さんも、お気に召したようね
		PRINT_IMG "えっちハート"
		PRINTW 」
		PRINTL
		PRINTFORML %CALLNAME:TARGET%が『[天才美人錬金術師]カリオストロ』にスタイル変更しました。
		PRINTFORML 起床前メニューの　[121]能力表示→[92]戦闘能力表示画面→[115]スタイル変更　からも変更できます（戦闘能力に変化はありません）。
	ENDIF
ELSE
	IF CSTR:スタイル管理文字列 != ""
		;別スタイルの維持条件を満たさない場合、デフォルトスタイルに戻る
		CSTR:スタイル管理文字列 '= ""
		CALL スタイル変更_25(TARGET, "変更後処理", 1)
		CALL 口上変数初期化
		KSTR:(K++) = IMAGEPATH_%CSTR:画像フォルダ%/短冊_デフォルト
		CALL 口上変数表示(, 0)
	ENDIF
	IF TALENT:種族 == 種族_ヒューマン && TALENT:性別 == 性別_女性 && TALENT:体格 == 体格_小柄 && TALENT:バストサイズ == バストサイズ_貧乳 && TALENT:体型 == 体型_普通 && TALENT:下半身 == 下半身_普通
		;身長を元に戻し再計算
		BASE:身長 = 0
		BASE:バスト = 0
		BASE:ウエスト = 0
		BASE:ヒップ = 0
		CALL INIT_BODYSIZE(TARGET)
		PRINTL 「やっぱり、いつものカリオストロが一番っ☆ ってことだよね♪」
	ELSE
		IF CSTR:スタイル管理文字列 != ""
			;別スタイルの維持条件を満たさない場合、デフォルトスタイルに戻る
			CSTR:スタイル管理文字列 '= ""
			CALL スタイル変更_25(TARGET, "変更後処理", 1)
			CALL 口上変数初期化
			KSTR:(K++) = IMAGEPATH_%CSTR:画像フォルダ%/短冊_デフォルト
			CALL 口上変数表示(, 0)
		ENDIF
		PRINT 「
		FONTITALIC
		PRINT 今回はこの姿での実証実験、てことか。いいぜ、付き合ってやるよ
		FONTSTYLE 0
		PRINTL 」
	ENDIF
ENDIF
PRINT カリオストロは現在の肉体を
PRINTFORM 性別【%GET_TALENTNAME(2, TALENT:性別, 0,TARGET)%】　
PRINTFORM 種族【%GET_TALENTNAME(変更箇所:人種, TALENT:種族, 0,TARGET)%\@ TALENT:種族 == 6? (%CSTR:特殊種族%)# \@】　
PRINTFORM 体格【%GET_TALENTNAME(変更箇所:全長, TALENT:体格, 0,TARGET)%】　
PRINTFORM 体型【%GET_TALENTNAME(変更箇所:腹囲, TALENT:体型, 0,TARGET)%】　
PRINTFORML 下半身【%GET_TALENTNAME(変更箇所:臀囲, TALENT:下半身, 0,TARGET)%】
PRINTFORM 身長:%DECIMAL_STRING(BASE:身長, 1), 6, RIGHT%cm/ 
PRINTFORM B:%DECIMAL_STRING(補正込みバストサイズ(TARGET), 1), 6, RIGHT%cm / 
PRINTFORM W:%DECIMAL_STRING(補正込みウエストサイズ(TARGET), 1), 6, RIGHT%cm / 
PRINTFORM H:%DECIMAL_STRING(補正込みヒップサイズ(TARGET), 1), 6, RIGHT%cm
PRINTL
SIF GETBIT(TALENT:性別, 0)
	PRINTFORM 乳の大きさ【%GET_TALENTNAME(変更箇所:胸囲, TALENT:バストサイズ, 0,TARGET)%】
SIF GETBIT(TALENT:性別, 1)
	PRINTFORM 男性器の大きさ【%GET_TALENTNAME(変更箇所:竿長, TALENT:男性器サイズ, 0,TARGET)%】
PRINTW へと再錬成しました


TIME += 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE370_25
;一括管理
SIF GLOBAL_COMABLE(370)
	RETURN RESULT
LOCAL = GETCHARA(25)
SIF TARGET != LOCAL
	RETURN 0
;陥落後にZPショップから外見変更を伴う素質変化を1度は行うまで使用不可
SIF 特定状況口上用フラグ:LOCAL:0 >= 0
	RETURN 0
;妊娠中は赤ちゃんへの影響を鑑みて使用不可
SIF CFLAG:LOCAL:妊娠発覚
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM370_25
LOCAL = GETCHARA(25)



