@固有アビ_25_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果数値２
#DIM 効果成功率
#DIMS アビ名
#DIM Lv閾値 =  1, 13, 20, 33

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = コラプス＋＋
	消費ＭＰ = 45
	効果数値 = 120
	効果数値２ = 30
	効果成功率 = 100
	SIF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
		アビ名 = ニュークリアシンサシス
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = コラプス＋
	消費ＭＰ = 35
	効果数値 = 120
	効果数値２ = 20
	効果成功率 = 100
ELSE
	アビ名 = コラプス
	消費ＭＰ = 20
	効果数値 = 80
	効果数値２ = 10
	効果成功率 = 80
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕{効果数値}％］の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（Ｂ枠・3ターン）の防御力減少効果を与える。成功率{効果成功率}％"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += @"<br>［1］のスロウ効果を与える。成功率{効果成功率}％"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>自属性刻印が3以上の時、1消費して強化効果を［1］消去、2回発動。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
	FOR LOCAL, 0, 1+(刻印現在数確認(属性刻印名())>2)
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:効果量 = 1
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_スロウ
		ENDIF
	NEXT
	IF 刻印現在数確認(属性刻印名()) > 2
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = %属性刻印名()%
		CALL アビテンプレート_刻印減少(1)
	ENDIF
ENDSELECT
RETURN RESULT


@固有アビ_25_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果数値２
#DIMS 枠名 = "ファンタスマゴリア"
#DIM Lv閾値 =  5, 16, 24, 40

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = ファンタズマゴリア＋＋
	消費ＭＰ = 80
	効果数値 = 30
	効果数値２ = 25
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
		アビ名 = ｱﾙｹｲﾅ・ｴﾎﾞﾘｭｰｼｮﾝ
		枠名 = ｱﾙｹｲﾅ・ｴﾎﾞﾘｭｰｼｮﾝ
	ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = ファンタズマゴリア＋
	消費ＭＰ = 70
	効果数値 = 30
	効果数値２ = 15
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = ファンタズマゴリア
	消費ＭＰ = 50
	効果数値 = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（3ターン・消去不可）の%枠名%効果を与える。
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（3ターン）の%枠名%効果を与える。
		ENDIF
		SWAP TARGET, キャラ番号
		詳細文文字列受け渡し変数 += @"<br>［累積1 最大5］の%属性刻印名()%効果を与える。"
		SWAP TARGET, キャラ番号
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（3ターン・消去不可）の%枠名%効果を与える。
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に（3ターン）の%枠名%効果を与える。
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>＞［{効果数値}％］の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［{効果数値}％］の防御力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［{効果数値}％］のDA率上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［{効果数値/2}％］のTA率上昇効果を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			詳細文文字列受け渡し変数 += @"<br>＞［{効果数値２*2}％］のクリティカル率上昇効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>＞［{効果数値２+5}％］のクリティカルダメージ率上昇効果を与える。"
		ENDIF
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += @"<br>（1回）のディスペルガード効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［累積1 最大5］の%属性刻印名()%効果を与える。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = %枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = %枠名%
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:特殊表示オプション = 1
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット


		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 防御力_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = DA率_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 効果数値 / 2
		アビ汎用文字列:バフ・デバフ枠 = TA率_%枠名%
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 効果数値２ * 2
			アビ汎用文字列:バフ・デバフ枠 = CT率_%枠名%
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
			CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 効果数値２ + 5
			アビ汎用文字列:バフ・デバフ枠 = CT倍率_%枠名%
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
			CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ディスペルガード_%枠名%
			アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_付随バフ効果セット(枠名)
		ENDIF

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ対象能力 = %属性刻印名()%
		CALL アビテンプレート_刻印追加(1)
ENDSELECT
RETURN RESULT

@バフ・デバフ特殊表示_ファンタスマゴリア(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("CT率_ファンタスマゴリア", 隊列番号)
PRINTFORML ファンタスマゴリア
PRINTFORML 攻撃力・防御力・DA率・TA率\@ LOCAL > -1 ? ・クリティカル率・クリティカルダメージ # \@が上昇した状態
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

@バフ・デバフ特殊表示_ｱﾙｹｲﾅ・ｴﾎﾞﾘｭｰｼｮﾝ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
LOCAL = バフ番号取得_枠("ディスペルガード_ｱﾙｹｲﾅ・ｴﾎﾞﾘｭｰｼｮﾝ", 隊列番号)
SIF LOCAL > -1
	LOCAL:1 = DT_CELL_GET("戦闘効果データベース", LOCAL, "持続回数")
PRINTFORML アルケイナ・エボリューション
PRINTFORML 攻撃力・防御力・DA率・TA率・クリティカル率・クリティカルダメージが上昇し
PRINTFORML ディスペルを１回無効化する状態
PRINTFORM 持続ターン： {DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}ターン
PRINTFORML 　／　ディスペルガード回数：残り {LOCAL:1}回

@固有アビ_25_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果成功率
#DIMS アビ名
#DIM Lv閾値 = 8, 18, 29, 42

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = リーンフォース＋＋
	効果数値 = 80
	消費ＭＰ = 50
	SIF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
		アビ名 = ネフシュタン
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = リーンフォース＋
	効果数値 = 60
	消費ＭＰ = 35
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = リーンフォース
	効果数値 = 35
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に［回復力✕{効果数値}％］のＨＰ回復を与える。
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			詳細文文字列受け渡し変数 += "<br>［1］のデバフ消去を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			詳細文文字列受け渡し変数 += "<br>［回復力✕10％（上限150）］（独自枠・7ターン）の再生効果を与える。"
			詳細文文字列受け渡し変数 += "<br>［100％］（独自枠・1回）の被ダメージ減少効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_回復処理_MAXでも回復
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
			CALL アビテンプレート_有利効果_デバフ消去
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3 && 陥落チェック(キャラ番号)
			アビ汎用文字列:バフ・デバフ枠 = 再生_ﾈﾌｼｭﾀﾝ
			アビ汎用文字列:バフ・デバフ対象能力 = 再生
			アビ汎用変数:効果割合 = 0
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = MIN(RESULT * 10 / 100, 150)
			アビ汎用変数:持続ターン = 7
			CALL アビテンプレート_有利効果_バフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 被ダメ無効_ﾈﾌｼｭﾀﾝ
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果割合 = 100
			アビ汎用変数:効果量 = 0
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT
RETURN RESULT

@固有アビ_25_11(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

IF !TALENT:キャラ番号:恋慕 || 基礎BATTLE_STATE:キャラ番号:Lv < 43
	アビ名 = 
ELSE
	アビ名 = アルス・グローリア
	IF キャラ隊列検索(GETCHARA(25)) < 0
		消費ＭＰ = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ / 10
	ELSE
		消費ＭＰ = BATTLE_STATE:キャラ隊列検索(GETCHARA(25)):最大ＭＰ / 10
	ENDIF
ENDIF

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:0
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}(最大ＭＰの１０％)
		詳細文文字列受け渡し変数 += "<br>味方全体に（消去不可）のエターナル・ウロボロス効果を与える。"
		詳細文文字列受け渡し変数 += "<br>このアビリティは1戦闘中1回しか使えない。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}(最大ＭＰの１０％)
		詳細文文字列受け渡し変数 += "<br>味方全体に（消去不可）エターナル・ウロボロス効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞［100％］のDA率上昇効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞［30％］のTA率上昇効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞［100％］の奥義ダメージ上昇効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞<b>［35％］の奥義ゲージ上昇効率<font color='#FF3030'>減少</font>効果を与える。</b>"
		詳細文文字列受け渡し変数 += "<br>このアビリティは1戦闘中1回しか使えない。"
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = エターナル・ウロボロス
		アビ汎用文字列:バフ・デバフ対象能力 = エターナル・ウロボロス
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = DA率_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		CALL アビテンプレート_有利効果_付随バフ効果セット("エターナル・ウロボロス")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 30
		アビ汎用文字列:バフ・デバフ枠 = TA率_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		CALL アビテンプレート_有利効果_付随バフ効果セット("エターナル・ウロボロス")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ枠 = 奥義D_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_奥義ダメージ
		CALL アビテンプレート_有利効果_付随バフ効果セット("エターナル・ウロボロス")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 35
		アビ汎用文字列:バフ・デバフ枠 = GE率_ｴﾀｰﾅﾙｳﾛﾎﾞﾛｽ
		アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("エターナル・ウロボロス")

		戦闘中カウント:キャラ隊列検索(GETCHARA(25)):0 = 1
ENDSELECT
RETURN RESULT

@バフ・デバフ特殊表示_エターナル・ウロボロス(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
PRINTL エターナル・ウロボロス
HTML_PRINT "DA率・TA率・奥義ダメージが上昇しているが、<b>奥義ゲージの上昇量が<font color='#FF3030'>減少</font>した状態</b>（永続・消去不可）"
PRINTL

@固有アビ_25_12(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値
#DIM CONST 使用可能ターン = 40

IF !TALENT:キャラ番号:恋慕 || 基礎BATTLE_STATE:キャラ番号:Lv < 43
	アビ名 = 
ELSE
	アビ名 = エンデュミオン
	IF キャラ隊列検索(GETCHARA(25)) < 0
		消費ＭＰ = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ / 2
	ELSE
		消費ＭＰ = BATTLE_STATE:キャラ隊列検索(GETCHARA(25)):最大ＭＰ / 2
	ENDIF
ENDIF
IF キャラ隊列検索(GETCHARA(25)) > -1
	判定値 = 探索中全体カウント:累積ターン数 + 現在ターン数
	FOR LOCAL, 0, 10
		判定値 += 戦闘中カウント:LOCAL:奥義回数 + 探索中カウント:LOCAL:奥義回数
	NEXT
	SIF 探索中カウント:キャラ隊列検索(GETCHARA(25)):2 || 判定値 < 使用可能ターン
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}(最大ＭＰの５０％)　味方全体がウェポンバースト効果を与える。
		詳細文文字列受け渡し変数 += "<br>［全回復］のＭＰ回復を与える。"
		詳細文文字列受け渡し変数 += "<br>［100％］（独自枠）の自動復活効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは探索中の累積ﾀｰﾝ数＋PT全体の奥義発動回数が{使用可能ターン}（現在{判定値}）以上の時に使用できる。"
		詳細文文字列受け渡し変数 += "<br><b>このアビリティは1探索中1回しか使えない</b>。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		CALL アビテンプレート_有利効果_ウェポンバースト

		アビ汎用変数:効果割合 = 10000
		CALL アビテンプレート_ＭＰ回復処理(1)

		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = エンデュミオン
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ対象能力 = 自動復活
		CALL アビテンプレート_有利効果_バフ効果セット

		;アビ効果→MP消費の順に適応されるため、カリオストロのMPが半分になるのを防ぐため消費を0に
		消費MP一時保存 = 0
		探索中カウント:キャラ隊列検索(GETCHARA(25)):2 = 1
ENDSELECT
RETURN RESULT

@固有アビ_25_13(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 判定値

IF !TALENT:キャラ番号:恋慕 || 基礎BATTLE_STATE:キャラ番号:Lv < 43
	アビ名 = 
ELSE
	アビ名 = ロード・トゥ・ジオメトリ
	IF キャラ隊列検索(GETCHARA(25)) < 0
		消費ＭＰ = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ / 20
	ELSE
		消費ＭＰ = BATTLE_STATE:キャラ隊列検索(GETCHARA(25)):最大ＭＰ / 20
	ENDIF
ENDIF

IF キャラ隊列検索(GETCHARA(25)) > -1
	判定値 = ( 戦闘中カウント:キャラ隊列検索(GETCHARA(25)):1 ? MAX(0, 6 + 戦闘中カウント:キャラ隊列検索(GETCHARA(25)):1 - 現在ターン数) # 0)
	SIF 判定値
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}(最大ＭＰの５％)
		詳細文文字列受け渡し変数 += "<br>敵全体に（6ターン・消去不可）のロード・トゥ・ジオメトリ効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは［6ターン］のＣＤが発生する。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"残り{判定値}ターン。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}(最大ＭＰの５％)
		詳細文文字列受け渡し変数 += "<br>敵全体に（6ターン・消去不可）のロード・トゥ・ジオメトリ効果を与える。成功率200％。"
		詳細文文字列受け渡し変数 += "<br>＞［100％］のDA率減少効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞［100％］のTA率減少効果を与える。"
		詳細文文字列受け渡し変数 += "<br>＞［30％］の命中率減少効果を与える。"
		詳細文文字列受け渡し変数 += "<br>［100％］（1ターン・消去不可・弱体耐性貫通）の封印効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは［6ターン］のＣＤが発生する。"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"残り{判定値}ターン。"
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ロード・トゥ・ジオメトリ
		アビ汎用文字列:バフ・デバフ対象能力 = ロード・トゥ・ジオメトリ
		アビ汎用変数:基礎成功確率 = 200
		アビ汎用変数:持続ターン = 6
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = DA率_RtoG
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("ロード・トゥ・ジオメトリ")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = TA率_RtoG
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("ロード・トゥ・ジオメトリ")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 30
		アビ汎用文字列:バフ・デバフ枠 = 命中率_RtoG
		アビ汎用文字列:バフ・デバフ対象能力 = 命中率
		CALL アビテンプレート_不利効果_付随デバフ効果セット("ロード・トゥ・ジオメトリ")

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:弱体耐性貫通オプション = 1
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:効果量 = 0
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ枠 = 封印_ロード・トゥ・ジオメトリ
		アビ汎用文字列:バフ・デバフ対象能力 = 封印
		CALL アビテンプレート_不利効果_デバフ効果セット

		戦闘中カウント:キャラ隊列検索(GETCHARA(25)):1 = 現在ターン数
ENDSELECT
RETURN RESULT

@バフ・デバフ特殊表示_ロード・トゥ・ジオメトリ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCAL
PRINTFORML ロード・トゥ・ジオメトリ
PRINTFORML DA率・TA率・命中率が減少した状態
PRINTFORML 持続ターン： {DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}ターン
