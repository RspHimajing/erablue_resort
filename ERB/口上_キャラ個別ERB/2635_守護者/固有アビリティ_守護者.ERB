
@固有アビ_2635_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 守護者隊列
#DIM クールタイム

守護者隊列 = キャラ隊列検索(GETCHARA(2635))
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 19
	アビ名 = つむじ風＋＋
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 7
	アビ名 = つむじ風＋
ELSE
	アビ名 = つむじ風
ENDIF
消費ＭＰ = 0

IF 守護者隊列 >= 0
	クールタイム = 戦闘中カウント:守護者隊列:1 + 4 - 現在ターン数
	SIF クールタイム > 0
		アビテンプレート用_アビ封印用フラグ = 1
	SIF 戦闘中カウント:守護者隊列:1 == 0
		クールタイム = 0
	SIF クールタイム == 0
		アビテンプレート用_アビ封印用フラグ = 0
ENDIF
;消費はないがターン制限はある。
SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = <br>敵縦列に風属性の［１５０％］ダメージを与える。
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 19
			詳細文文字列受け渡し変数 += @"<br>更に対象に[つむじ風]を付与"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは［3ターン］のＣＤが発生する。"
		SIF クールタイム > 0
			詳細文文字列受け渡し変数 += @"残り{クールタイム}ターン。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_風属性
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 19
			アビ汎用文字列:バフ・デバフ枠 = つむじ風
			アビ汎用変数:特殊表示オプション = 1
			アビ汎用文字列:バフ・デバフ対象能力 = 自傷
			アビ汎用変数:効果割合 = 0
			CALL バフ込み数値算出("攻撃力")
			アビ汎用変数:効果量 = RESULT * 20 / 100
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_不利効果_デバフ効果セット
			戦闘中カウント:戦闘行動キャラ:0 = 現在ターン数
		ENDIF
		アビテンプレート用_アビ封印用フラグ = 1
		戦闘中カウント:守護者隊列:1 = 現在ターン数
ENDSELECT

@固有アビ_2635_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 守護者隊列

守護者隊列 = キャラ隊列検索(GETCHARA(2635))
アビ名 = ハイガード
消費ＭＰ = 0

IF 刻印現在数確認("スタミナ", 守護者隊列) < 3
	アビテンプレート用_アビ封印用フラグ = 1
ELSEIF 刻印現在数確認("ハイガード", 守護者隊列) > 0
	アビテンプレート用_アビ封印用フラグ = 1
ELSE
	アビテンプレート用_アビ封印用フラグ = 0
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費スタミナ：３<br>自身にハイガード状態を付与。
		詳細文文字列受け渡し変数 += @"<br>ハイガード中は毎ターンスタミナを１消費する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = スタミナ
		CALL アビテンプレート_刻印減少(3)
		アビ汎用文字列:バフ・デバフ枠 = ハイガード
		アビ汎用文字列:バフ・デバフ対象能力 = ハイガード
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
			CALL アビテンプレート_刻印追加(1)
		アビ汎用文字列:バフ・デバフ枠 = ハイガード_被ダメージ
		アビ汎用変数:効果割合 = 90
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		CALL アビテンプレート_有利効果_付随バフ効果セット("ハイガード")
		アビ汎用文字列:バフ・デバフ枠 = ハイガード_魔女のブローチ
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		CALL アビテンプレート_有利効果_付随バフ効果セット("ハイガード")
		アビ汎用文字列:バフ・デバフ枠 = ハイガード_歩き移動
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		CALL アビテンプレート_不利効果_付随デバフ効果セット("ハイガード")
ENDSELECT

@固有アビ_2635_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 守護者隊列

守護者隊列 = キャラ隊列検索(GETCHARA(2635))
アビ名 = 突撃
消費ＭＰ = 0

IF 刻印現在数確認("スタミナ", 守護者隊列) < 2
	アビテンプレート用_アビ封印用フラグ = 1
ELSE
	アビテンプレート用_アビ封印用フラグ = 0
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費スタミナ：２<br>敵単体に［攻撃力✕{350}％］の自属性ダメージを与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 350
		CALL アビテンプレート_ダメージ処理_自属性
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = スタミナ
		CALL アビテンプレート_刻印減少(2)
ENDSELECT


@固有戦闘開始時処理_キャラ_2635

;つむじ風CT初期化
#DIM クールタイム
クールタイム = 0
;敵視上昇
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果量 = 300
アビ汎用文字列:バフ・デバフ枠 = 敵視アップ
アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
;魔女のブローチ有効化
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:累積上限_固定値 = 1
アビ汎用文字列:バフ・デバフ対象能力 = 魔女のブローチ
CALL アビテンプレート_刻印追加(1)
CALL アビテンプレート_有利効果_バフ効果セット
;スタミナ最大化
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:累積上限_固定値 = 15
アビ汎用文字列:バフ・デバフ対象能力 = スタミナ
CALL アビテンプレート_刻印追加(15)
CALL アビテンプレート_有利効果_バフ効果セット

	;テスト用のやつ機能してたら変だゾ！
	;CALL アビ汎用変数文字列リセット
	;アビ汎用変数:効果量 = 100
	;CALL アビテンプレート_有利効果_奥義ゲージUP

@固有ターン開始時処理_キャラ_2635
#DIM 守護者隊列
守護者隊列 = キャラ隊列検索(GETCHARA(2635))
;ハイガードでなければスタミナ回復
IF 刻印現在数確認("ハイガード", 守護者隊列) < 1
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:累積上限_固定値 = 15
	アビ汎用文字列:バフ・デバフ対象能力 = スタミナ
	CALL アビテンプレート_刻印追加(1)
ENDIF
;ハイガードであればスタミナ減少
IF 刻印現在数確認("ハイガード", 守護者隊列) > 0
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = スタミナ
	CALL アビテンプレート_刻印減少(1)
ENDIF
;スタミナが足りなければハイガード解除
IF 刻印現在数確認("スタミナ", 守護者隊列) < 1
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = ハイガード
	CALL アビテンプレート_刻印減少(1)
ENDIF

@バフ・デバフ特殊表示_つむじ風(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS
PRINTFORML つむじ風
PRINTFORML 対象に継続ダメージを付与する。
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}

@バフ・デバフ特殊表示_魔女のブローチ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS
PRINTFORML 魔女のブローチ
PRINTFORML 【守護者】アビリティ発動中、被ダメージを一部返す

@バフ・デバフ特殊表示_スタミナ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS
PRINTFORML スタミナ
PRINTFORML 通常時１ターンに１回復。特定アビリティ効果で消費。
PRINTFORML 最大スタック数：１５

@バフ・デバフ特殊表示_ハイガード(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS
PRINTFORML ハイガード
PRINTFORML より強力なガードを行う。
PRINTFORML ハイガード発動時に一定量のスタミナを消費し、ハイガード継続中はスタミナが徐々に減少する。


;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_2635(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 遺物 □：『魔女のブローチ』 □-------------------------------------------------------------------------------
KSTR:(K++) = ハイガード中、被ダメージを一部返す
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　

CALL 口上変数表示(対象キャラ, 1)
