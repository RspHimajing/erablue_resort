
@固有アビ_1369_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

#DIM ループ用
#DIM 威力
#DIM 回数
#DIM 累積自属攻
#DIM 累積与ダメ
#DIMS 自属性

アビテンプレート用_アビ封印用フラグ = 1

;スタイル判定関数を呼び出す
;スタイル変更が無い場合はここは何も触らなくて良い
IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = 本能「イドの解放」＋＋＋
	消費ＭＰ = 40
	威力 = 140
	回数 = 5
	累積自属攻 = 10
	累積与ダメ = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	アビ名 = 本能「イドの解放」＋＋
	消費ＭＰ = 40
	威力 = 120
	回数 = 4
	累積自属攻 = 8
	累積与ダメ = 75
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = 本能「イドの解放」＋
	消費ＭＰ = 30
	威力 = 100
	回数 = 3
	累積自属攻 = 5
	累積与ダメ = 50
ELSE
	アビ名 = 本能「イドの解放」
	消費ＭＰ = 20
	威力 = 80
	回数 = 2
	累積自属攻 = 3
	累積与ダメ = 25
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力}％］の自属性ダメージを{回数}回与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に［累積{累積自属攻}最大50］の自属性強化効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に［累積{累積与ダメ}最大500］の与ダメージ上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>◆通常攻撃後に自動発動"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;ここにアビリティ効果を記述する。
		;基本的にアビテンプレートの関数を使用すること
		;テンプレート使用の例は下部分を参照
		
		;セーブデータ互換処理で戦闘時以外にアビ名取られることがあるので、場所を移動
		自属性 '= 属性数値文字列変換(BATTLE_STATE:キャラ隊列検索(キャラ番号):属性)
		
		;ダメージ
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 威力
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			アビ汎用変数:かばう無視オプション = 3
		FOR ループ用, 0, 回数
			CALL アビテンプレート_ダメージ処理_自属性
		NEXT
		
		;累積自属性強化バフ
		;なんか怪しい。○属性攻撃力上昇がしたい時って、○強化より属性与ダメージ_○の方がいいのか…？
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = %自属性%強化累積
		アビ汎用文字列:バフ・デバフ対象能力 = %自属性%強化
		アビ汎用変数:効果量 = 累積自属攻
		アビ汎用変数:累積上限_固定値 = 50
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		;累積与ダメージ上昇バフ
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 与ダメージ累積
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:効果量 = 累積与ダメ
		アビ汎用変数:累積上限_固定値 = 500
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_累積バフ効果セット
ENDSELECT

RETURN RESULT



@固有アビ_1369_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

#DIM 威力
#DIMS 範囲

アビテンプレート用_アビ封印用フラグ = 1

;スタイル判定関数を呼び出す
;スタイル変更が無い場合はここは何も触らなくて良い
IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
 	アビ名 = 抑制「スーパーエゴ」＋＋＋
 	消費ＭＰ = 200
 	威力 = 300
 	範囲 = 敵全体
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
 	アビ名 = 抑制「スーパーエゴ」＋＋
 	消費ＭＰ = 100
 	威力 = 300
 	範囲 = 敵単体
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
 	アビ名 = 抑制「スーパーエゴ」＋
 	消費ＭＰ = 50
 	威力 = 200
 	範囲 = 敵単体
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
 	アビ名 = 抑制「スーパーエゴ」
 	消費ＭＰ = 30
 	威力 = 150
 	範囲 = 敵単体
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>%範囲%に［攻撃力✕{威力}％］の自属性ダメージを与える。"
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += @"<br>対象に［25％］（Ｂ枠・3ターン）の攻撃力減少・防御力減少効果を与える。基礎成功率80％。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
			IF OPTION変数:ツールチップ切り替え == 0 && ダンジョン表示モード == "戦闘画面"
				詳細文文字列受け渡し変数 += @"<br>対象に（2ターン）の<font color='#%カラーパレット_HTML("紫")%'><nonbutton title='<br>行動することができなくなります。<br>'>抑制</nonbutton></font>効果を与える。基礎成功率100％。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>対象に（2ターン）の抑制効果を与える。基礎成功率100％。"
			ENDIF
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>◆被ダメージ時に自動発動"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;ダメージ
		CALL アビ汎用変数文字列リセット
		IF 範囲 == "敵単体"
			CALL アビ対象選択テンプレート_敵単体
		ELSE
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ENDIF
		アビ汎用変数:効果割合 = 威力
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			アビ汎用変数:かばう無視オプション = 3
		CALL アビテンプレート_ダメージ処理_自属性
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			;攻撃力減少デバフ
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(アビテンプレート用_対象保存:0)
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 25
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			;防御力減少デバフ
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(アビテンプレート用_対象保存:0)
			アビ汎用文字列:バフ・デバフ枠 = 防御力B
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 25
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
			;抑制デバフ
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(アビテンプレート用_対象保存:0)
			アビ汎用文字列:バフ・デバフ枠 = 抑制
			アビ汎用文字列:バフ・デバフ対象能力 = 抑制
			アビ汎用変数:持続ターン = 2
			アビ汎用変数:基礎成功確率 = 100
			アビ汎用変数:特殊表示オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ名 = 抑制
			アビ汎用文字列:バフ・デバフ枠 = 抑制_封印
			アビ汎用文字列:バフ・デバフ対象能力 = 封印
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_不利効果_付随デバフ効果セット("抑制")
		ENDIF
ENDSELECT

RETURN RESULT



@固有アビ_1369_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

#DIM ループ用
#DIM 威力
#DIM 回数
#DIM ディスペル回数

アビテンプレート用_アビ封印用フラグ = 1

;スタイル判定関数を呼び出す
;スタイル変更が無い場合はここは何も触らなくて良い
IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 75
 	アビ名 = 夢符「ご先祖様が見ているぞ」＋＋
 	消費ＭＰ = 100
 	威力 = 100
 	回数 = 10
 	ディスペル回数 = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
 	アビ名 = 夢符「ご先祖様が見ているぞ」＋
 	消費ＭＰ = 50
 	威力 = 80
 	回数 = 8
 	ディスペル回数 = 1
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
 	アビ名 = 夢符「ご先祖様が見ているぞ」
 	消費ＭＰ = 30
 	威力 = 60
 	回数 = 6
 	ディスペル回数 = 0
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>ターゲットに関わらず［攻撃力✕{威力}％］の自属性ダメージを{回数}回与える。"
		SIF ディスペル回数 > 0
			詳細文文字列受け渡し変数 += @"<br>敵全体のバフ効果を{ディスペル回数}つ消去する。"
		詳細文文字列受け渡し変数 += @"<br>◆敵が特殊技使用時に自動発動"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;ダメージ
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵ランダムＸ体(回数)
		アビ汎用変数:効果割合 = 威力
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			アビ汎用変数:かばう無視オプション = 3
		CALL アビテンプレート_ダメージ処理_自属性
		
		;ディスペル
		IF ディスペル回数 > 0
			;ディスペル
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			FOR ループ用, 0, ディスペル回数
				CALL アビテンプレート_不利効果_バフ消去()
			NEXT
		ENDIF
ENDSELECT

RETURN RESULT



@固有アビ_1369_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

#DIMS 効果時間

アビテンプレート用_アビ封印用フラグ = 1

;スタイル判定関数を呼び出す
;スタイル変更が無い場合はここは何も触らなくて良い
IF スタイル判定関数(キャラ番号, "デフォルト") == 0
	アビ名 = 
	RETURN 0
ENDIF

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
 	アビ名 = 「嫌われ者のフィロソフィ」＋＋
 	消費ＭＰ = 200
 	効果時間 = 1ターン
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
 	アビ名 = 「嫌われ者のフィロソフィ」＋
 	消費ＭＰ = 200
 	効果時間 = このターン中、1度だけ
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
 	アビ名 = 「嫌われ者のフィロソフィ」
 	消費ＭＰ = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身の奥義ゲージを［100％］にする。"
		SIF 効果時間 != ""
			詳細文文字列受け渡し変数 += @"<br>自身に［250％］（独自枠・%効果時間%）の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>◆ターン開始時に敵が大技待機中またはCTMAXの場合に自動発動"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;ポンバ
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		CALL アビテンプレート_有利効果_ウェポンバースト
		
		IF 効果時間 != ""
			;いつアサ？
			;与ダメ上昇とどっちがいいのかしら？とりあえず別枠攻撃
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = 嫌われ者のﾌｨﾛｿﾌｨ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 250
			アビ汎用変数:持続ターン = 1
			SIF 効果時間 != "1ターン"
				アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

RETURN RESULT



@固有通常攻撃後処理_キャラ_1369(全滅フラグ, 連撃数)
#DIM 全滅フラグ
#DIM 連撃数

#DIM キャラ番号
#DIMS アビ名
#DIM ループ用
#DIM 威力
#DIM 回数
#DIM 累積自属攻
#DIM 累積与ダメ
#DIMS 自属性

SIF 全滅フラグ
	RETURN 0

キャラ番号 = GETCHARA(1369)
自属性 '= 属性数値文字列変換(BATTLE_STATE:キャラ隊列検索(キャラ番号):属性)

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = 本能「イドの解放」＋＋＋
	威力 = 140
	回数 = 5
	累積自属攻 = 10
	累積与ダメ = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	アビ名 = 本能「イドの解放」＋＋
	威力 = 120
	回数 = 4
	累積自属攻 = 8
	累積与ダメ = 75
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = 本能「イドの解放」＋
	威力 = 100
	回数 = 3
	累積自属攻 = 5
	累積与ダメ = 50
ELSE
	アビ名 = 本能「イドの解放」
	威力 = 80
	回数 = 2
	累積自属攻 = 3
	累積与ダメ = 25
ENDIF

;メッセージ
アビテンプレート用_アビ名 = %アビ名%
アビテンプレート用_アビ名表示済フラグ = 0

;ダメージ
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_指定(アビテンプレート用_対象保存:0)
アビ汎用変数:効果割合 = 威力
SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ汎用変数:かばう無視オプション = 3
FOR ループ用, 0, 回数
	CALL アビテンプレート_ダメージ処理_自属性
NEXT

;累積自属性強化バフ
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = %自属性%強化累積
アビ汎用文字列:バフ・デバフ対象能力 = %自属性%強化
アビ汎用変数:効果量 = 累積自属攻
アビ汎用変数:累積上限_固定値 = 50
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_累積バフ効果セット

;累積与ダメージ上昇バフ
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = 与ダメージ累積
アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
アビ汎用変数:効果量 = 累積与ダメ
アビ汎用変数:累積上限_固定値 = 500
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_累積バフ効果セット

RETURN RESULT



@固有被ダメ時処理_キャラ_1369(全滅フラグ)
#DIM 全滅フラグ

#DIM キャラ番号
#DIMS アビ名
#DIM 威力
#DIMS 範囲

SIF 全滅フラグ
	RETURN 0

キャラ番号 = GETCHARA(1369)

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
 	アビ名 = 抑制「スーパーエゴ」＋＋＋
 	威力 = 300
 	範囲 = 敵全体
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
 	アビ名 = 抑制「スーパーエゴ」＋＋
 	威力 = 300
 	範囲 = 敵単体
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
 	アビ名 = 抑制「スーパーエゴ」＋
 	威力 = 200
 	範囲 = 敵単体
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
 	アビ名 = 抑制「スーパーエゴ」
 	威力 = 150
 	範囲 = 敵単体
ELSE
	RETURN 0
ENDIF

;メッセージ
アビテンプレート用_アビ名 = %アビ名%
アビテンプレート用_アビ名表示済フラグ = 0

;ダメージ
CALL アビ汎用変数文字列リセット
IF 範囲 == "敵単体"
	CALL アビ対象選択テンプレート_指定(戦闘行動対象)
ELSE
	CALL アビ対象選択テンプレート_敵全体_生者のみ
ENDIF
アビ汎用変数:効果割合 = 威力
SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ汎用変数:かばう無視オプション = 3
CALL アビテンプレート_ダメージ処理_自属性

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	;攻撃力減少デバフ
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(アビテンプレート用_対象保存:0)
	アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:効果割合 = 25
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:基礎成功確率 = 80
	CALL アビテンプレート_不利効果_デバフ効果セット
	
	;防御力減少デバフ
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(アビテンプレート用_対象保存:0)
	アビ汎用文字列:バフ・デバフ枠 = 防御力B
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:効果割合 = 25
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:基礎成功確率 = 80
	CALL アビテンプレート_不利効果_デバフ効果セット
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	;抑制デバフ
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_指定(アビテンプレート用_対象保存:0)
	アビ汎用文字列:バフ・デバフ枠 = 抑制
	アビ汎用文字列:バフ・デバフ対象能力 = 抑制
	アビ汎用変数:持続ターン = 2
	アビ汎用変数:基礎成功確率 = 100
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ名 = 抑制
	アビ汎用文字列:バフ・デバフ枠 = 抑制_封印
	アビ汎用文字列:バフ・デバフ対象能力 = 封印
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_不利効果_付随デバフ効果セット("抑制")
ENDIF

RETURN RESULT



@固有行動直後処理_バフ・デバフ_ご先祖様が見ているぞ(効果番号)
#DIM 効果番号

#DIM ループ用
#DIM キャラ番号
#DIM 隊列番号
#DIMS アビ名
#DIM 威力
#DIM 回数
#DIM ディスペル回数
#DIM 戦闘行動キャラ退避
#DIMS アビ名退避

キャラ番号 = GETCHARA(1369)
隊列番号 = キャラ隊列検索(キャラ番号)

SIF BATTLE_STATE:隊列番号:ＨＰ == 0 || アビテンプレート用_アビ名 == "攻撃" || アビテンプレート用_アビ名 == ""
	RETURN 0

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 75
 	アビ名 = 夢符「ご先祖様が見ているぞ」＋＋
 	威力 = 100
 	回数 = 10
 	ディスペル回数 = 2
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
 	アビ名 = 夢符「ご先祖様が見ているぞ」＋
 	威力 = 80
 	回数 = 8
 	ディスペル回数 = 1
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
 	アビ名 = 夢符「ご先祖様が見ているぞ」
 	威力 = 60
 	回数 = 6
 	ディスペル回数 = 0
ELSE
	RETURN 0
ENDIF

;メッセージ
戦闘行動キャラ退避 = 戦闘行動キャラ
戦闘行動キャラ = 隊列番号
アビ名退避 '= アビテンプレート用_アビ名
アビテンプレート用_アビ名 = %アビ名%

;ダメージ
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵ランダムＸ体(回数)
アビ汎用変数:効果割合 = 威力
SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ汎用変数:かばう無視オプション = 3
CALLFORM アビテンプレート_ダメージ処理_自属性

;ディスペル
IF ディスペル回数 > 0
	;ディスペル
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	FOR ループ用, 0, ディスペル回数
		CALL アビテンプレート_不利効果_バフ消去()
	NEXT
ENDIF

戦闘行動キャラ = 戦闘行動キャラ退避
アビテンプレート用_アビ名 '= アビ名退避

RETURN RESULT



@固有ターン開始時処理_キャラ_1369
#DIM ループ用
#DIM 実行フラグ

#DIM キャラ番号
#DIMS アビ名
#DIMS 効果時間

キャラ番号 = GETCHARA(1369)

;このタイミングで、ご先祖様が見ているぞバフを敵全体に付与
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵全体_生者のみ
アビ汎用文字列:バフ・デバフ枠 = ご先祖様が見ているぞ
アビ汎用文字列:バフ・デバフ対象能力 = ご先祖様が見ているぞ
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
 	アビ名 = 「嫌われ者のフィロソフィ」＋＋
 	効果時間 = 1ターン
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 60
 	アビ名 = 「嫌われ者のフィロソフィ」＋
 	効果時間 = このターン中、1度だけ
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
 	アビ名 = 「嫌われ者のフィロソフィ」
ELSE
	RETURN 0
ENDIF

実行フラグ = バフ所持者検索("大技待機")
IF !実行フラグ
	FOR ループ用, 0, 10
		IF 敵BATTLE_STATE:ループ用:ＨＰ > 0 && 敵BATTLE_STATE:ループ用:最大チャージターン > 0 && 敵BATTLE_STATE:ループ用:チャージターン == 敵BATTLE_STATE:ループ用:最大チャージターン
			実行フラグ = 1
			BREAK
		ENDIF
	NEXT
ENDIF

SIF !実行フラグ
	RETURN 0

;メッセージ
アビテンプレート用_アビ名 = %アビ名%
アビテンプレート用_アビ名表示済フラグ = 0

;ポンバ
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
CALL アビテンプレート_有利効果_ウェポンバースト

IF 効果時間 != ""
	;いつアサ
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 嫌われ者のﾌｨﾛｿﾌｨ
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:効果割合 = 250
	アビ汎用変数:持続ターン = 1
	SIF 効果時間 != "1ターン"
		アビ汎用変数:持続回数 = 1
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF

RETURN RESULT



@バフ・デバフ特殊表示_抑制(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

#DIM キャラ番号
#DIMS アビ名

キャラ番号 = GETCHARA(1369)

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
 	アビ名 = 抑制「スーパーエゴ」＋＋＋
ELSE
 	アビ名 = 抑制「スーパーエゴ」＋＋
ENDIF

PRINTFORML 抑制デバフ：%アビ名%
PRINTFORML 行動することができなくなります。
PRINTFORML 効果枠：抑制                効果割合： 100％　　
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}　　
RETURN RESULT



;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_1369(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

KSTR:(K++) = □ 特性：『無意識を操る程度の能力』 □---------------------------------------------------
KSTR:(K++) = 　固有アビリティを使用できないが条件を満たすことで自動発動
KSTR:(K++) = 　攻撃力と防御力が低いが潜伏効果/命中率UP/回避率UP/クリティカル率UP
KSTR:(K++) = 　

IF 基礎BATTLE_STATE:対象キャラ:Lv >= 50
	KSTR:(K++) = □ 特性：『今、貴方の後ろに居るの』 □-----------------------------------------------------
	KSTR:(K++) = 　クリティカル発生時に与ダメージが上昇
	KSTR:(K++) = 　敵のかばうに関わらず奥義と固有アビリティが命中する
	KSTR:(K++) = 　
ENDIF

CALL 口上変数表示(対象キャラ, 1)
RETURN RESULT
