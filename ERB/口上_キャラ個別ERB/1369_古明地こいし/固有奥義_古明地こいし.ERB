
@固有奥義_1369(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名

#DIMS 文字列分割, 2
#DIM 奥義実威力
#DIM 裂傷効果量

;スタイルによる奥義の変更部分、スタイル変化_キャラ名.ERBを参照
; IF CSTR:キャラ番号:スタイル管理文字列 == "任意のスタイル名"
; 	CALL 任意のスタイル名固有奥義(ARGS, キャラ番号)
; 	RETURN RESULT
; ENDIF

; IF レベルやイベント完了などで強化される場合、奥義名や奥義威力などを分岐させる
; 	奥義名 = 羊瀾怒濤
; 	奥義威力 = 550
; ELSE
;	奥義名 = （任意の奥義名）
;	奥義威力 = （任意の奥義威力数値。汎用奥義は300、固有奥義は400～500を基準とする）
; ENDIF

奥義威力 = 0
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 70
	奥義名 = 「ブランブリーローズガーデン」
	奥義実威力 = 600
	裂傷効果量 = 20
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
	奥義名 = 「ローズ地獄」
	奥義実威力 = 500
	裂傷効果量 = 15
ELSE
	奥義名 = 「サブタレイニアンローズ」
	奥義実威力 = 400
	裂傷効果量 = 10
ENDIF

;奥義威力を0にした場合、通常の奥義処理は行われず奥義追加効果のみが行われる。
;全体攻撃にしたい時や、攻撃しない奥義を作る場合は0に設定のこと


;下の４つの文字列に文章を入れると、対象選択時の文章を変化させることが出来る。
;これらを使わない場合は「誰を攻撃しますか？」と表示される。
;アビ汎用文字列:選択時メッセージ１ = 
;アビ汎用文字列:選択時メッセージ２ = 
;アビ汎用文字列:選択時メッセージ３ = 
;アビ汎用文字列:選択時メッセージ４ = 

;下の４つの文字列に文章を入れると、奥義使用時の文章を変化させることが出来る。
;これらを使わない場合は「（キャラ名）の奥義：（奥義名）！」と表示される。
;アビ汎用文字列:実行時メッセージ１ = 
;アビ汎用文字列:実行時メッセージ２ = 
;アビ汎用文字列:実行時メッセージ３ = 
;アビ汎用文字列:実行時メッセージ４ = 

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義実威力}％　全体攻撃
		IF OPTION変数:ツールチップ切り替え == 0 && ダンジョン表示モード == "戦闘画面"
			詳細文文字列受け渡し変数 += @"<br>対象に［自身のレベル✕{裂傷効果量}］（3ターン）の<font color='#%カラーパレット_HTML("紫")%'><nonbutton title='<br>行動終了時、ダメージを受けます。<br>'>裂傷</nonbutton></font>効果を与える。基礎成功率100％。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>対象に［自身のレベル✕{裂傷効果量}］（3ターン）の裂傷効果を与える。基礎成功率100％。"
		ENDIF
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 70
			詳細文文字列受け渡し変数 += @"<br>◆2回発動"
		
		;耳飾り補正部分
		IF 装備ステータス補正保存:キャラ番号:装備耳飾り番号 > 0
			;一旦受け渡し変数を退避
			LOCALS = %詳細文文字列受け渡し変数%
			;効果文章を呼び出してから必要ない部分をカット
			CALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:キャラ番号:装備耳飾り番号, "耳飾り名", 1)%(装備ステータス補正保存:キャラ番号:装備耳飾り番号)
			SPLIT 詳細文文字列受け渡し変数, "【奥義補正】<br>", 文字列分割
			詳細文文字列受け渡し変数 = %LOCALS%<br>【指輪補正】　%文字列分割:1%
		ENDIF
		
		RETURN 奥義威力
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる
		
		;処理の都合上、ここから奥義ダメージまでにリセットを噛ませてないため
		;必要な場合を除きここでリセット
		CALL アビ汎用変数文字列リセット
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;基本的にアビテンプレートの関数を使用すること
		;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載
		
		;ダメージ
		アビ汎用文字列:ダメージ種類 = 奥義ダメージ
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 奥義実威力
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			アビ汎用変数:かばう無視オプション = 3
		CALL アビテンプレート_ダメージ処理_自属性
		
		;裂傷デバフ
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 裂傷
		アビ汎用文字列:バフ・デバフ対象能力 = 裂傷
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 裂傷_自傷
		アビ汎用文字列:バフ・デバフ対象能力 = 自傷
		アビ汎用変数:効果量 = 基礎BATTLE_STATE:キャラ番号:Lv * 裂傷効果量
		CALL アビテンプレート_不利効果_付随デバフ効果セット("裂傷")
ENDSELECT

RETURN RESULT



@固有戦闘開始時処理_キャラ_1369
#DIM キャラ番号

キャラ番号 = GETCHARA(1369)

;このタイミングで、ご先祖様が見ているぞバフを敵全体に付与
;戦闘開始時でやらないと1ターン目に来ない
SIF 基礎BATTLE_STATE:キャラ番号:Lv < 15
	RETURN 0

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵全体_生者のみ
アビ汎用文字列:バフ・デバフ枠 = ご先祖様が見ているぞ
アビ汎用文字列:バフ・デバフ対象能力 = ご先祖様が見ているぞ
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット


;奥義が＋＋になったら2回発動
SIF 基礎BATTLE_STATE:キャラ番号:Lv < 70
	RETURN 0

;奥義再発動バフ(非表示)
CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:バフ・デバフ枠 = ﾌﾞﾗﾝﾌﾞﾘｰﾛｰｽﾞｶﾞｰﾃﾞﾝ
アビ汎用文字列:バフ・デバフ対象能力 = 奥義再発動
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_バフ効果セット

RETURN RESULT



@バフ・デバフ特殊表示_裂傷(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数

#DIM キャラ番号
#DIMS 奥義名
#DIM 裂傷効果量

キャラ番号 = GETCHARA(1369)

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 70
	奥義名 = 「ブランブリーローズガーデン」
	裂傷効果量 = 20
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
	奥義名 = 「ローズ地獄」
	裂傷効果量 = 15
ELSE
	奥義名 = 「サブタレイニアンローズ」
	裂傷効果量 = 10
ENDIF

PRINTFORML 裂傷デバフ：奥義：%奥義名%
PRINTFORML 行動終了時、ダメージを受けます。
PRINTFORML 効果枠：裂傷                効果量：{基礎BATTLE_STATE:キャラ番号:Lv * 裂傷効果量, 4}　　
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}　　

RETURN RESULT
