@固有奥義_31(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 奥義変化フラグ

;リミVerの処理はリミのファイルにまとめるため、別関数を呼ぶ
IF CSTR:キャラ番号:スタイル管理文字列 == "[揺らぎの想姫]ナルメア"
	CALL 揺らぎの想姫固有奥義(ARGS, キャラ番号)
	RETURN RESULT
ENDIF

LOCAL = キャラ隊列検索(キャラ番号)
IF LOCAL <= 0
	奥義変化フラグ = 0
ELSEIF 戦闘中カウント:LOCAL:0 == 0
	奥義変化フラグ = 0
ELSE
	奥義変化フラグ = 1
ENDIF


IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	IF 奥義変化フラグ == 0
		奥義名 = 胡蝶刃改・源氏舞
	ELSE
		奥義名 = 胡蝶刃改・神楽舞
	ENDIF
	奥義威力 = 500
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	IF 奥義変化フラグ == 0
		奥義名 = 胡蝶刃・源氏舞＋
	ELSE
		奥義名 = 胡蝶刃・神楽舞＋
	ENDIF
	奥義威力 = 450
ELSE
	IF 奥義変化フラグ == 0
		奥義名 = 胡蝶刃・源氏舞
	ELSE
		奥義名 = 胡蝶刃・神楽舞
	ENDIF
	奥義威力 = 400
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％
		IF 奥義変化フラグ == 0
			詳細文文字列受け渡し変数 += @"<br>自身の攻撃力を［15％/累積/最大50％/4T］上昇させる。"
			SIF 奥義名 == "胡蝶刃改・源氏舞"
				詳細文文字列受け渡し変数 += @"<br>自身の攻撃力(B枠)を［30％/3T］上昇させる。"
		ELSE
			詳細文文字列受け渡し変数 += @"<br>自身の胡蝶刃による防御力減少を解除する。"
			SIF 奥義名 == "胡蝶刃改・神楽舞"
				詳細文文字列受け渡し変数 += @"<br>自身の回避率(A枠)を［100％/2回］上昇させる。"
		ENDIF
		RETURN 奥義威力
	CASE "奥義前効果"
	
	CASE "奥義追加効果"
		CALL アビ対象選択テンプレート_自己のみ
		IF 戦闘中カウント:戦闘行動キャラ:0 == 0
			アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 15
			アビ汎用変数:累積上限_割合 = 50
			アビ汎用変数:持続ターン = 4
			CALL アビテンプレート_有利効果_累積バフ効果セット
			IF 奥義名 == "胡蝶刃改・源氏舞"
				アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
				アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
				アビ汎用変数:効果割合 = 30
				アビ汎用変数:持続ターン = 3
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
		ELSE
			CALL 戦闘効果削除(デバフ番号取得_枠("胡蝶刃_防御力", 戦闘行動キャラ))
			IF 奥義名 == "胡蝶刃改・神楽舞"
				アビ汎用文字列:バフ・デバフ枠 = 回避率A
				アビ汎用文字列:バフ・デバフ対象能力 = 回避率
				アビ汎用変数:効果量 = 100
				アビ汎用変数:持続回数 = 2
			ENDIF
		ENDIF
ENDSELECT




@揺らぎの想姫固有奥義(ARGS, キャラ番号)
;@固有奥義31_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	奥義名 = 胡蝶刃・屠龍＋
	奥義威力 = 800
ELSE
	奥義名 = 胡蝶刃・屠龍
	奥義威力 = 700
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％
		詳細文文字列受け渡し変数 += @"<br>自分のアビリティ再使用間隔を2ターン短縮する。"
		詳細文文字列受け渡し変数 += @"<br>次のターン進行時に攻撃行動を2回行う。"
		RETURN 奥義威力
	CASE "奥義前効果"
	
	CASE "奥義追加効果"
		SIF バフ番号取得_枠("再行動" , 戦闘行動キャラ) < 0
			BATTLE_STATE:戦闘行動キャラ:行動済みフラグ = 1
		戦闘中カウント:キャラ隊列検索(キャラ番号):0 -= 2
		戦闘中カウント:キャラ隊列検索(キャラ番号):1 -= 2
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 再行動
		アビ汎用文字列:バフ・デバフ対象能力 = 再行動
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


