;特定のキャラをメインにするイベントカテゴリ
;未神宮の若夫婦を任意のイベント名に変えて使うこと、他との重複不可

@イベントカテゴリ登録_キャライベント_未神宮の若夫婦
;汎用結婚イベントを発生させず、代わりにこのイベントを発生させる時に使用する。
;1を任意のキャラのNOに変更すること

RESULTS = 結婚イベント1_


@イベント発生条件_キャライベント_未神宮の若夫婦
#DIM 対象キャラ

対象キャラ = GETCHARA(1) ;任意のキャラのNOを入れる

;開放条件を満たしたら1を返すこと
;結婚周りのフラグチェック、理由がないなら残しておくことを推奨
SIF CFLAG:対象キャラ:結婚イベント解放 < 1
	RETURN 0
SIF CFLAG:対象キャラ:結婚イベント禁止 > 0
	RETURN 0

RETURN 1

@イベント消滅条件_キャライベント_未神宮の若夫婦
#DIM 対象キャラ

対象キャラ = GETCHARA(1) ;任意のキャラのNOを入れる

;この条件を満たした時、イベントは発生してない扱いとなる
;発生条件＞消滅条件で見るので、発生と消滅を両方満たしている時は「発生した！」というメッセージが出るが発生していない扱いになる
;そのため条件には注意すること

;消滅条件を満たしたら1を返す
; IF 消滅条件式
; 	RETURN 1
; ENDIF
; RETURN 0

;結婚相手は滞在していること
SIF CFLAG:対象キャラ:滞在期間 < 1
	RETURN 1
;結婚周りのフラグチェック、理由がないなら残しておくことを推奨
SIF CFLAG:対象キャラ:結婚イベント解放 < 1
	RETURN 1
SIF CFLAG:対象キャラ:結婚イベント禁止 > 0
	RETURN 1

RETURN 0

;例えば、「発生条件を満たしている時のみ出現させたい」のなら以下のようになる
; CALL イベント発生条件_キャライベント_未神宮の若夫婦
; IF RESULT
; 	;発生条件OKなので0を返す
; 	RETURN 0
; ELSE
; 	;発生条件アウトなので1を返す
; 	RETURN 1
; ENDIF

@イベント関数_キャライベント_未神宮の若夫婦(ARG)
#DIM 対象キャラ

対象キャラ = GETCHARA(1) ;任意のキャラのNOを入れる


DRAWLINE
PRINTL ［未神宮の若夫婦］
DRAWLINE
PRINTW
PRINTL 老宮司：
PRINTFORMW 「掛けまくも畏き　未神宮の大前に────」
PRINTL
PRINTFORMW 未神宮の本社の中、厳かな雰囲気の中で宮司が祝詞を読み上げる。
PRINTFORMW %CALLNAME:MASTER%とアニラは顔を軽く伏して、神に捧げる御言葉を聞いていた。
PRINTL
IF TALENT:MASTER:性別 == 2
	PRINTFORMW %CALLNAME:MASTER%は急遽決めた家紋の入った袴を履き、アニラは白無垢と角隠しを纏っている。
ELSE
	PRINTFORMW %CALLNAME:MASTER%とアニラは共に、白無垢と角隠しを纏っている。
ENDIF
PRINTFORMW 目線だけでアニラの方に目をやると、同じくこちらを見ていたアニラと目が合った。
PRINTFORMW 化粧の乗った美しいアニラの唇が薄く微笑み、%CALLNAME:MASTER%は幸せな心地に包まれる。
PRINTL
PRINTL 老宮司：
PRINTFORMW 「子孫の八十続　栂の木の弥継継に立ち栄えしめ給へと　恐み恐みも白す────」
PRINTL
PRINTFORMW 祝詞の奏上が終わり、三献の儀に移る。
PRINTFORMW 平たい盃に神酒が注がれ、%CALLNAME:MASTER%とアニラは盃を交わすことで夫婦の契りを結んだ。
PRINTL
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …
PRINTL
PRINTFORMW 神前式が終わった未神宮では、大きな祭りが開かれていた。
PRINTFORMW 当代の十二神将であるアニラの婚姻という慶事を祝うため、近隣の島からも多くの参拝客が訪れている。
PRINTFORMW 人の集まるところ商人あり。と多くの出店も開かれており、酒や軽食が安価で振る舞われている。
PRINTFORMW 十二年に一度の歳神の元旦よりも盛り上がっているのではないか、と思うほどの盛り上がりであった。
PRINTL
PRINTFORMW %CALLNAME:MASTER%は未神宮の中に設けられた部屋にて、遠くに見える喧騒を眺めている。
PRINTFORMW 隣にいるアニラも、どこか夢見心地のような顔で同じく祭りの風景を見つめていた。
PRINTL
PRINTL アニラ：
PRINTFORMW 「……くふふ、まさか我がこんなに早く結婚するとはのう」
PRINTL
PRINTFORMW 不満だったか？　と%CALLNAME:MASTER%が尋ねる。
PRINTL
PRINTL アニラ：
PRINTFORMW 「いーや、逆じゃ。あまりにあまりに幸せすぎて、まるで夢の中におるようでな。
PRINTFORMW 　%CSTR:対象キャラ:二人称%をしっかりと掴んでおらぬと、夢から覚めてしまうやもしれぬ」
PRINTL
PRINTFORMW ぎゅぅ、とやや強めにアニラが%CALLNAME:MASTER%を抱きしめる。
PRINTFORMW %CALLNAME:MASTER%もそれに答えるようにアニラを抱きしめると、腕の中でアニラがくふふと笑った。
PRINTL
PRINTL アニラ：
PRINTFORMW 「おっと……そういえば、結婚したのじゃからいつまでも「%CSTR:対象キャラ:二人称%」と呼ぶのも無粋じゃの。
PRINTFORMW 　さて、%CSTR:対象キャラ:二人称%は我になんと呼んで欲しいかの？」
PRINTL
PRINTBUTTON "[0] 「お前様」と呼んでもらう", 0
PRINTL
PRINTBUTTON "[1] 「旦那様」と呼んでもらう", 1
PRINTL
PRINTBUTTON "[2] 任意入力", 2
PRINTL
DRAWLINE
PRINTBUTTON @"[999] %CSTR:対象キャラ:二人称%のままがいい", 999
PRINTL
PRINTL
BINPUT

IF RESULT == 999
	PRINTL
	PRINTL アニラ：
	PRINTFORMW 「「%CSTR:対象キャラ:二人称%」のままが良いと？　くふふ、%CSTR:対象キャラ:二人称%が言うなら、そのままにしておこうかの。
	PRINTFORMW 　もしかして、%CSTR:対象キャラ:二人称%と呼ばれるのを気に入っておったのか？」
ELSE
	SELECTCASE RESULT
		CASE 0
			CSTR:対象キャラ:二人称 = お前様
		CASE 1
			CSTR:対象キャラ:二人称 = 旦那様
		CASE 2
			DRAWLINE
			PRINTL 呼んでもらう二人称を入力してください。
			DRAWLINE

			INPUTS
			CSTR:対象キャラ:二人称 '= RESULTS
	ENDSELECT
	PRINTL
	PRINTL アニラ：
	PRINTFORMW 「了解じゃ。では、これからは「%CSTR:対象キャラ:二人称%」と呼ぼうかの。
	PRINTFORMW 　くふふ。%CSTR:対象キャラ:二人称%、%CSTR:対象キャラ:二人称%……なんだか気恥ずかしいが、嬉しくも感じるのじゃ」
ENDIF
PRINTL
PRINTFORMW そうやってアニラとなんでもない会話をしていると、だんだんと空が暗くなってきた。
PRINTFORMW もうすぐ夜になるが、祭りの賑わいは更に大きくなっている。
PRINTFORMW 話によると、今日の祭りは夜通し行われる予定なのだとか。
PRINTL
PRINTL アニラ：
PRINTFORMW 「のう、%CSTR:対象キャラ:二人称%。
PRINTFORMW 　祭りを眺めるのも良いが、そろそろ……じゃろ？」
PRINTL
PRINTFORMW アニラはちらちらと、部屋の真ん中に敷かれた布団に目をやる。
PRINTFORMW 二人がいるこの部屋には、明日の正午までは誰も近寄ってこない。
PRINTFORMW もちろん、それは新婚夫婦である二人の時間を確保するためで。
PRINTFORMW 俗に言ってしまえば「新婚初夜」なのである。
PRINTL
PRINTFORMW %CALLNAME:MASTER%はアニラの手を引き、布団の上へと横たわらせた。
PRINTL
PRINTL アニラ：
IF GETBIT(TALENT:MASTER:性別, 1)
	PRINTFORMW 「くふふ、%CSTR:対象キャラ:二人称%。
	CALL PRINT_STRW("　我の胎に、次の十二神将を授けてもらえるかのう_H_？」")
ELSE
	PRINTFORMW 「くふふ、%CSTR:対象キャラ:二人称%。
	CALL PRINT_STRW("　今夜はずっと、ずーっと愛して欲しいのじゃ_H_」")
ENDIF
PRINTL
PRINTFORMW %CALLNAME:MASTER%は部屋の灯りを消し、アニラに覆いかぶさった。
PRINTL
PRINTFORMW ………
PRINTFORMW ……
PRINTFORMW …

;以下は汎用結婚イベで表示される、素質変化の文章。必要ないなら消しても良い
;-----------ここから
PRINTL 
SETCOLOR 0xFFFF00
PRINTFORMW %CALLNAME:対象キャラ%は［伴侶］を得ました。
RESETCOLOR
IF TALENT:対象キャラ:浮気 > 0
	PRINTFORM %CALLNAME:対象キャラ%は
	SETCOLOR カラーパレット("えっちな色")
	PRINT [浮気]
	RESETCOLOR
	PRINT を失った……
ENDIF
IF TALENT:対象キャラ:恋人持ち != 2
	SIF TALENT:対象キャラ:浮気 > 0
		WAIT
	PRINTFORM %CALLNAME:対象キャラ%は
	SETCOLOR カラーパレット("えっちな色")
	IF TALENT:対象キャラ:性別 == 1 || TALENT:対象キャラ:性別 == 3
		PRINT [人妻]
	ELSE
		PRINT [既婚者]
	ENDIF
	RESETCOLOR
	PRINT を得た！
ENDIF
PRINTL
PRINTL
FORCEWAIT
;-----------ここまで

;イベント中にアイテムやステータスの変動が起こる場合、以下の分岐の中に記述すること
;イベント再視聴で無限に稼ぎが出来たりしないようにする措置
IF !FLAG:クリア済み視聴フラグ
	;結婚イベントなので、伴侶素質を付与して履歴に登録する
	;何かの理由があるなら消しても良いが、残すことを推奨
	;-----------ここから
	TALENT:対象キャラ:恋慕 = 2
	初体験日:対象キャラ:伴侶 = DAY
	CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:対象キャラ%を[伴侶]陥落させた</font><img src='えっちハート'>","うふふ実績")
	CALL 履歴データベース登録(CFLAG:対象キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>陥落素質[伴侶]を得た</font><img src='えっちハート'>","うふふ実績")
	IF TALENT:対象キャラ:浮気 > 0
		TALENT:対象キャラ:浮気 = 0
	ENDIF
	IF TALENT:対象キャラ:恋人持ち != 2
		TALENT:対象キャラ:恋人持ち = 2
	ENDIF
	;-----------ここまで
ENDIF

CALL イベント完了フラグ汎用処理("未神宮の若夫婦")






