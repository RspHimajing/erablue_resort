;2830 リィン・シュバルツァー
;--------------------------------------------------
;１「孤影斬→緋空斬」　陸の型
;--------------------------------------------------
@固有アビ_2830_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
#DIM 効果数値３

; 参考
; 大体消費が重く、威力が高くなったブレイズ
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = 緋空斬
	消費ＭＰ = 30
	効果数値１ = 200
	効果数値２   = 20
	効果数値３   = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 1
	アビ名 = 孤影斬
	消費ＭＰ = 20
	効果数値１ = 150
	効果数値２   = 15
	効果数値３   = 0
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕{効果数値１}％］の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［{効果数値２}％］（Ｂ枠・3ターン）の防御力減少効果を与える。成功率80％。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
			詳細文文字列受け渡し変数 += @"<br>［回復力✕{効果数値３}％］（3ターン）の火傷効果を与える。成功率70％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		;対象選択
		CALL アビ対象選択テンプレート_敵単体
		;敵単体に自属性ダメージ
		アビ汎用変数:効果割合 = 効果数値１
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 効果数値３ / 100
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 70
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

;--------------------------------------------------
;２「疾風→裏疾風」　弐の型
;--------------------------------------------------
@固有アビ_2830_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 効果数値
#DIM 消費ＭＰ
#DIM 対象数

; 参考
; Lv1：エクスプロージョン互換　Lv2：威力アップと消費大幅アップ
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = 裏疾風
	消費ＭＰ = 60
	効果数値 = 75
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
	アビ名 = 疾風
	消費ＭＰ = 30
	効果数値 = 25
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕敵の数✕{効果数値}％］の火属性ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			対象数 += 1
		NEXT
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値 * MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

;--------------------------------------------------
;３「螺旋撃→螺旋撃・紅蓮」　壱の型＋突撃陣オーダー
;--------------------------------------------------
@固有アビ_2830_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIMS アビ名
#DIM 消費ＭＰ
#DIM 効果数値

; 参考
; Lv1：ファイア++互換　Lv2：威力アップ＆ヒートアップ付与
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = 螺旋撃・紅蓮
	消費ＭＰ = 40
	効果数値 = 400
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
	アビ名 = 螺旋撃
	消費ＭＰ = 25
	効果数値 = 300
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕{効果数値}％］の自属性ダメージを与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
		詳細文文字列受け渡し変数 += @"<br>味方全体に［30％］（Ａ枠・3ターン）の攻撃力上昇効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		;対象選択
		CALL アビ対象選択テンプレート_敵単体
		;敵単体に自属性ダメージ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
