;アビリティメモ
;■基本設計について
;刻印アビリティを利用する。
;『分解試薬』を用いて自身の錬金術（分解）の効力を高める戦い方。所持は最大５個まで。
;『分解試薬』を消費する際、持っているモノは必ず全て消費する設計。
;■サポアビについて
;サポアビは陥落状態で倍に強化され、アビリティの使用感が大幅に向上される設計
;サポアビ１『新分解理論』：最初から解禁。毎ターン開始ごとに『分解試薬』を１つ獲得。陥落状態では２個取得。（陥落の先が実装された場合は３個取得）
;サポアビ２『三元質定理』：レベル１５で解禁。ダメージを受けると５０％の確率で『分解試薬』をひとつ得る。陥落状態では１００％化。（陥落の先が実装された場合は得られる『分解試薬』がふたつに）
;サポアビ３『哲学的結婚』：レベル３０で解禁。３０％の確率で『分解試薬』を消費しなくなる。陥落状態では７０％化。（陥落の先が実装された場合は１００％）
;■アビリティについて
;アビ１『コンキリオ・サーラリウス』：単体連続攻撃。発動ごとに『分解試薬』をひとつ獲得。使用時『分解試薬』は消費しない。奥義ゲージも上昇する。『分解試薬』が増加するごとに攻撃回数が１回増える。５個保持中では更に例外的に１０回攻撃。モチーフは錬金術三大要素の一つ『塩』
;アビ２『スブリマー卜・サルヴァレーテ』：基本的にデバフ。１個消費で防御力低下。２個消費で攻撃力低下追加。３個消費で列化＆回避率低下追加。４個消費で効力強化＋味方命中率バフ。５個で全体化＋更に効力強化＋味方命中率バフ強化。モチーフは錬金術三大要素の一つ『硫黄』
;アビ３『ハイドラグラム・ウェリタシー』：ランダム複数攻撃。１個消費で敵全体の中からランダム３回複数攻撃。２個消費でランダム４回攻撃。３個消費で全体化。４個消費で威力強化＋敵命中率低下デバフ。５個消費で更に威力強化＋敵命中率低下デバフ強化。モチーフは錬金術三大要素の一つ『水銀』

;----------------------------------------------------------------------------------
;アビリティ１処理
;----------------------------------------------------------------------------------	
@固有アビ_169_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM 攻撃回数
#DIM 奥義ゲージ量
#DIM 分解試薬数
#DIM 効果数値

隊列番号 = キャラ隊列検索(GETCHARA(169))
分解試薬数 = 刻印現在数確認("分解試薬", 隊列番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
消費ＭＰ = 35
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	アビ名 = コンキリオ・サーラリウス+
	奥義ゲージ量 = 分解試薬数 + 25
	効果数値 = 分解試薬数 + 105
ELSE
	アビ名 = コンキリオ・サーラリウス
	奥義ゲージ量 = 分解試薬数 + 15
	効果数値 = 分解試薬数 + 90
ENDIF

;分解試薬の回数分だけ攻撃回数増加処理
IF 分解試薬数 == 5
	攻撃回数 = 分解試薬数 + 5
ELSE
	攻撃回数 = 分解試薬数 + 4
ENDIF

SELECTCASE RAND:2
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「錬金術が三原質、『塩』の本質をお見せしましょう」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「『塩』とは硫黄と水銀を結びつけるエーテル物質。その力を引き出せば…」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕{効果数値}％］の自属性ダメージを{攻撃回数}回与える。
		詳細文文字列受け渡し変数 += @"<br>自身に［{奥義ゲージ量}］の奥義ゲージ増加を与える。"
		詳細文文字列受け渡し変数 += @"<br>［1］の分解試薬を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		FOR LOCAL, 0, 攻撃回数
			CALL アビテンプレート_ダメージ処理_自属性
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 奥義ゲージ量
		CALL アビテンプレート_有利効果_奥義ゲージUP

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
		CALL アビテンプレート_刻印追加(1)
ENDSELECT

;----------------------------------------------------------------------------------
;アビリティ２処理
;----------------------------------------------------------------------------------	
@固有アビ_169_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM 防御低下値
#DIM 攻撃低下値
#DIM 回避低下値
#DIM 命中上昇値
#DIM 成功率
#DIM プロメティア番号
#DIM 分解試薬数
#DIM 効果ターン

隊列番号 = キャラ隊列検索(GETCHARA(169))
プロメティア番号 = GETCHARA(169)
分解試薬数 = 刻印現在数確認("分解試薬", 隊列番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = スブリマー卜・サルヴァレーテ+
	消費ＭＰ = 50
	IF 分解試薬数 == 5
		防御低下値 = 35
		攻撃低下値 = 35
		回避低下値 = 35
		命中上昇値 = 35
		成功率 = 150
		効果ターン = 5
	ELSEIF 分解試薬数 >= 4
		防御低下値 = 25
		攻撃低下値 = 25
		回避低下値 = 25
		命中上昇値 = 25
		成功率 = 120
		効果ターン = 4
	ELSEIF 分解試薬数 >= 3
		防御低下値 = 15
		攻撃低下値 = 15
		回避低下値 = 15
		成功率 = 100
		効果ターン = 3
	ELSEIF 分解試薬数 >= 2
		防御低下値 = 15
		攻撃低下値 = 15
		成功率 = 80
		効果ターン = 3
	ELSE
		防御低下値 = 15
		成功率 = 80
		効果ターン = 3
	ENDIF
ELSE
	アビ名 = スブリマー卜・サルヴァレーテ
	消費ＭＰ = 50
	IF 分解試薬数 == 5
		防御低下値 = 25
		攻撃低下値 = 25
		回避低下値 = 25
		命中上昇値 = 25
		成功率 = 120
		効果ターン = 4
	ELSEIF 分解試薬数 >= 4
		防御低下値 = 15
		攻撃低下値 = 15
		回避低下値 = 15
		命中上昇値 = 15
		成功率 = 110
		効果ターン = 3
	ELSEIF 分解試薬数 >= 3
		防御低下値 = 10
		攻撃低下値 = 10
		回避低下値 = 10
		成功率 = 90
		効果ターン = 3
	ELSEIF 分解試薬数 >= 2
		防御低下値 = 10
		攻撃低下値 = 10
		成功率 = 80
		効果ターン = 3
	ELSE
		防御低下値 = 10
		成功率 = 80
		効果ターン = 3
	ENDIF
ENDIF

SELECTCASE RAND:2
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「錬金術が三原質、金属の父たる『硫黄』の能力…味わっていただきましょう」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「『硫黄』とは『陽』の相。能動的なそれを『分解』の力で取り除いてやれば…」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 分解試薬数 >= 4
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に対し、防御力を［{防御低下値}％］、攻撃力を［{攻撃低下値}％］、回避率を［{回避低下値}％］、
			詳細文文字列受け渡し変数 += @"<br>それぞれ独自枠の減少効果を［{効果ターン}ターン］与える。"
			詳細文文字列受け渡し変数 += @"<br>味方全体に［{命中上昇値}％］（独自枠・{効果ターン}ターン）の命中率上昇効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、［{分解試薬数}］の分解試薬を消費する。"
		ELSEIF 分解試薬数 >= 3
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵縦列に対し、防御力を［{防御低下値}％］、攻撃力を［{攻撃低下値}％］、回避率を［{回避低下値}％］、
			詳細文文字列受け渡し変数 += @"<br>それぞれ独自枠の減少効果を［{効果ターン}ターン］与える。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、［{分解試薬数}］の分解試薬を消費する。"
		ELSEIF 分解試薬数 >= 2
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［{防御低下値}％］（独自枠・{効果ターン}ターン）の防御力減少効果を与える。
			詳細文文字列受け渡し変数 += @"<br>［{攻撃低下値}％］（独自枠・{効果ターン}ターン）の攻撃力減少効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、［{分解試薬数}］の分解試薬を消費する。"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［{防御低下値}％］（独自枠・{効果ターン}ターン）の防御力減少効果を与える。
			詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、［{分解試薬数}］の分解試薬を消費する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 分解試薬数 >= 4
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 攻撃低下値
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 回避低下値
			アビ汎用文字列:バフ・デバフ枠 = 回避率_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 命中上昇値
			アビ汎用文字列:バフ・デバフ枠 = 命中率_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:持続ターン = 効果ターン
			CALL アビテンプレート_有利効果_バフ効果セット
		
		ELSEIF 分解試薬数 >= 3
			;敵縦列は頭に1回のみでよい
			CALL アビ対象選択テンプレート_敵縦列
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 攻撃低下値
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 回避低下値
			アビ汎用文字列:バフ・デバフ枠 = 回避率_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ELSEIF 分解試薬数 >= 2
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 攻撃低下値
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ELSE
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 防御低下値
			アビ汎用文字列:バフ・デバフ枠 = 防御力_サルヴァ
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ENDIF

		;サポアビ３の使用判定
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			IF 陥落チェック(キャラ番号)
				;確率６０％
				IF RAND:10 < 6
					WSTR:(K++) = 特性：『哲学的結婚』の効果で『分解試薬』は消費されなかった！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ELSE
				;確率３０％
				IF RAND:10 < 3
					WSTR:(K++) = 特性：『哲学的結婚』の効果で『分解試薬』は消費されなかった！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ENDIF
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
			CALL アビテンプレート_刻印減少(分解試薬数)
		ENDIF

ENDSELECT

;----------------------------------------------------------------------------------
;アビリティ３処理
;----------------------------------------------------------------------------------	
@固有アビ_169_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM 命中低下値
#DIM 成功率
#DIM アビダメ倍率
#DIM アビダメ回数
#DIM L_CNT
#DIM 分解試薬数
#DIM 攻撃対象数
#DIM 効果ターン

隊列番号 = キャラ隊列検索(GETCHARA(169))
分解試薬数 = 刻印現在数確認("分解試薬", 隊列番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = ハイドラグラム・ウェリタシー+
	消費ＭＰ = 60
	IF 分解試薬数 == 5
		アビダメ倍率 = 145
		アビダメ回数 = 8
		命中低下値 = 35
		成功率 = 120
		効果ターン = 5
	ELSEIF 分解試薬数 >= 4
		アビダメ倍率 = 145
		アビダメ回数 = 6
		命中低下値 = 25
		成功率 = 100
		効果ターン = 4
	ELSEIF 分解試薬数 >= 3
		アビダメ倍率 = 135
		アビダメ回数 = 5
	ELSEIF 分解試薬数 >= 2
		アビダメ倍率 = 125
		アビダメ回数 = 4
		攻撃対象数 = 4
	ELSE
		アビダメ倍率 = 115
		アビダメ回数 = 3
		攻撃対象数 = 3
	ENDIF
ELSE
	アビ名 = ハイドラグラム・ウェリタシー
	消費ＭＰ = 45
	IF 分解試薬数 == 5
		アビダメ倍率 = 130
		アビダメ回数 = 6
		命中低下値 = 25
		成功率 = 100
		効果ターン = 4
	ELSEIF 分解試薬数 >= 4
		アビダメ倍率 = 130
		アビダメ回数 = 4
		命中低下値 = 15
		成功率 = 80
		効果ターン = 3
	ELSEIF 分解試薬数 >= 3
		アビダメ倍率 = 120
		アビダメ回数 = 4
	ELSEIF 分解試薬数 >= 2
		アビダメ倍率 = 110
		アビダメ回数 = 4
		攻撃対象数 = 4
	ELSE
		アビダメ倍率 = 100
		アビダメ回数 = 3
		攻撃対象数 = 3
	ENDIF
ENDIF

SELECTCASE RAND:2
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「錬金術が三原質、『水銀』とは金属の母。それが激しく暴れ狂う痛みを教えましょう」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「『陰』の相たる『水銀』。水の如く静かに流れるその本質を分解してやれば…！」
ENDSELECT

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 分解試薬数 >= 4
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕{アビダメ倍率}％］の自属性ダメージを{アビダメ回数}回与える。
			詳細文文字列受け渡し変数 += @"<br>［{命中低下値}％］（独自枠・{効果ターン}ターン）の命中率減少効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、［{分解試薬数}］の分解試薬を消費する。"
		ELSEIF 分解試薬数 >= 3
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕{アビダメ倍率}％］の自属性ダメージを{アビダメ回数}回与える。
			詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、［{分解試薬数}］の分解試薬を消費する。"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ランダムな敵{攻撃対象数}体に［攻撃力✕{アビダメ倍率}％］の自属性ダメージを{アビダメ回数}回与える。
			詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、［{分解試薬数}］の分解試薬を消費する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 分解試薬数 >= 4
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = アビダメ倍率
			FOR L_CNT, 0, アビダメ回数
				CALL アビテンプレート_ダメージ処理_自属性
			NEXT

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 命中低下値
			アビ汎用文字列:バフ・デバフ枠 = 命中率_ハイドラ
			アビ汎用文字列:バフ・デバフ対象能力 = 命中率
			アビ汎用変数:持続ターン = 効果ターン
			アビ汎用変数:基礎成功確率 = 成功率
			CALL アビテンプレート_不利効果_デバフ効果セット

		ELSEIF 分解試薬数 >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = アビダメ倍率
			FOR L_CNT, 0, アビダメ回数
				CALL アビテンプレート_ダメージ処理_自属性
			NEXT
		ELSE
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(アビダメ回数)
			アビ汎用変数:効果割合 = アビダメ倍率
			CALL アビテンプレート_ダメージ処理_自属性
		ENDIF

		;サポアビ３の使用判定
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			IF 陥落チェック(キャラ番号)
				;確率６０％
				IF RAND:10 < 6
					WSTR:(K++) = 特性：『哲学的結婚』の効果で『分解試薬』は消費されなかった！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ELSE
				;確率３０％
				IF RAND:10 < 3
					WSTR:(K++) = 特性：『哲学的結婚』の効果で『分解試薬』は消費されなかった！
				ELSE
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_自己のみ
					アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
					CALL アビテンプレート_刻印減少(分解試薬数)
				ENDIF
			ENDIF
		ELSE
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
			CALL アビテンプレート_刻印減少(分解試薬数)
		ENDIF

ENDSELECT

;----------------------------------------------------------------------------------
;サポアビ処理
;----------------------------------------------------------------------------------
@バフ・デバフ特殊表示_三元質定理(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM プロメティア番号

隊列番号 = キャラ隊列検索(GETCHARA(169))
プロメティア番号 = GETCHARA(169)

PRINTFORML サポートバフ：三元質定理
PRINTFORML 効果枠：三元質定理
IF 陥落チェック(プロメティア番号)
	PRINTFORML 効果：ダメージを受けると１００％の確率で『分解試薬』を１つ獲得
ELSE
	PRINTFORML 効果：ダメージを受けると５０％の確率で『分解試薬』を１つ獲得
ENDIF
PRINTFORML 持続ターン：永続

;----------------------------------------------------------------------------------
;『分解試薬』戦闘画面上表示処理
@探索欄表示_キャラ_169(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "分解試薬:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"Ū{刻印現在数確認("分解試薬", 隊列番号)}", 200, 90

;----------------------------------------------------------------------------------
;サポアビ１『新分解理論』処理
@固有行動開始時処理_キャラ_169
#DIM プロメティア番号

プロメティア番号 = GETCHARA(169)

SELECTCASE RAND:4
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「参りますよ。覚悟してください」
CASE 1
	アビ汎用文字列:実行時メッセージ１ = 「崩壊の力の一端、その身で受けてもらいます」
CASE 2
	アビ汎用文字列:実行時メッセージ１ = 「分解準備……！」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「私にはクラリスのような才能はありませんので…！」
ENDSELECT

IF TALENT:プロメティア番号:恋慕 == 2
	;結婚済みの場合
	アビテンプレート用_アビ名 = 真説・新分解理論
ELSE
	アビテンプレート用_アビ名 = 新分解理論
ENDIF

IF 陥落チェック(プロメティア番号)
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
	CALL アビテンプレート_刻印追加(2)
ELSE
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
	CALL アビテンプレート_刻印追加(1)
ENDIF

;----------------------------------------------------------------------------------
;サポアビ２『三元質定理』処理
@固有戦闘開始時処理_キャラ_169
;戦闘開始時処理関数から呼び出し
#DIM プロメティア番号

プロメティア番号 = GETCHARA(169)

;サポアビ２呼び出し処理
IF 基礎BATTLE_STATE:プロメティア番号:Lv >= 15
	アビテンプレート用_アビ名 = 三元質定理

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = 三元質定理
	アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_パッシブバフ効果セット
ENDIF

;サポアビ１『真説・新分解理論』処理
IF TALENT:プロメティア番号:恋慕 == 2

	SELECTCASE RAND:3
	CASE 0
		アビ汎用文字列:実行時メッセージ１ = 無限の愛の熱量が『分解試薬』の中身を全て満たす……！
	CASE 1
		アビ汎用文字列:実行時メッセージ１ = 荒れ狂う愛のエネルギーが『分解試薬』全てにチャージされる……！
	CASEELSE
		アビ汎用文字列:実行時メッセージ１ = 愛情の奔流が『分解試薬』の全てを満たしてゆく……！
	ENDSELECT

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
	CALL アビテンプレート_刻印追加(5)
ENDIF

@特殊反応_被ダメ時_三元質定理(バフ番号, 隊列番号, 被ダメ記録順)
#DIM 被ダメ記録順
#DIM 隊列番号
#DIM バフ番号
#DIM プロメティア番号

隊列番号 = キャラ隊列検索(GETCHARA(169))
プロメティア番号 = GETCHARA(169)

SELECTCASE RAND:2
CASE 0
	KSTR:(K++) = 「眼には眼を。歯には歯を。痛みには痛みを！」
CASE 1
	KSTR:(K++) = 「く…！　攻撃エネルギーの残滓、それを『塩』の力で化合変換して…！」
ENDSELECT

IF 陥落チェック(プロメティア番号)
	;陥落時、成功率１００％
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
	CALL アビテンプレート_刻印追加(1)
ELSE
	;陥落前、成功率５０％
	IF RAND:2 == 0
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
		CALL アビテンプレート_刻印追加(1)
	ELSE
		KSTR:(K++) = 「…っ！　錬金術の制御が…っ」
		WSTR:(K++) = 『三元質定理』によりプロメティアは『分解試薬』を生成しようとしたが失敗した…
	ENDIF
ENDIF

;--------------------------------------------------
;戦闘能力の固有処理説明
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_169(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

;口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;基本的にWAIT系列は使わないことを推奨

IF TALENT:対象キャラ:恋慕 == 1 || TALENT:対象キャラ:セフレ
	;恋愛・セフレの場合
	KSTR:(K++) = □ 特性：『新分解理論』 □-----------------------------------------------------------------
	KSTR:(K++) = 　毎ターン開始時に『分解試薬』を２つ獲得
	KSTR:(K++) = 　アビリティ２・３、および奥義発動時、『分解試薬』をすべて消費し、威力・効力・範囲を向上させる
	KSTR:(K++) = 　
ELSEIF TALENT:対象キャラ:恋慕 == 2
	;結婚済みの場合
	KSTR:(K++) = □ 特性：『真説・新分解理論』 □-----------------------------------------------------------------
	KSTR:(K++) = 　戦闘開始時、『分解試薬』を５つ獲得。また毎ターン開始時に『分解試薬』を２つ獲得
	KSTR:(K++) = 　アビリティ２・３、および奥義発動時、『分解試薬』をすべて消費し、威力・効力・範囲を向上させる
	KSTR:(K++) = 　
ELSE
	;未陥落の場合
	KSTR:(K++) = □ 特性：『新分解理論』 □-----------------------------------------------------------------
	KSTR:(K++) = 　毎ターン開始時に『分解試薬』を１つ獲得
	KSTR:(K++) = 　アビリティ２・３、および奥義発動時、『分解試薬』をすべて消費し、威力・効力・範囲を向上させる
	KSTR:(K++) = 　
ENDIF

IF 基礎BATTLE_STATE:対象キャラ:Lv >= 15
	KSTR:(K++) = □ 特性：『三元質定理』 □-----------------------------------------------------------------
	IF 陥落チェック(対象キャラ)
		KSTR:(K++) = 　ダメージを受けると１００％の確率で『分解試薬』を１つ獲得
		KSTR:(K++) = 　
	ELSE
		KSTR:(K++) = 　ダメージを受けると５０％の確率で『分解試薬』を１つ獲得
		KSTR:(K++) = 　
	ENDIF
ENDIF

IF 基礎BATTLE_STATE:対象キャラ:Lv >= 30
	KSTR:(K++) = □ 特性：『哲学的結婚』 □-----------------------------------------------------------------
	IF 陥落チェック(対象キャラ)
		KSTR:(K++) = 　『分解試薬』消費時、６０％の確率で消費しない
		KSTR:(K++) = 　
	ELSE
		KSTR:(K++) = 　『分解試薬』消費時、３０％の確率で消費しない
		KSTR:(K++) = 　
	ENDIF
ENDIF

CALL 口上変数表示(対象キャラ, 1)
