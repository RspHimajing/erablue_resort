;■奥義について
;奥義『マグナ・アウルムレア』。モチーフは錬金術の終着点『黄金』。
;全体攻撃。および味方全体へのＨＰ、ＭＰ回復。『分解試薬』の数が増えるごとに威力・回復力が増加。
;奥義発動に関しては必ず『分解試薬』を全て消費する（サポアビ３の効果は受けない）
;陥落の上位版（結婚とかそこらへん）が実装されたら更に進化した『マグヌム・マグナ・アウルムレア』を実装する予定

@固有奥義_169(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 隊列番号
#DIM 分解試薬数
#DIM ＨＰ回復量
#DIM ＭＰ回復量
#DIM 効果数値

隊列番号 = キャラ隊列検索(GETCHARA(169))
分解試薬数 = 刻印現在数確認("分解試薬", 隊列番号)

IF (TALENT:キャラ番号:恋慕 == 1 || TALENT:キャラ番号:セフレ) && 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	;恋愛・セフレの場合
	奥義名 = マグナ・アウルムレア+
	奥義威力 = 0
	効果数値 = 分解試薬数 * 20 + 500
	ＨＰ回復量 = 分解試薬数 * 20 + 200
	ＭＰ回復量 = 分解試薬数 * 10 + 100
ELSEIF TALENT:キャラ番号:恋慕 == 2 && 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	;結婚済みの場合
	奥義名 = マグヌム・マグナ・アウルムレア
	奥義威力 = 0
	効果数値 = 分解試薬数 * 30 + 550
	ＨＰ回復量 = 分解試薬数 * 20 + 300
	ＭＰ回復量 = 分解試薬数 * 15 + 100
ELSEIF TALENT:キャラ番号:恋慕 == 2 && 基礎BATTLE_STATE:キャラ番号:Lv >= 60
	;結婚済みの場合
	奥義名 = マグヌム・マグナ・アウルムレア+
	奥義威力 = 0
	効果数値 = 分解試薬数 * 40 + 650
	ＨＰ回復量 = 分解試薬数 * 30 + 350
	ＭＰ回復量 = 分解試薬数 * 25 + 150
ELSE
	奥義名 = マグナ・アウルムレア
	奥義威力 = 0
	効果数値 = 分解試薬数 * 10 + 400
	ＨＰ回復量 = 分解試薬数 * 10 + 100
	ＭＰ回復量 = 分解試薬数 * 10 + 50
ENDIF

SELECTCASE RAND:2
CASE 0
	アビ汎用文字列:実行時メッセージ１ = 「『塩』、『硫黄』、『水銀』…錬金術が三原質。それらが等しく結合し──『黄金』へ至る！」
CASEELSE
	アビ汎用文字列:実行時メッセージ１ = 「『黄金』の力よ！　今こそ彼方より此方へと顕現せよ！」
ENDSELECT

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{効果数値}％<br>
		詳細文文字列受け渡し変数 += "敵全体を錬金術で存在分解し、発生した純粋エネルギーを癒しの力として味方全体へ還元する。"
		RETURN 奥義威力
	CASE "奥義説明文_詳細"
		詳細文文字列受け渡し変数 += @"敵全体に自属性［{効果数値}％］のダメージを与えた後、"
		詳細文文字列受け渡し変数 += @"<br>味方のＨＰを［{ＨＰ回復量}］、ＭＰを［{ＭＰ回復量}］回復する。"
		詳細文文字列受け渡し変数 += @"<br>『分解試薬』を全て消費する。"
	CASE "奥義追加効果"
		アビ汎用文字列:ダメージ種類 = 奥義ダメージ
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = ＨＰ回復量
		CALL アビテンプレート_回復処理_MAXでも回復

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = ＭＰ回復量
		CALL アビテンプレート_ＭＰ回復処理_MAXでも回復

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 分解試薬
		CALL アビテンプレート_刻印減少(分解試薬数)

ENDSELECT
