
@固有アビ_11_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 祝福判定
#DIM 祝福対象

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = 祝福の接吻＋
	消費ＭＰ = 200
ELSE
	アビ名 = 祝福の接吻
	消費ＭＰ = 250
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方単体に（独自枠・消去不可）の巌迫の祝福効果を与える。
		詳細文文字列受け渡し変数 += @"<br>既に厳迫の祝福が付与されている時、［500］のＨＰ回復を与える。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方単体に（独自枠・消去不可）の巌迫の祝福効果を与える。
		詳細文文字列受け渡し変数 += @"<br>＞［50］の土属性攻撃上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［100％］の防御力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［100％］のDA率上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［100％］の弱体耐性上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［20％］の追撃効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>既に厳迫の祝福が付与されている時、［500］のＨＰ回復を与える。"
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		祝福対象 = アビテンプレート用_対象保存:0
		祝福判定 = バフ番号取得_枠("巌迫の祝福", 祝福対象) 
		IF 祝福判定 > -1
			アビ汎用変数:効果量 = 500
			CALL アビテンプレート_回復処理_MAXでも回復
			
			CALL アビ汎用変数文字列リセット
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 巌迫の祝福
		アビ汎用文字列:バフ・デバフ対象能力 = 巌迫の祝福
		アビ汎用変数:効果量 = 0
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 巌迫の祝福_属性与ダメージ_土
		アビ汎用文字列:バフ・デバフ対象能力 = 属性与ダメージ_土
		アビ汎用変数:効果量 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("巌迫の祝福")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 巌迫の祝福_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_有利効果_付随バフ効果セット("巌迫の祝福")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 巌迫の祝福_DA率
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_有利効果_付随バフ効果セット("巌迫の祝福")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 巌迫の祝福_弱体耐性
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体耐性
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_有利効果_付随バフ効果セット("巌迫の祝福")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 巌迫の祝福_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 20
		CALL アビテンプレート_有利効果_付随バフ効果セット("巌迫の祝福")
ENDSELECT


@固有アビ_11_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 75
	アビ名 = 降砂岩＋
	消費ＭＰ = 100
ELSE
	アビ名 = 降砂岩
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ランダムな敵に［666］の無属性ダメージを6回与える。
		詳細文文字列受け渡し変数 += @"<br>敵全体に［1］のスロウ効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［1］のバフ消去を与える。"
		詳細文文字列受け渡し変数 += @"<br>◆『金』の楔石効果中、［4の倍数ターン］終了時に自動発動する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		FOR LOCAL, 0, 6
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果量 = 666
			CALL アビテンプレート_ダメージ処理_無属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_スロウ
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_バフ消去
ENDSELECT

@固有アビ_11_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = 『金』の楔石
	消費ＭＰ = 500
ENDIF

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:0
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（独自枠・消去不可）の『金』の楔石効果を与える。
		詳細文文字列受け渡し変数 += @"<br>このアビリティは再使用出来ない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（独自枠・消去不可）の『金』の楔石効果を与える。
		詳細文文字列受け渡し変数 += @"<br>＞［100％］の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［300％］の防御力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［1000％］のTA率上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞弱体無効効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［10％］の与ダメージ上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは再使用出来ない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 『金』の楔石
		アビ汎用文字列:バフ・デバフ対象能力 = 『金』の楔石
		アビ汎用変数:効果量 = 0
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 『金』の楔石_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_有利効果_付随バフ効果セット("『金』の楔石")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 『金』の楔石_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 300
		CALL アビテンプレート_有利効果_付随バフ効果セット("『金』の楔石")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 『金』の楔石_絶対TA
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 1000
		CALL アビテンプレート_有利効果_付随バフ効果セット("『金』の楔石")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 『金』の楔石_弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		CALL アビテンプレート_有利効果_付随バフ効果セット("『金』の楔石")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 『金』の楔石_与ダメ増加
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:効果割合 = 10
		CALL アビテンプレート_有利効果_付随バフ効果セット("『金』の楔石")
		
		戦闘中カウント:戦闘行動キャラ:0 = 現在ターン数
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有行動開始時処理_キャラ_11
;ターン開始時処理関数から呼び出し
#DIM 『金』の楔石
#DIM 隊列番号

SIF 戦闘中カウント:戦闘行動キャラ:0 == 現在ターン数
	RETURN 0

隊列番号 = キャラ隊列検索(GETCHARA(11))

『金』の楔石 = バフ番号取得_枠("『金』の楔石", 隊列番号)
IF 『金』の楔石 > -1
	IF 現在ターン数 % 2 != 戦闘中カウント:戦闘行動キャラ:0 % 2
		WSTR:(K++) = ガレヲンは静かに佇んでいる…
		行動不能フラグ = 1
	ELSEIF 現在ターン数 % 2 == 戦闘中カウント:戦闘行動キャラ:0 % 2
		WSTR:(K++) = 母なる大地の仁愛が隆起する…
		アビテンプレート用_アビ名 = 『金』の楔石
		
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = 母なる大地の仁愛
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 20
		CALL アビテンプレート_有利効果_奥義ゲージUP
	ENDIF
ENDIF


@固有ターン終了時処理_キャラ_11
;降砂岩自動発動処理
;ターン終了時処理関数から呼び出し
#DIM 『金』の楔石
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(11))

『金』の楔石 = バフ番号取得_枠("『金』の楔石", 隊列番号)
IF 『金』の楔石 > -1
	IF 現在ターン数 % 4 == 0
		WSTR:(K++) = 《破潰。（usubut…）》
		アビテンプレート用_アビ名 = 降砂岩
		
		FOR LOCAL, 0, 6
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果量 = 666
			CALL アビテンプレート_ダメージ処理_無属性
		NEXT
		
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_スロウ
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_バフ消去
	ENDIF
ENDIF



@バフ・デバフ特殊表示_巌迫の祝福(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 巌迫の祝福
PRINTFORML 土属性攻撃力・防御力・DA率・弱体耐性が上昇/通常攻撃時に追撃を行う状態［永続/消去不可］



@バフ・デバフ特殊表示_『金』の楔石(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 『金』の楔石
PRINTFORML 攻撃性能・防御性能が大幅に上昇/特定ターン中行動不能になるが、特定ターンに与ダメージ・奥義ゲージが上昇する状態［永続/消去不可］
PRINTFORML （バフ付与ターンが奇数ターンだった場合、偶数ターン行動不能・奇数ターンにバフ効果）
PRINTFORML （バフ付与ターンが偶数ターンだった場合、奇数ターン行動不能・偶数ターンにバフ効果）

