
;奥義に相当する大技が原作に無いためだいぶ独自解釈でつくりました
;かばうにしても仲間を守るというよりは叩かれたいだけなので、鉄壁にはしていないつもりです

;☆元にした原作スキル
;「きけんなあそび」
;一定回数物理攻撃無効状態にする
;　→そのままは強すぎるので３割カットくらいに

;「いくらでもたたいていいのよ♪」
;対象の物理防御力と魔法防御力を上昇させる
;　→＋で追加される防御バフがこれのつもり
;　　こっちは２ターンでもよかったかもしれない

@固有奥義_2092(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM シールド回数
#DIM 防御アップ値
#DIM 隊列番号
#DIM 奥義ランク

隊列番号 = キャラ隊列検索(GETCHARA(2092))

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	奥義ランク = 2
ELSE
	奥義ランク = 1
ENDIF

SELECTCASE 奥義ランク
	CASE IS >= 2
		奥義名 = きけんなあそび＋
		奥義威力 = 0
		シールド回数 = 5
		防御アップ値 = 40
	CASEELSE
		奥義名 = きけんなあそび
		奥義威力 = 0
		シールド回数 = 4
ENDSELECT


;奥義威力を0にした場合、通常の奥義処理は行われず奥義追加効果のみが行われる。
;全体攻撃にしたい時や、攻撃しない奥義を作る場合は0に設定のこと

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：なし
		詳細文文字列受け渡し変数 += @"<br>自身に（2ターン）の全体かばう効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［30％］（独自枠・{シールド回数}回or1ターン・消去不可）の被ダメージ減少効果を与える。"
		IF 奥義ランク == 2
			詳細文文字列受け渡し変数 += @"<br>［{防御アップ値}％］（独自枠・1ターン）の防御力上昇効果を与える。"
		ENDIF
		RETURN 奥義威力
	CASE "奥義前効果"
		;対象決定の後、ダメージよりも前に発生する効果。基本の奥義ダメージ部分は記載する必要はない。
		;奥義威力が0だとここの部分もスキップされる
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;基本的にアビテンプレートの関数を使用すること
		;テンプレート使用の例は固有アビリティ_キャラ名.ERBに記載
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 全体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 全体かばう
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = きけんなあそび
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果量 = 30
		アビ汎用変数:持続回数 = シールド回数
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		;消去不可だけどそもそも１ターンで消えます
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 奥義ランク == 2
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ枠 = いくらでもたたいていいのよ♪
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 防御アップ値
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT
