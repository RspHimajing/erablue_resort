
;1アビ
;☆元にした原作スキル
;「あぶないまなざし」
;効果時間中に近接物理攻撃を受けた場合、攻撃者に石化判定を与える

;とにかく叩かれてほしいので敵対心上昇させつつ
;カウンターで石化……が無いので麻痺付与にしました
;でもそれだと叩かれなくなるんじゃ？とも思う（原作時点での疑問）ので麻痺強度は低め

@固有アビ_2092_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 敵対心上昇値
#DIM カウンター回数

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = あぶないまなざし＋
	消費ＭＰ = 35
	敵対心上昇値 = 100
	カウンター回数 = 5
ELSE
	アビ名 = あぶないまなざし
	消費ＭＰ = 40
	敵対心上昇値 = 75
	カウンター回数 = 4
ENDIF

;敵対心の枠はアトラクトダンスと被るようにＣ（動き的には同じようなことしてると思うので）
SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身に［{敵対心上昇値}％］（Ｃ枠・3ターン）の敵対心上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（{カウンター回数}回or3ターン）のあぶないまなざし効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身に［{敵対心上昇値}％］（Ｃ枠・3ターン）の敵対心上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（{カウンター回数}回or3ターン）のあぶないまなざし効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>（あぶないまなざし："
		詳細文文字列受け渡し変数 += @"<br>　被ターゲット時、攻撃してきた対象に［30％］（通常枠・1ターン）の麻痺効果を与える。成功率40％。）"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 敵対心C
		アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
		アビ汎用変数:効果割合 = 敵対心上昇値
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = あぶないまなざし
		アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被タゲ時
		アビ汎用変数:持続回数 = カウンター回数
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@特殊反応_被タゲ時_あぶないまなざし(バフ番号, 攻撃者隊列, 被タゲ記録順)
#DIM 被タゲ記録順
#DIM 攻撃者隊列
#DIM バフ番号

CALL アビ対象選択テンプレート_指定(攻撃者隊列)

アビテンプレート用_アビ名 = あぶないまなざし

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 麻痺
アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
アビ汎用変数:基礎成功確率 = 50
アビ汎用変数:効果割合 = 30
アビ汎用変数:持続ターン = 1
CALL アビテンプレート_不利効果_デバフ効果セット


@バフ固有説明文_あぶないまなざし(バフ・デバフ効果量, バフ・デバフ効果割合)
#DIM バフ・デバフ効果量
#DIM バフ・デバフ効果割合
PRINTL 被ターゲット時、攻撃してきた対象に［30％］（通常枠・1ターン）の麻痺効果を与える。成功率50％。
RETURN 0


;2アビ
;☆元にした原作スキル
;「ナッシングネス！」
;対象の範囲に全ての力を失わせる虚無を作る
;
;「！」までが名前
;説明文に反してただのダメージ魔法でした
;力を失わせるくらいはやってほしいので累積攻撃デバフを追加しています

@固有アビ_2092_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 威力値

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = ナッシングネス！＋
	消費ＭＰ = 35
	威力値 = 250
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビ名 = ナッシングネス！
	消費ＭＰ = 40
	威力値 = 200
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>敵縦列に［攻撃力✕{威力値}％］の自属性ダメージを与える。"
		詳細文文字列受け渡し変数 += @"<br>［累積10％最大30％］（3ターン）の攻撃力減少効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 威力値
		CALL アビテンプレート_ダメージ処理_自属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_累積デバフ効果セット
ENDSELECT


;3アビ
;☆元にした原作スキル
;「ミスティックフェザー」
;自分を中心とした範囲内にいた者に、HP・MP・SP回復力上昇効果と、MP、SPの自然回復を一定時間発生させる。

;独自枠の代わりに効果量は低め（自己再生だけはそこそこのつもり）
;MP版再生は未実装のようなのでとりあえず見送り
;SP（スタミナ）自然回復は高揚あたりで解釈

@固有アビ_2092_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 再生効果割合
#DIM 回復力上昇割合

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	アビ名 = ミスティックフェザー
	消費ＭＰ = 120
	回復力上昇割合 = 20
	再生効果割合 = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>味方全体に［{回復力上昇割合}％］（独自枠・3ターン）の回復力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>［5％］（独自枠・3ターン）の高揚効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>自身に［回復力✕{再生効果割合}％］（独自枠・3ターン）の再生効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ミスティックフェザー_回復力
		アビ汎用文字列:バフ・デバフ対象能力 = 回復力
		アビ汎用変数:効果割合 = 回復力上昇割合
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ミスティックフェザー_高揚
		アビ汎用文字列:バフ・デバフ対象能力 = 高揚
		アビ汎用変数:効果割合 = 5
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = ミスティックフェザー_再生
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = RESULT * 再生効果割合 / 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



@固有ターン終了時処理_キャラ_2092
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 被ダメ判定

隊列番号 = キャラ隊列検索(GETCHARA(2092))

被ダメ判定 = 0
IF 戦闘中カウント:隊列番号:被ダメ合計 > 戦闘中カウント:隊列番号:0
	被ダメ判定 = 1
ENDIF

IF 被ダメ判定 == 1
	SELECTCASE RAND:3
		CASE 0
			KSTR:(K++) = 「ぃやったぁぁぁぁぁぁぁぁ！」
		CASE 1
			KSTR:(K++) = 「ありがとうございますっ！」
		CASE 2
			KSTR:(K++) = 「……ぉおんっ！」
	ENDSELECT

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 15
	CALL アビテンプレート_有利効果_奥義ゲージUP

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 1
	アビ汎用変数:持続ターン = 2
	CALL アビテンプレート_有利効果_テンション
ELSE
	SELECTCASE RAND:3
		CASE 0
			KSTR:(K++) = 「なんで私を叩かないの？
			KSTR:(K++) = 　いくじなし！　よわむし！　おくびょうもの！」
		CASE 1
			KSTR:(K++) = 「どうして無視するの？　怯えてるの？　躊躇しているの？」
		CASE 2
			KSTR:(K++) = 「私の事が好きなんじゃないの！？
			KSTR:(K++) = 　も、もももももしかして私の勘違いとか……」
	ENDSELECT

	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 10
	CALL アビテンプレート_不利効果_奥義ゲージDOWN
ENDIF

戦闘中カウント:隊列番号:0 = 戦闘中カウント:隊列番号:被ダメ合計



;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_2092(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『好きだから叩くんでしょう？』 □-----------------------------------------------
KSTR:(K++) = 　ダメージを受けた際、ターン終了時に奥義ゲージ［15％］アップ／
KSTR:(K++) = 　自身に［1］（2ターン）のテンション効果を与える。
KSTR:(K++) = 　ダメージを受けなかった際、ターン終了時に奥義ゲージ［10％］ダウン。

CALL 口上変数表示(対象キャラ, 1)
