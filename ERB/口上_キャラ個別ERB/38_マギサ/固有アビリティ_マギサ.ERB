;--------------------------------------------------
;固有アビリティ_マギサ
;--------------------------------------------------

@固有アビ_38_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 威力
#DIM 効果成功率
#DIMS アビ名

	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
		アビ名 = イラプション＋＋
		威力 = 280
		効果成功率 = 100
		消費ＭＰ = 80
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
		アビ名 = イラプション＋
		威力 = 250
		効果成功率 = 95
		消費ＭＰ = 70
	ELSE
		アビ名 = イラプション
		威力 = 200
		効果成功率 = 90
		消費ＭＰ = 60
	ENDIF

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕{威力}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［2％］（通常枠・3ターン）の火傷効果を与える。成功率{効果成功率}％。"

			RETURN 消費ＭＰ
		CASE "アビ効果"
			;攻撃部分
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 威力
			CALL アビテンプレート_ダメージ処理_自属性

			;火傷部分
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 2
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
	RETURN RESULT


@固有アビ_38_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 防御バフ値

	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 36
		アビ名 = ウォール＋＋
		消費ＭＰ = 60
		防御バフ値 = 60
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
		アビ名 = ウォール＋
		消費ＭＰ = 50
		防御バフ値 = 40
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
		アビ名 = ウォール
		消費ＭＰ = 30
	ELSE
		アビ名 = 
	ENDIF

	アビ汎用文字列:実行時メッセージ１ = 魔力の防壁を展開した！

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>味方全体の状態異常および弱体化を一度だけ防ぐ魔法防壁を展開する。"
			SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
				詳細文文字列受け渡し変数 += @"<br>［{防御バフ値}％］（独自枠・1回）の被ダメージ減少効果を与える。"

			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>味方全体に（1回）の弱体無効効果を与える。"
			SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
				詳細文文字列受け渡し変数 += @"<br>［{防御バフ値}％］（独自枠・1回）の被ダメージ減少効果を与える。"
		CASE "アビ効果"
			;弱体無効付与
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = 弱体無効
			アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット

			;防御バフ
			IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果割合 = 防御バフ値
				アビ汎用変数:持続回数 = 1
				アビ汎用文字列:バフ・デバフ枠 = ウォール_防御
				アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
				CALL アビテンプレート_有利効果_バフ効果セット
			ENDIF
	ENDSELECT
	RETURN RESULT


@固有アビ_38_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 威力
#DIM 効果成功率
#DIMS アビ名

	IF 基礎BATTLE_STATE:キャラ番号:Lv >= 44
		アビ名 = ヴァルプルギス＋＋
		威力 = 200
		効果成功率 = 90
		消費ＭＰ = 130
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
		アビ名 = ヴァルプルギス＋
		威力 = 150
		効果成功率 = 85
		消費ＭＰ = 100
	ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
		アビ名 = ヴァルプルギス
		威力 = 125
		効果成功率 = 80
		消費ＭＰ = 80
	ELSE
		アビ名 = 
	ENDIF

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>敵単体に［攻撃力✕{威力}％］の自属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［攻撃力✕{威力}％］の闇属性ダメージを与える。"
			詳細文文字列受け渡し変数 += @"<br>［50％］（独自枠・3ターン）の暗闇効果を与える。成功率{効果成功率}％。"
			詳細文文字列受け渡し変数 += @"<br>［50％］（独自枠・3ターン）の回避率減少効果を与える。成功率{効果成功率}％。"

			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵単体

			;自属性攻撃
			アビ汎用変数:効果割合 = 威力
			CALL アビテンプレート_ダメージ処理_自属性

			;闇属性攻撃
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 威力
			CALL アビテンプレート_ダメージ処理_闇属性

			;暗闇付与
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ヴァルプ_暗闇
			アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_不利効果_デバフ効果セット

			;回避率デバフ付与
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ヴァルプ_回避
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
	RETURN RESULT


@固有アビ_38_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

	隊列番号 = キャラ隊列検索(キャラ番号)

	IF 固有38_4_サンクション取得条件() == 1
		アビ名 = サンクション
		消費ＭＰ = 120
	ELSE
		アビ名 = 
	ENDIF

	SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:1 > 0
		アビテンプレート用_アビ封印用フラグ = 1

	アビ汎用文字列:実行時メッセージ１ = 召喚に応じ、魔神モラクスがその巨体を現す！

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>魔神モラクスを召喚する。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティは1戦闘中1回しか使えない。"
			RETURN 消費ＭＰ
		CASE "アビ説明文_詳細"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>自身に［累積5％最大99999％］（独自枠・消去不可）の攻撃力上昇効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>［100％］（独自枠・消去不可）の追撃効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>［100］（独自枠・消去不可）の自傷効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>ターン終了時、［累積5％最大99999％］（独自枠・消去不可）の攻撃力上昇が発生する効果を与える。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティは1戦闘中1回しか使えない。"
		CASE "アビ効果"
			;モラクスの「攻撃」「追撃」「自傷」は3つセットで扱うこと
			CALL アビ対象選択テンプレート_自己のみ
			戦闘中カウント:キャラ隊列検索(GETCHARA(38)):2 = 現在ターン数

			;攻撃バフ
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = モラクス_攻撃
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:効果割合 = 5
			アビ汎用変数:累積上限_固定値 = 99999
			CALL アビテンプレート_有利効果_累積バフ効果セット

			;追撃バフ
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = モラクス_追撃
			アビ汎用文字列:バフ・デバフ対象能力 = 追撃
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_有利効果_バフ効果セット

			;自傷デバフ
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = モラクス_自傷
			アビ汎用文字列:バフ・デバフ対象能力 = 自傷
			アビ汎用変数:持続ターン = -1
			アビ汎用変数:効果量 = 100
			アビ汎用変数:弱体耐性貫通オプション = 1
			CALL アビテンプレート_不利効果_デバフ効果セット
	
			戦闘中カウント:キャラ隊列検索(GETCHARA(38)):1 = 1
	ENDSELECT
	RETURN RESULT


;ターン終了時処理関数から呼び出し
@固有ターン終了時処理_キャラ_38
#DIM バフ・デバフ行数
#DIM 隊列番号

	隊列番号 = キャラ隊列検索(GETCHARA(38))

	バフ・デバフ行数 = バフ番号取得_枠("モラクス_攻撃", 隊列番号)
	IF バフ・デバフ行数 > -1
		アビ汎用文字列:実行時メッセージ１ = 魔神モラクスが力を増していく……！

		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:消去不可オプション = 1
		アビ汎用文字列:バフ・デバフ枠 = モラクス_攻撃
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:効果割合 = 5
		アビ汎用変数:累積上限_固定値 = 99999
		CALL アビテンプレート_有利効果_累積バフ効果セット
	ENDIF


@固有アビ_38_5(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号

	隊列番号 = キャラ隊列検索(キャラ番号)

	IF 固有38_4_サンクション取得条件() == 1
		アビ名 = リベレイション
		消費ＭＰ = 120
	ELSE
		アビ名 = 
	ENDIF

	SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:1 != 1
		アビテンプレート用_アビ封印用フラグ = 1

	アビ汎用文字列:実行時メッセージ１ = 蓄積された魔力が一気に解放される……！

	SELECTCASE ARGS
		CASE "アビ名"
			TSTR:コマンド名受渡 = %アビ名%
		CASE "アビ説明文"
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
			詳細文文字列受け渡し変数 += @"<br>召喚中の魔神モラクスの力を解放する。"
			IF 隊列番号 > -1
				詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕魔神モラクスを維持していたターン（{現在ターン数 - 戦闘中カウント:隊列番号:2}）✕40％］の自属性ダメージを与える。"
			ELSE
				詳細文文字列受け渡し変数 += @"<br>敵全体に［攻撃力✕魔神モラクスを維持していたターン✕40％］の自属性ダメージを与える。"
			ENDIF
			詳細文文字列受け渡し変数 += @"<br>アビリティ「サンクション」によって得た効果を全て消去する。"
			詳細文文字列受け渡し変数 += @"<br>このアビリティはサンクション使用後のみ、1戦闘中1回しか使えない。"
			RETURN 消費ＭＰ
		CASE "アビ効果"
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 40 * (現在ターン数 - 戦闘中カウント:隊列番号:2)
			CALL アビテンプレート_ダメージ処理_自属性

			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:消去不可バフデバフ削除オプション = 1
			CALL アビテンプレート_不利効果_バフ消去("モラクス_追撃")
			CALL アビテンプレート_不利効果_バフ消去("モラクス_攻撃")
			CALL アビテンプレート_有利効果_デバフ消去("モラクス_自傷")

			戦闘中カウント:キャラ隊列検索(GETCHARA(38)):1 += 1
	ENDSELECT
	RETURN RESULT


;特殊処理：サンクション取得チェック
@固有38_4_サンクション取得条件()
#FUNCTION

	;サンクション習得条件を突破していたら１を返す
	SIF イベント完了フラグ確認("魔女と晩酌") > 0
		RETURNF 1

	;覚えてなかったらゼロを返す
	RETURNF 0

