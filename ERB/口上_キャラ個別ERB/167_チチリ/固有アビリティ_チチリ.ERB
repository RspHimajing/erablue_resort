
@固有アビ_167_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
	アビ名 = 正鵠衝++
	消費ＭＰ = 125
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = 正鵠衝+
	消費ＭＰ = 125
ELSE
	アビ名 = 正鵠衝
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に［攻撃力✕400％］の火属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>［300］（通常枠・3ターン）の被ダメージ上昇効果を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			詳細文文字列受け渡し変数 += @"<br>自身に［1000％］（Ａ枠・2ターン）のTA率上昇効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 400
		CALL アビテンプレート_ダメージ処理_火属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 300
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ上昇
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 56
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 1000
			アビ汎用文字列:バフ・デバフ枠 = TA率A
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:持続ターン = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT



@固有アビ_167_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
	アビ名 = 柘榴乱舞++
	消費ＭＰ = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = 柘榴乱舞+
	消費ＭＰ = 80
ELSE
	アビ名 = 柘榴乱舞
	消費ＭＰ = 100
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［攻撃力✕100％］の火属性ダメージを8回与える。
		詳細文文字列受け渡し変数 += @"<br>［1］のスロウ効果を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			詳細文文字列受け渡し変数 += @"<br>味方全体に［10％］の奥義ゲージ増加を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		FOR LOCAL, 0, 8
			CALL アビテンプレート_ダメージ処理_火属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 1
		CALL アビテンプレート_不利効果_スロウ
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 72
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 10
			CALL アビテンプレート_有利効果_奥義ゲージUP
		ENDIF
ENDSELECT



@固有アビ_167_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
#DIM バフ・デバフ行数

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 鎮守の威光+
	消費ＭＰ = 300
ELSE
	アビ名 = 鎮守の威光
	消費ＭＰ = 300
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に［1］（通常枠・5ターン）の最大CT増加効果を与える。
		詳細文文字列受け渡し変数 += @"<br>［100％］（1ターン・消去不可）の圧伏効果を与える。"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			詳細文文字列受け渡し変数 += @"<br>（2ターン）の恐怖効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:バフ・デバフ枠 = 最大ＣＴ増加
		アビ汎用文字列:バフ・デバフ対象能力 = 最大ＣＴ増加
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 圧伏
		アビ汎用文字列:バフ・デバフ対象能力 = 封印
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 80
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 恐怖
			アビ汎用文字列:バフ・デバフ対象能力 = 恐怖
			アビ汎用変数:効果量 = 0
			アビ汎用変数:持続ターン = 2
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT



@固有戦闘開始時処理_キャラ_167
;戦闘開始時処理関数から呼び出し
#DIM 隊列番号

隊列番号 = キャラ隊列検索(GETCHARA(167))

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果割合 = 10
アビ汎用文字列:バフ・デバフ枠 = 赤き火炎の鉞
アビ汎用文字列:バフ・デバフ名 = 赤き火炎の鉞
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビテンプレ変数リセット

@固有ターン終了時処理_キャラ_167
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 被ダメ判定

隊列番号 = キャラ隊列検索(GETCHARA(167))

被ダメ判定 = 0
IF 戦闘中カウント:隊列番号:被ダメ合計 > 戦闘中カウント:隊列番号:0
	被ダメ判定 = 1
ENDIF

IF 被ダメ判定 == 1
	WSTR:(K++) = 「ざぁ～こ_H_」
	アビテンプレート用_アビ名 = 赤き火炎の鉞

	CALL アビ対象選択テンプレート_敵ランダムＸ体(4)
	アビ汎用変数:効果割合 = 200
	CALL アビテンプレート_ダメージ処理_火属性
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:効果量 = 15
	CALL アビテンプレート_有利効果_奥義ゲージUP
ENDIF

戦闘中カウント:隊列番号:0 = 戦闘中カウント:隊列番号:被ダメ合計



@バフ・デバフ特殊表示_赤き火炎の鉞(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 赤き火炎の鉞
PRINTFORML 敵のターゲットにされやすいが、アビリティ与ダメージが上昇/
PRINTFORML ダメージを受けた際、ターン終了時にターゲットに関わらず［攻撃力✕200％］の4回火属性ダメージ/自身の奥義ゲージが上昇する状態［永続/消去不可］

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_167(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『赤き火炎の鉞』 □------------------------------------------------------------
KSTR:(K++) = 　敵のターゲットにされやすいが、アビリティ与ダメージアップ
KSTR:(K++) = 　◆ダメージを受けた際、ターン終了時にターゲットに関わらず4回火属性ダメージ/自身の奥義ゲージアップ

CALL 口上変数表示(対象キャラ, 1)



