
@固有アビ_64_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = 死ト愛ノ世界+
	消費ＭＰ = 100
ELSE
	アビ名 = 死ト愛ノ世界
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>フィールドを（6ターン）の死ト愛ノ世界に染め上げる
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>フィールドを（3ターン）の死ト愛ノ世界に染め上げる
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>＞敵味方全体に［20％］の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞味方全体に［30％］の追撃効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:フィールド枠 = 死ト愛ノ世界
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
			アビ汎用変数:持続ターン = 6
		ELSE
			アビ汎用変数:持続ターン = 3
		ENDIF
		CALL アビテンプレート_フィールド効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 死ト愛ノ世界_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 20
		アビ汎用文字列:適用範囲 = 敵味方全体
		アビ汎用文字列:付随先_隊列設定 = 戦闘行動キャラ
		CALL アビテンプレート_有利効果_付随バフ効果セット("死ト愛ノ世界")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 死ト愛ノ世界_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 30
		アビ汎用文字列:適用範囲 = 味方全体
		アビ汎用文字列:付随先_隊列設定 = 戦闘行動キャラ
		CALL アビテンプレート_有利効果_付随バフ効果セット("死ト愛ノ世界")
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有アビ_64_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = 愛シイ人+
	消費ＭＰ = 175
ELSE
	アビ名 = 愛シイ人
	消費ＭＰ = 200
ENDIF

IF 隊列番号 > -1 && 刻印現在数確認("愛ノ救イ", 隊列番号) < 2
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界"
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

SIF 隊列番号 > -1 && バフ番号取得_枠("愛シイ人", 隊列番号) > -1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方単体と自身に（4ターン・消去不可）の愛シイ人効果を与える。
		詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、自身の愛ノ救イが［2］減少する。"
		詳細文文字列受け渡し変数 += @"<br>既に愛ノ救イ効果を得ている時、このアビリティは使用出来ない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方単体と自身に（独自枠・4ターン・消去不可）の愛シイ人効果を与える。
		詳細文文字列受け渡し変数 += @"<br>＞［999％］のTA率上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［50％］の被ダメージ減少効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、自身の愛ノ救イが［2］減少する。"
		詳細文文字列受け渡し変数 += @"<br>既に愛ノ救イ効果を得ている時、このアビリティは使用出来ない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 愛シイ人
		アビ汎用文字列:バフ・デバフ対象能力 = 愛シイ人
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 愛シイ人_絶対TA
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 999
		CALL アビテンプレート_有利効果_付随バフ効果セット("愛シイ人")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 愛シイ人_被ダメ軽減
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("愛シイ人")
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 愛シイ人
		アビ汎用文字列:バフ・デバフ対象能力 = 愛シイ人
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 愛シイ人_絶対TA
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 999
		CALL アビテンプレート_有利効果_付随バフ効果セット("愛シイ人")
		
		IF !(陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界")
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
			CALL アビテンプレート_刻印減少(2)
		ENDIF
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有アビ_64_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
	アビ名 = 終焉ノ愛
	消費ＭＰ = 100

IF 隊列番号 > -1 && 刻印現在数確認("愛ノ救イ", 隊列番号) < 1
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

IF 隊列番号 > -1 && 刻印現在数確認("愛ノ救イ", 隊列番号) < 1
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界"
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:1 == 1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方単体に（通常枠・1回）の奥義再発動効果を与える。
		詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、自身の愛ノ救イが［1］減少する。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは1ターン中1回しか使えない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 奥義再発動
		アビ汎用文字列:バフ・デバフ対象能力 = 奥義再発動
		アビ汎用変数:持続回数 = 1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		IF !(陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界")
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
			CALL アビテンプレート_刻印減少(1)
		ENDIF
		戦闘中カウント:戦闘行動キャラ:1 = 1
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有アビ_64_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 永久ノ誓イ
	消費ＭＰ = 300
ENDIF

IF 隊列番号 > -1 && 刻印現在数確認("愛ノ救イ", 隊列番号) < 4
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界"
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:2 == 1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（独自枠・消去不可）の永久ノ誓イ効果を与える。
		詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、自身の愛ノ救イが［4］減少する。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは1戦闘中1回しか使えない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
		RETURN 消費ＭＰ
	CASE "アビ説明文_詳細"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に（独自枠・消去不可）の永久ノ誓イ効果を与える。
		詳細文文字列受け渡し変数 += @"<br>＞［50％］の攻撃力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞［50％］の防御力上昇効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>＞弱体無効効果を与える。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティを使用した時、自身の愛ノ救イが［4］減少する。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティは1戦闘中1回しか使えない。"
		詳細文文字列受け渡し変数 += @"<br>このアビリティでは行動回数を消費しない。"
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 永久ノ誓イ
		アビ汎用文字列:バフ・デバフ対象能力 = 永久ノ誓イ
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 永久ノ誓イ_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("永久ノ誓イ")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 永久ノ誓イ_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_有利効果_付随バフ効果セット("永久ノ誓イ")

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 永久ノ誓イ_弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		CALL アビテンプレート_有利効果_付随バフ効果セット("永久ノ誓イ")

		IF !(陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界")
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
			CALL アビテンプレート_刻印減少(4)
		ENDIF
		
		戦闘中カウント:戦闘行動キャラ:2 = 1
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有戦闘開始時処理_キャラ_64
#DIM バフ・デバフ行数
#DIM ニーア番号
#DIM 隊列番号

ニーア番号 = GETCHARA(64)
隊列番号 = キャラ隊列検索(ニーア番号)

IF 陥落チェック(ニーア番号)
	アビ汎用文字列:実行時メッセージ１ = 「どこまでも……あなたと共に！」
ELSE
	アビ汎用文字列:実行時メッセージ１ = 「あなたも私を否定するの……？」
ENDIF

;愛ノ救イ再付与用
戦闘中カウント:戦闘行動キャラ:0 = 1

CALL アビ対象選択テンプレート_自己のみ

アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
アビ汎用変数:累積上限_固定値 = 13
CALL アビテンプレート_刻印追加(13)

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 愛ノ救イ_被ダメ軽減
アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
アビ汎用変数:効果割合 = 90
CALL アビテンプレート_有利効果_付随バフ効果セット("愛ノ救イ")

アビテンプレート用_アビ名 = 恋人の正位置

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_味方全体_生者のみ
アビ汎用文字列:バフ・デバフ枠 = 渇望
アビ汎用文字列:バフ・デバフ対象能力 = 自動復活
アビ汎用変数:効果割合 = 50
アビ汎用変数:持続回数 = 1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_バフ効果セット

IF 陥落チェック(ニーア番号) && 基礎BATTLE_STATE:ニーア番号:Lv >= 75
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 恋人の正位置
	アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
	アビ汎用変数:持続回数 = 1
	アビ汎用変数:消去不可オプション = 1
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF



@固有通常攻撃後処理_バフ_永久ノ誓イ(バフ・デバフ行数, 全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ
#DIM バフ・デバフ行数

SIF 全滅フラグ
	RETURN 0

アビテンプレート用_アビ名 = 永久ノ誓イ

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 200
FOR LOCAL, 0, 6
	CALL アビテンプレート_ダメージ処理_闇属性
NEXT


@固有ターン終了時処理_キャラ_64
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号

IF BATTLE_STATE:戦闘行動キャラ:ＨＰ > 0
	;行動後に愛ノ救イ以外で倒れて自動復活なら減らない
	IF 戦闘中カウント:戦闘行動キャラ:0 == 2
		戦闘中カウント:戦闘行動キャラ:0 = 1
	ELSEIF 戦闘中カウント:戦闘行動キャラ:0
		IF 刻印現在数確認("愛ノ救イ", 戦闘行動キャラ) > 2
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
			CALL アビテンプレート_刻印減少(2)
		ELSEIF 刻印現在数確認("愛ノ救イ", 戦闘行動キャラ) <= 2 && BATTLE_STATE:戦闘行動キャラ:ＨＰ > 0
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
			CALL アビテンプレート_刻印減少(999)
			BATTLE_STATE:戦闘行動キャラ:ＨＰ = 0
			WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)%は倒れた！
			CALL 戦闘不能者登録(戦闘行動キャラ, -1)
		ENDIF
	ELSE
		戦闘中カウント:戦闘行動キャラ:0 = 1
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
		アビ汎用変数:累積上限_固定値 = 13
		CALL アビテンプレート_刻印追加(13)
	ENDIF
ENDIF
戦闘中カウント:戦闘行動キャラ:1 = 0



@固有戦闘不能時処理_キャラ_64()
#DIM 対象番号
#DIM 永久ノ誓イ
#DIM 渇望
#DIM 自動復活

永久ノ誓イ = バフ番号取得_枠("永久ノ誓イ", 戦闘行動キャラ)
自動復活 = バフ番号取得("自動復活", 戦闘行動キャラ)

IF 永久ノ誓イ > -1
	CALL 戦闘効果削除(永久ノ誓イ)
ENDIF

IF 刻印現在数確認("愛ノ救イ", 戦闘行動キャラ) > 0
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 残執
	アビ汎用文字列:バフ・デバフ対象能力 = 残執
	アビ汎用変数:持続ターン = 4
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット
	
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 残執
	アビ汎用文字列:バフ・デバフ対象能力 = 残執
	アビ汎用変数:持続ターン = 4
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 残執_防御力
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用変数:効果割合 = 50
	アビ汎用文字列:適用範囲 = 敵味方全体
	CALL アビテンプレート_不利効果_付随デバフ効果セット("残執")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 残執_連撃率
	アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
	アビ汎用変数:効果量 = 100
	アビ汎用文字列:適用範囲 = 敵味方全体
	CALL アビテンプレート_不利効果_付随デバフ効果セット("残執")

	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 残執_強圧
	アビ汎用文字列:バフ・デバフ対象能力 = 防御力
	アビ汎用文字列:適用範囲 = 敵味方全体
	CALL アビテンプレート_不利効果_付随デバフ効果セット("残執")
ENDIF

IF BATTLE_STATE:戦闘行動キャラ:ＨＰ > 0
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ対象能力 = 愛ノ救イ
	アビ汎用変数:累積上限_固定値 = 13
	CALL アビテンプレート_刻印追加(13)
	SIF BATTLE_STATE:戦闘行動キャラ:行動済みフラグ
		戦闘中カウント:戦闘行動キャラ:0 = 2
ELSE
	戦闘中カウント:戦闘行動キャラ:0 = 0
ENDIF

@探索欄表示_キャラ_64(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "愛ノ救イ:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"♥{刻印現在数確認("愛ノ救イ", 隊列番号)}", 200, 90



@バフ・デバフ特殊表示_愛シイ人(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 愛シイ人
PRINTFORML 必ずトリプルアタックを行い、被ダメージを軽減する状態［消去不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_永久ノ誓イ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 永久ノ誓イ
PRINTFORML 攻撃力・防御力が上昇/弱体効果を無効化/通常攻撃時に6回闇属性追加ダメージを与える状態［消去不可］
PRINTFORML ◆ニーアが戦闘不能時、乗機に永久ノ誓イ効果
PRINTFORML 持続ターン：永続



@バフ・デバフ特殊表示_愛ノ救イ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 愛ノ救イ
PRINTFORML 毎ターン2減少し、0になったターン終了時に戦闘不能になるが、被ダメージを軽減する状態［消去不可］
PRINTFORML カウント：{刻印現在数確認("愛ノ救イ", 隊列番号)}



@バフ・デバフ特殊表示_渇望(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 渇望
PRINTFORML 戦闘不能になった時、1度だけ復活する状態［消去不可］
PRINTFORML 持続回数：1



@バフ・デバフ特殊表示_残執(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 残執
PRINTFORML 防御力・連続攻撃率が大幅に減少/HPが回復できない状態［回復不可］
PRINTFORML 持続ターン：{DT_CELL_GET("戦闘効果データベース", バフ・デバフ行数, "持続ターン")}



@フィールド説明文_死ト愛ノ世界
#DIM 配列番号

配列番号 = バフ・デバフ効果検索("フィールド", "枠", "死ト愛ノ世界", -1)

LOCALS = 死ト愛ノ世界<br>
LOCALS += "敵味方の攻撃力が上昇/味方全体が通常攻撃時に追撃を行う状態<br>"
LOCALS += @"効果ターン：{DT_CELL_GET("戦闘効果データベース", 配列番号, "持続ターン")}ターン<br>"

詳細文文字列受け渡し変数 = %LOCALS%

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_64(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『愛ヲノゾム意思』 □-----------------------------------------------------------------
KSTR:(K++) = 　戦闘開始時、自身に愛ノ救イ効果［永続/消去不可］
KSTR:(K++) = 　◆愛ノ救イ効果以外で戦闘不能になった時、敵味方全体に残執効果［回復不可］
KSTR:(K++) = 　
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = 　
	KSTR:(K++) = □ 特性：『死の抱擁』 □-----------------------------------------------------------------
	KSTR:(K++) = 　死ト愛ノ世界の展開時間を［3ターン］延長
	KSTR:(K++) = 　死ト愛ノ世界展開中、アビリティ発動時に愛ノ救イが減少しない
	KSTR:(K++) = 　
ENDIF
KSTR:(K++) = □ 特性：『恋人の正位置』 □----------------------------------------------------------
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 75
	KSTR:(K++) = 　メインメンバー編成時、戦闘開始時に味方全体に渇望効果［永続/消去不可］/弱体効果無効効果［1回］
ELSE
	KSTR:(K++) = 　メインメンバー編成時、戦闘開始時に味方全体に渇望効果［永続/消去不可］
ENDIF
KSTR:(K++) = 　◆再発動不可
KSTR:(K++) = 　
;保留中
;IF 基礎BATTLE_STATE:対象キャラ:Lv >= 70
;	KSTR:(K++) = □ 特性：『至賢の領域・恋人と死』 □--------------------------------------------------
;	KSTR:(K++) = 　搭載兵器『ペイン・オブ・デス』使用時、愛ノ救イが6上昇
;	KSTR:(K++) = 　
;ENDIF

CALL 口上変数表示(対象キャラ, 1)


