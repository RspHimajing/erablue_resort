;-------------------------------------------------
;配列にアイテム名と値段放り込んでこれを呼ぶとお店になる処理
;完全に独立しているのでリゾートだろうがダンジョンだろうが呼べる
;
;汎用アイテム購入用_品揃え登録:0 = 薬草,200,ルピ
;のように記述する
;-------------------------------------------------
@汎用アイテム購入画面処理(売却許可フラグ = 1)
#DIMS 商売文字列保存, 3
#DIM 選択アイテム番号
#DIM 購入個数
#DIM 売却許可フラグ
#DIM 売却モード
#DIM 売価
#DIM アイテムフラグ

VARSET LOCALS
選択アイテム番号 = -1
購入個数 = 1
売却モード = 0

$購入画面LOOP

IF 汎用アイテム購入用_序文 != ""
	DRAWLINE
	HTML_PRINT 汎用アイテム購入用_序文
	DRAWLINE
ENDIF

;枠作成
LOCALS = <div rect='81,31,1937,381' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='81,444,1937,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='2050,444,3125,2812' border='31' bcolor='#C0C0C0'></div>"
IF 売却許可フラグ
	LOCALS += "<div rect='2050,31,3125,381' border='31' bcolor='#C0C0C0'></div>"
	LOCALS += "<div rect='2125,125,3125,381'>"
	IF 売却モード == 0
		;購入画面
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■購入</font>"
		LOCALS += "　　　<button value='1100'>■売却</button>"
	ELSE
		;売却画面
		LOCALS += @"<button value='1100'>■購入</button>"
		LOCALS += @"　　　<font color='#%カラーパレット_HTML("黄")%'>■売却</font>"
	ENDIF
	LOCALS += "</div>"
ENDIF

LOCALS:2 = 所持ルピ：%NUM_FORMAT(MONEY)%<br>
LOCALS:2 += @"所持ZP　：%ZP所持量全文字列()%"
LOCALS += @"<div rect='156,125,1862,312'>%LOCALS:2%</div>"

IF 売却モード == 0
	;購入画面

	;品揃え描写
	LOCALS:1 = 
	FOR LOCAL, 0, 100
		SIF 汎用アイテム購入用_品揃え登録:LOCAL == ""
			BREAK
		VARSET 商売文字列保存
		SPLIT 汎用アイテム購入用_品揃え登録:LOCAL, ",", 商売文字列保存
		IF 商売文字列保存:1 == ""
			;特殊値段を設定されてない場合
			TRYCCALLFORM アイテム個別文章表示_%商売文字列保存:0%("値段")
				売価 = RESULT
				商売文字列保存:1 = %NUM_FORMAT(売価)%
				商売文字列保存:2 = %詳細文文字列受け渡し変数%
			CATCH
				;アイテムにない場合素材を見る
				CALLFORM 素材性能記述_%商売文字列保存:0%
				売価 = (素材性能_素材ランク) * (素材性能_素材ランク) * 500 + 300
				商売文字列保存:1 = %NUM_FORMAT(売価)%
				商売文字列保存:2 = ルピ
			ENDCATCH
		ELSE
			売価 = TOINT(商売文字列保存:1)
			商売文字列保存:1 = %NUM_FORMAT(売価)%
		ENDIF
		LOCALS:1 += @"<button value='{LOCAL}'>[{LOCAL, 2}]%商売文字列保存:0, 20, LEFT%%商売文字列保存:1, 6%%商売文字列保存:2%</button>"
		LOCALS:1 += "<br>"
	NEXT
	LOCALS += @"<div rect='156,537,1937,2687'>%LOCALS:1%</div>"

	LOCALS += "<div rect='1100,3081,825,125'><button value='999'>[999] 店から出る</button></div>"

	IF 選択アイテム番号 > -1
		;アイテム詳細文章
		VARSET 商売文字列保存
		SPLIT 汎用アイテム購入用_品揃え登録:選択アイテム番号, ",", 商売文字列保存
		詳細文文字列受け渡し変数 = 
		TRYCCALLFORM アイテム個別文章表示_%商売文字列保存:0%
			アイテムフラグ = 1
			LOCALS:3 = %詳細文文字列受け渡し変数%<br>
			LOCALS:3 += @"購入額：{購入個数, 2}個 / "
		CATCH
			;アイテムにない場合素材を見る
			アイテムフラグ = 0
			CALLFORM 素材性能記述_%商売文字列保存:0%
			LOCALS:3 = ■%商売文字列保存:0%　　　所持数:{素材アイテム数量算出(商売文字列保存:0)}<br>
			LOCALS:3 += @"　カテゴリ：%素材性能_カテゴリ%<br>"
			LOCALS:3 += @"　%素材性能_属性%属性<br>"
			LOCALS:3 += @"　ランク{素材性能_素材ランク}<br>"
			IF 素材性能_カテゴリ == "食物素材"
				LOCALS:3 += @"　食材分類：%素材性能_食材分類%<br>"
			ELSEIF 素材性能_カテゴリ == "贈答アイテム"
				LOCALS:3 += @"　プレゼント種別：%素材性能_プレゼント%<br>"
			ENDIF
			LOCALS:3 += "<br>"
			LOCALS:3 += @"購入額：{購入個数, 2}個 / "
		ENDCATCH
		IF 商売文字列保存:1 == ""
			;特殊値段を設定されてない場合
			詳細文文字列受け渡し変数 = 
			TRYCCALLFORM アイテム個別文章表示_%商売文字列保存:0%("値段")
				LOCALS:3 += @"{RESULT * 購入個数, 6}%詳細文文字列受け渡し変数%<br><br>"
			CATCH
				;アイテムにない場合素材を見る
				CALLFORM 素材性能記述_%商売文字列保存:0%
				売価 = (素材性能_素材ランク) * (素材性能_素材ランク) * 500 + 300
				LOCALS:3 += @"{売価 * 購入個数, 6}ルピ<br><br>"
			ENDCATCH
		ELSE
			LOCALS:3 += @"{TOINT(商売文字列保存:1) * 購入個数, 6}%商売文字列保存:2%<br><br>"
		ENDIF
		IF アイテムフラグ && DT_CELL_GET("所持アイテムデータベース", 0, 商売文字列保存:0) >= 127
			LOCALS:3 += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>　　　　　"
			LOCALS:3 += @"　このアイテムを%TOFULL(TOSTR(購入個数)), 4%個購入する　"
			LOCALS:3 += @"（所持上限）</font>"
		ELSE
			LOCALS:3 += @"<button value='1011'>[+10]</button> "
			LOCALS:3 += @"<button value='1001'>[+1]</button>"
			LOCALS:3 += @"　<button value='1000'>このアイテムを%TOFULL(TOSTR(購入個数)), 4%個購入する</button>　"
			LOCALS:3 += @"<button value='1002'>[-1]</button> "
			LOCALS:3 += @"<button value='1012'>[-10]</button>"
		ENDIF
		LOCALS += @"<div rect='2125,537,3800,2687'>%LOCALS:3%</div>"
	ENDIF
ELSE
	;売却画面
	
	;品揃え描写
	VARSET 表示アイテム名
	LOCALS:1 = 
	DT_COLUMN_NAMES "所持アイテムデータベース", 表示アイテム名
	FOR LOCAL, 1, 25
		LOCAL:1 = DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:LOCAL)
		IF LOCAL:1 > 0
			CALLFORM アイテム個別文章表示_%表示アイテム名:LOCAL%("値段")
			LOCALS:1 += @"<button value='{LOCAL}'>[{LOCAL, 2}]%表示アイテム名:LOCAL, 20, LEFT%{RESULT / 2, 6}%詳細文文字列受け渡し変数%</button><br>"
		ENDIF
	NEXT
	SIF LOCALS:1 == ""
		選択アイテム番号 = -1

	LOCALS += @"<div rect='156,537,1937,2687'>%LOCALS:1%</div>"

	LOCALS += "<div rect='1100,3081,825,125'><button value='999'>[999] 店から出る</button></div>"

	IF 選択アイテム番号 > -1
		;アイテム詳細文章
		TRYCALLFORM アイテム個別文章表示_%表示アイテム名:選択アイテム番号%
		LOCALS:3 = %詳細文文字列受け渡し変数%<br>
		TRYCALLFORM アイテム個別文章表示_%表示アイテム名:選択アイテム番号%("値段")
		LOCALS:3 += @"売却額：{購入個数, 2}個 / {RESULT / 2 * 購入個数, 6}ルピ<br><br>"
		LOCALS:3 += @"<button value='1011'>[+10]</button> "
		LOCALS:3 += @"<button value='1001'>[+1]</button>"
		LOCALS:3 += @"　<button value='1000'>このアイテムを%TOFULL(TOSTR(購入個数)), 4%個売却する</button>　"
		LOCALS:3 += @"<button value='1002'>[-1]</button> "
		LOCALS:3 += @"<button value='1012'>[-10]</button>"
		LOCALS += @"<div rect='2125,537,3800,2687'>%LOCALS:3%</div>"
	ENDIF
ENDIF

HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 29
	PRINTL
NEXT
BINPUT

SELECTCASE RESULT
	CASE 0 TO 99
		選択アイテム番号 = RESULT
		購入個数 = 1
	CASE 999
		汎用アイテム購入用_序文 = 
		購入個数 = 1
		選択アイテム番号 = -1
		VARSET 汎用アイテム購入用_品揃え登録
		RETURN 0
	CASE 1011
		IF 売却モード == 0
			IF アイテムフラグ
				購入個数 = MIN(購入個数 + 10, MIN(99, 127 - DT_CELL_GET("所持アイテムデータベース", 0, 商売文字列保存:0)))
			ELSE
				購入個数 = MIN(購入個数 + 10, 99)
			ENDIF
		ELSE
			購入個数 = MIN(購入個数 + 10, DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:選択アイテム番号)) 
		ENDIF
	CASE 1001
		IF 売却モード == 0
			IF アイテムフラグ
				購入個数 = MIN(購入個数 + 1, MIN(99, 127 - DT_CELL_GET("所持アイテムデータベース", 0, 商売文字列保存:0)))
			ELSE
				購入個数 = MIN(購入個数 + 1, 99)
			ENDIF
		ELSE
			購入個数 = MIN(購入個数 + 1, DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:選択アイテム番号)) 
		ENDIF
	CASE 1002
		購入個数 = MAX(購入個数 - 1, 1)
	CASE 1012
		購入個数 = MAX(購入個数 - 10, 1)
	CASE 1100
		INVERTBIT 売却モード, 0
		購入個数 = 1
		選択アイテム番号 = -1
		GOTO 購入画面LOOP
	CASE 1000
		IF 売却モード == 0
			VARSET 商売文字列保存
			SPLIT 汎用アイテム購入用_品揃え登録:選択アイテム番号, ",", 商売文字列保存
			IF 商売文字列保存:2 == "ルピ"
				売価 = TOINT(商売文字列保存:1)
				IF MONEY < 売価 * 購入個数
					PRINTW ルピが足りません
					GOTO 購入画面LOOP
				ENDIF
				MONEY -= 売価 * 購入個数
			ELSEIF 商売文字列保存:2 == "ZP"
				売価 = TOINT(商売文字列保存:1)
				IF FLAG:ZP所持量 < 売価 * 購入個数
					PRINTW ZPが足りません
					GOTO 購入画面LOOP
				ENDIF
				FLAG:ZP所持量 -= 売価 * 購入個数
			ELSE
				;特殊値段を設定されてない場合
				TRYCCALLFORM アイテム個別文章表示_%商売文字列保存:0%("値段")
					売価 = RESULT
				CATCH
					CALLFORM 素材性能記述_%商売文字列保存:0%
					売価 = (素材性能_素材ランク) * (素材性能_素材ランク) * 500 + 300
				ENDCATCH
				IF MONEY < 売価 * 購入個数
					PRINTW ルピが足りません
					GOTO 購入画面LOOP
				ENDIF
				MONEY -= 売価 * 購入個数
			ENDIF
			IF EXISTFUNCTION(@"アイテム個別文章表示_%商売文字列保存:0%")
				CALL アイテム増減汎用処理(商売文字列保存:0, 購入個数)
			ELSE
				CALL 素材入手処理(商売文字列保存:0, 購入個数)
			ENDIF
			PRINTFORMW %商売文字列保存:0%を%TOFULL(TOSTR(購入個数)), 4%個購入しました。
			購入個数 = 1
		ELSE
			CALL アイテム増減汎用処理(表示アイテム名:選択アイテム番号, 購入個数 * -1)
			PRINTFORMW %表示アイテム名:選択アイテム番号%を%TOFULL(TOSTR(購入個数)), 4%個売却しました。
			CALLFORM アイテム個別文章表示_%表示アイテム名:選択アイテム番号%("値段")
			IF 詳細文文字列受け渡し変数 == "ルピ"
				MONEY += RESULT * 購入個数 / 2
			ELSEIF 詳細文文字列受け渡し変数 == "ZP"
				FLAG:ZP所持量 += RESULT * 購入個数 / 2
			ENDIF
			購入個数 = 1
		ENDIF
ENDSELECT

GOTO 購入画面LOOP
