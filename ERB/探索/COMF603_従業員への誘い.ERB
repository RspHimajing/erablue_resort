;-------------------------------------------------
;従業員への誘い
;-------------------------------------------------
@COMNAME603
#FUNCTIONS
TSTR:コマンド名受渡 = 従業員への誘い
IF TALENT:TARGET:店持ち
	TSTR:コマンド名受渡 = 公式店舗の誘い
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_603
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("探索")

@COM_TOOLTIP_603
LOCALS = <br>■従業員への誘い<br>
LOCALS += "相手を雇用し、本格的にリゾートで働いてもらう契約を結ぶ。<br>"
LOCALS += "相手はリゾートの正式な従業員となる。<br>"
LOCALS += "相手が店舗を持っている場合、開いている店舗はリゾート公式店舗となり、制服が設定出来るようになる。<br>"
LOCALS += "<br>COMタイプ：探索<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM603
#DIM 初期登録
#DIMS 制服記録

IF TALENT:TARGET:定住者 < 1
	LOCAL:2 = 0
	FOR LOCAL, 0, 従業員部屋数
		IF 従業員部屋割り配列:LOCAL < 1
			LOCAL:2 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:2 == 0
		PRINTW 従業員部屋がいっぱいです。
		RETURN -1
	ENDIF
ENDIF

DRAWLINE
IF TALENT:TARGET:店持ち
	PRINTFORML %CALLNAME:TARGET%の店「%商業区店割り配列:(店持ちキャラ店検索(TARGET)):店舗名%」をリゾートの公式店舗にしますか？
	PRINTL ※公式店舗にすると、その店に所属するキャラの制服を設定出来るようになります。　（必要ルピ：１日 / 2000ルピ）
ELSE
	PRINTFORML %CALLNAME:TARGET%を正式に従業員として雇いますか？　（必要ルピ：１日 / 2000ルピ）
ENDIF



PRINTBUTTON "[0]はい", 0
PRINTL
PRINTBUTTON "[1]いいえ", 1
PRINTL
$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 0
		TALENT:TARGET:従業員 = 1
		CFLAG:TARGET:滞在期間 = 999
		CFLAG:TARGET:長期雇用 = 0
		CFLAG:TARGET:探索参加許可フラグ = 1
		IF STRFIND(NAME:TARGET, "[一般人]") > -1
			NAME:TARGET = [従業員]%CALLNAME:TARGET%
		ENDIF
		IF TALENT:TARGET:定住者 < 1
			CALL 滞在者部屋割り配列削除処理(TARGET)
			FOR LOCAL, 0, 従業員部屋数
				IF 従業員部屋割り配列:LOCAL < 1
					従業員部屋割り配列:LOCAL = TARGET
					BREAK
				ENDIF
			NEXT
		ELSEIF TALENT:TARGET:店持ち
			現在仕事:TARGET:0 = -1
		ENDIF
		;制服チェック
		初期登録 = 0
		制服記録 = 
		FOR LOCAL, 1, CHARANUM
			SIF LOCAL == TARGET
				CONTINUE
			IF TALENT:LOCAL:従業員
				IF 初期登録 == 0
					初期登録 = 1
					制服記録 '= 現在制服:LOCAL:0
				ELSE
					IF 制服記録 != 現在制服:LOCAL:0
						;違う制服があるならフラグを置き換えてBREAK
						初期登録 = -1
						BREAK
					ENDIF
				ENDIF
			ENDIF
		NEXT
		IF 初期登録 > 0
			現在制服:TARGET:0 '= 制服記録
			CALL CLOTHES_RESET_TRAIN(TARGET)
		ENDIF
		CALL 従業員人数チェック()
	CASE 1
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE603
#DIM ループ用
;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(602)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;既に従業員だと無し
SIF TALENT:TARGET:従業員
	RETURN 0
;好感度レベル50以上
SIF CFLAG:TARGET:好感度レベル < 50
	RETURN 0
RETURN 1

;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_COM603

PRINTFORML %CALLNAME:TARGET%を従業員として正式に雇いました。

