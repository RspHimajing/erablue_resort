@MODE_持続快楽_背面駅弁(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号
CALL MODE_持続快楽_Ｖ挿入系(実行キャラ番号, 対象キャラ番号, 130, 180)


@MODETYPE_背面駅弁(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %CALLNAME:ターゲット番号%を後ろから抱きかかえ、ペニスを突き入れる
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	IF 陥落チェック_性的(ターゲット番号) == 0 && BASE:酩酊 > 300
		;未陥落の泥酔姦。ヤってる最中にも酩酊は下がり続けるので、泥酔の500からちょい下げて300までで
		;むしろ未陥落でもないと別に酔ってコトに及んだ情報残してもシコくなさそうなので未陥落のみ
		詳細文文字列受け渡し変数 = %TEXTR("酔った勢いで/酔いに浮かされて/泥酔して")%、%CALLNAME:プレイヤー番号%
		IF TALENT:ターゲット番号:恋人持ち > 0
			IF RISKY_DAY(ターゲット番号) >= 1
				詳細文文字列受け渡し変数 += @"と逆駅弁の体位で浮気%TEXTR("托卵/孕ませ")%%TEXTR("セックス/えっちを")%している"
			ELSE
				詳細文文字列受け渡し変数 += @"と逆駅弁の体位で浮気%TEXTR("セックス/えっち")%している"
			ENDIF
		ELSE
			詳細文文字列受け渡し変数 += @"に%TEXTR("逆駅弁の体位で/後ろから抱きあげられて")%おまんこを%TEXTR("使われてしまっている/許してしまっている/楽しまれてしまっている")%"
		ENDIF
	ELSE
		詳細文文字列受け渡し変数 = %TEXTR("逆駅弁の体位で/後ろから抱きあげられて")%、%CALLNAME:プレイヤー番号%
		IF TALENT:ターゲット番号:恋人持ち > 0
			詳細文文字列受け渡し変数 += @"と浮気%TEXTR("セックス/えっち")%している"
		ELSE
			詳細文文字列受け渡し変数 += @"の%TEXTR("ペニス/チンポ")%を咥え込んだ"
		ENDIF
	ENDIF
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;挿入で使用するのはペニスと膣で1を返す
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
	CASE 部位_指
		;抱えるので手は使用中
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
	CASE 部位_口
		;口は届かない
		RETURN 部位状況_届かない
	CASE 部位_胸
		;胸は届かない
		RETURN 部位状況_届かない
	CASE 部位_膣
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_アナル
		;尻は届かない
		RETURN 部位状況_届かない
ENDSELECT


@MODE_絶頂相手_背面駅弁(絶頂部位, 処理モード, 判定結果)
#FUNCTION
#DIM 絶頂部位
#DIMS 処理モード
#DIM REF 判定結果
RETURNF MODE_絶頂相手_Ｖ挿入系(絶頂部位, 処理モード, 判定結果)


@MESSAGE_MODE_背面駅弁解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の膣内からゆっくりと陰茎を引き抜いた
SIF TCVAR:対象キャラ番号:膣内精液量
	PRINTFORML %CALLNAME:対象キャラ番号%の膣口から、%CALLNAME:実行キャラ番号%の精液がどろりと垂れ落ちた…
