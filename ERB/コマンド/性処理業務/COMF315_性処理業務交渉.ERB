;-------------------------------------------------
;性処理業務交渉
;-------------------------------------------------
@COMNAME315
#FUNCTIONS
IF TALENT:従業員 == 0
;なんとなくつけてみた
	IF TALENT:PLAYER:性別 == 2
		TSTR:コマンド名受渡 = パパ活値段交渉
	ELSE
		TSTR:コマンド名受渡 = ママ活値段交渉
	ENDIF
ELSE
	TSTR:コマンド名受渡 = 性処理業務交渉
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_315
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_315
LOCALS = <br>■性処理業務交渉<br>
LOCALS += "従業員相手に、性処理業務をしてくれないか交渉するコマンド。<br>"
LOCALS += "何をお願いするかによって成功率や支払う報酬が変化する。<br>"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：選択肢によって変動"
LOCALS += "<br>固有の取得経験：選択肢によって変動<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM315
#DIMS CONST 選択先順番制御 = "手コキ処理", "パイズリ処理", "フェラチオ処理", "手マン処理", "クンニ処理"
#DIM 選択先保存
#DIMS DYNAMIC 性処理選択肢, 100
#DIM DYNAMIC 性処理数


DRAWLINE
TRYCALLFORM KOJO_COM_{NO:TARGET}_315_BEFORE(TARGET)
PRINTFORML %CALLNAME:TARGET%にどの性処理を依頼しますか？
PRINTL

FOR LOCAL, 0, VARSIZE("選択先順番制御")
	;固有条件
	IF GETMETH(@"固有COMABLE_ターゲット側_%選択先順番制御:LOCAL%_{NO:TARGET}", 1, TARGET) == 0
		CONTINUE
	ENDIF
	IF GETMETH(@"固有COMABLE_プレイヤー側_%選択先順番制御:LOCAL%_{NO:PLAYER}", 1, PLAYER) == 0
		CONTINUE
	ENDIF
	CALLFORM 性処理_ABLE_%選択先順番制御:LOCAL%
	IF RESULT == -1
		CALLFORMF 性処理NAME_%選択先順番制御:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%選択先順番制御:LOCAL%
			HTML_PRINT @"<nonbutton title='%TSTR:ツールチップ受渡%'><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL}] %TSTR:コマンド名受渡%</font></nonbutton>"
		ELSE
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAINFORM [{LOCAL}] %TSTR:コマンド名受渡%
			RESETCOLOR
			PRINTL 
		ENDIF
	ELSEIF RESULT == 1
		CALLFORMF 性処理NAME_%選択先順番制御:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%選択先順番制御:LOCAL%
			HTML_PRINT @"<button value='{LOCAL}' title='%TSTR:ツールチップ受渡%'>[{LOCAL}] %TSTR:コマンド名受渡%</button>"
		ELSE
			PRINTBUTTON @"[{LOCAL}] %TSTR:コマンド名受渡%", LOCAL
			PRINTL 
		ENDIF
	ENDIF
NEXT

VARSET RESULTS
VARSET 性処理選択肢
ENUMFUNCBEGINSWITH "性処理_ABLE_"
性処理数 = RESULT
ARRAYCOPY "RESULTS", "性処理選択肢"
FOR LOCAL, 0, 性処理数
	性処理選択肢:LOCAL = %REPLACE(性処理選択肢:LOCAL, "性処理_ABLE_", "")%
	SIF MATCH(選択先順番制御, 性処理選択肢:LOCAL)
		CONTINUE
	;固有条件
	IF GETMETH(@"固有COMABLE_ターゲット側_%RESULTS:LOCAL%_{NO:TARGET}", 1, TARGET) == 0
		CONTINUE
	ENDIF
	IF GETMETH(@"固有COMABLE_プレイヤー側_%RESULTS:LOCAL%_{NO:PLAYER}", 1, PLAYER) == 0
		CONTINUE
	ENDIF
	CALLFORM %RESULTS:LOCAL%
	IF RESULT == -1
		CALLFORMF 性処理NAME_%性処理選択肢:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%性処理選択肢:LOCAL%
			HTML_PRINT @"<nonbutton title='%TSTR:ツールチップ受渡%'><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</font></nonbutton>"
		ELSE
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAINFORM [{LOCAL + 100}] %TSTR:コマンド名受渡%
			RESETCOLOR
			PRINTL 
		ENDIF
	ELSEIF RESULT == 1
		CALLFORMF 性処理NAME_%性処理選択肢:LOCAL%
		IF OPTION変数:ツールチップ切り替え == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%性処理選択肢:LOCAL%
			HTML_PRINT @"<button value='{LOCAL + 100}' title='%TSTR:ツールチップ受渡%'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</button>"
		ELSE
			PRINTBUTTON @"[{LOCAL + 100}] %TSTR:コマンド名受渡%", LOCAL + 100
			PRINTL 
		ENDIF
	ENDIF
NEXT


PRINTL
PRINTBUTTON "[999] 戻る", 999
PRINTL

DRAWLINE
BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN -1
	CASE IS < 100
		CALL 性処理業務交渉処理(TARGET, 選択先順番制御:LOCAL)
		SIF RESULT == -1
			RESTART
			
		IF STRFIND(コマンド初回フラグ:TARGET:0, @"-WORKING_%選択先順番制御:LOCAL%-") < 0
			コマンド初回フラグ_一時保存:TARGET:0 += @"-WORKING_%選択先順番制御:LOCAL%-"
		ENDIF
	CASEELSE
		CALL 性処理業務交渉処理(TARGET, 性処理選択肢:(LOCAL - 100))
		SIF RESULT == -1
			RESTART
		
		IF STRFIND(コマンド初回フラグ:TARGET:0, @"-WORKING_%性処理選択肢:(LOCAL - 100)%-") < 0
			コマンド初回フラグ_一時保存:TARGET:0 += @"-WORKING_%性処理選択肢:(LOCAL - 100)%-"
		ENDIF
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE315

;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(315)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;従業員もしくルピえっち交渉術必須
SIF TALENT:従業員 == 0 && あなた特殊能力:ルピえっち交渉術 != 1
	RETURN 0
;他人とデート中はダメ
SIF CFLAG:TARGET:デート && CFLAG:TARGET:デート != MASTER + 100 && TCVAR:TARGET:デートちょっかい値 <= 0
	RETURN 0

RETURN 1


;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_COM315(対象キャラ, 実行性処理)
#DIM 対象キャラ
#DIMS 実行性処理

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に、

IF RSTR:コマンド結果受渡し文字列 == "手コキ処理"
	PRINTFORML %TEXTR("手コキで抜いてほしい/手で抜いてほしいと/手で気持ちよくしてほしいと/ここで手コキしてほしいと/手コキの『お仕事』を")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "パイズリ処理"
	PRINTFORML %TEXTR("おっぱいを使って抜いてほしいと/パイズリで抜いてほしいと/ここでパイズリしてほしいと/パイズリの『お仕事』を")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "フェラチオ処理"
	PRINTFORML %TEXTR("口で抜いてほしいと/フェラで抜いてほしいと/ムラムラしてきたのでここでしゃぶってほしいと/フェラチオの『お仕事』を")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "手マン処理"
	PRINTFORML %TEXTR("手でおまんこをほじってほしいと/手マンで気持ちよくしてほしいと/ここで手マンしてほしいと/手マンの『お仕事』を")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "クンニ処理"
	PRINTFORML %TEXTR("おまんこを舐めて気持ちよくしてほしいと/ここでクンニしてほしいと/クンニの『お仕事』を")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "セックス処理"
	PRINTFORML %TEXTR("おまんこを使ってすっきりさせてほしいと/ここでおまんこを使わせてほしいと/おまんこで抜いてほしいと/性処理セックスの『お仕事』を/ここでオナニーするのにおまんこを貸してほしいと/子宮に精液をコキ捨てさせてほしいと")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "被セックス処理"
	PRINTFORML %TEXTR("チンポでおまんこをほじってほしいと/チンポを肉ディルドとして使わせてほしいと/肉バイブとしての『お仕事』を/セックスの『お仕事』を")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "アナルセックス処理"
	PRINTFORML %TEXTR("アナルを使ってすっきりさせてほしいと/オナニーするのにケツ穴を使わせてほしいと/アナルで抜いてほしいと/性処理アナルセックスの『お仕事』を/ケツ穴に精液をコキ捨てさせてほしいと")%お願いした。
ELSEIF RSTR:コマンド結果受渡し文字列 == "被アナルセックス処理"
	PRINTFORML %TEXTR("チンポでアナルをほじってほしいと/チンポを肉ディルドとして使わせてほしいと/肉アナルバイブとしての『お仕事』を/性処理アナルセックスの『お仕事』を")%お願いした。
ELSE
	;あとから増えた時用に一応おいとく
	PRINTFORML %RSTR:コマンド結果受渡し文字列%をお願いした。
ENDIF
CALLFORM MESSAGE_性処理_%RSTR:コマンド結果受渡し文字列%


@MESSAGE_COM315_非口上時

TRYCALLFORM MESSAGE_性処理_%RSTR:コマンド結果受渡し文字列%_非口上時



@性処理業務交渉処理(対象キャラ, 実行性処理)
#DIM 対象キャラ
#DIM 値段計算
#DIM 余裕金額
#DIMS 実行性処理

CALLFORM CAN_性処理_%実行性処理%(対象キャラ)
PRINTL

IF RESULT == -1
	値段計算 = 0
	RFLAG:コマンド結果受渡し変数 = 1
ELSE
	値段計算 = RESULT
ENDIF

;地の文もオプションが勝手につく仕様にしたので、すまんがハードルを上げさせてもらう。
;高貴処女とかの相手を考えるとこれでも多分借金まで到達する可能性がある。
PRINTFORML 所持ルピ：%MON_FORMAT(MONEY)%
IF 実行性処理 == "セックス処理"
	余裕金額 = 値段計算 + 30000 + 性処理_セックス処理_中出し価格(対象キャラ) 
	IF 知識素質:対象キャラ:高貴 > 0
		IF 初体験済みチェック("ファーストキス", 対象キャラ) == 0
			余裕金額 += 100000
		ENDIF
	ENDIF
ELSE
	余裕金額 = 値段計算 + 20000
ENDIF
IF MONEY < 余裕金額
	PRINTL オプションのことを考えると軍資金はもう少しあったほうがいいだろう。
	PRINTFORMW おおよそ%MON_FORMAT(余裕金額)%程度は持っておいた方が良さそうだ……
	RETURN -1
ENDIF

PRINTL
PRINTFORML %実行性処理%を%CALLNAME:対象キャラ%にお願いしますか？
PRINTBUTTON "[0] はい", 0
PRINTL
PRINTBUTTON "[1] いいえ", 1
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 0
		RSTR:コマンド結果受渡し文字列 = %実行性処理%
		RFLAG:コマンド結果受渡し変数２ = 値段計算
		MONEY -= 値段計算
		CALLFORM 性処理_%実行性処理%()
		RETURN RESULT
	CASE 1
		RETURN -1
ENDSELECT


@補正後好感度算出(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 補正後好感度

補正後好感度 = CFLAG:対象キャラ:好感度レベル
補正後好感度 += CFLAG:対象キャラ:恋慕レベル / 3

補正後好感度 -= TALENT:対象キャラ:自制心 * 7
補正後好感度 -= TALENT:対象キャラ:一線越えない * 10
補正後好感度 -= TALENT:対象キャラ:貞操 * 10
補正後好感度 -= TALENT:対象キャラ:羞恥心 * 5
補正後好感度 -= TALENT:対象キャラ:抵抗 * 15
補正後好感度 -= TALENT:対象キャラ:態度 * 5
SIF GETBIT(TALENT:対象キャラ:陥落不可, 1)
	補正後好感度 -= 20
SIF (TALENT:対象キャラ:恋人持ち == 1 || TALENT:対象キャラ:恋人持ち == 2) && EXP:対象キャラ:売春経験 < 10
	補正後好感度 -= 15

IF 同性愛性癖判定(対象キャラ, PLAYER)
	SELECTCASE 性癖素質:対象キャラ:同性愛
		CASE 1
			;補正なし
		CASE 2
			補正後好感度 -= 5
		CASE 3
			補正後好感度 -= 15
		CASEELSE
			補正後好感度 += 15
	ENDSELECT
ENDIF

SELECTCASE TALENT:対象キャラ:性別嗜好
	CASE 1
		SIF TALENT:PLAYER:性別 == 2
			補正後好感度 += 20
	CASE 2
		SIF TALENT:PLAYER:性別 == 1
			補正後好感度 += 20
	CASE 3
		補正後好感度 += 20
ENDSELECT

補正後好感度 += TALENT:対象キャラ:性的興味 * 5
補正後好感度 += TALENT:対象キャラ:経験豊富 * 5
補正後好感度 += TALENT:対象キャラ:ギャル * 5
補正後好感度 += TALENT:対象キャラ:弱味 * 20
補正後好感度 += EXP:対象キャラ:売春経験 / 10 * 5
SIF TALENT:対象キャラ:マイペース < 0
	補正後好感度 += 10
SIF 初体験日取得("初うふふ", 対象キャラ, MASTER) || フィールド展開:うふふ目撃スルー
	補正後好感度 += 10
SIF TALENT:対象キャラ:恋人持ち == 3 && TALENT:対象キャラ:子持ち
	補正後好感度 += 10

SIF TALENT:対象キャラ:セフレ
	補正後好感度 = 999

RETURNF 補正後好感度


@経過時間算出(行為回数)
#FUNCTION
#DIM CONST 減衰閾値 = 24
#DIM 行為回数

IF 行為回数 <= 減衰閾値
	;2時間までは線形
	RETURNF 行為回数 * 5
ELSE
	;以降は減衰
	RETURNF (減衰閾値 + LOG(行為回数 - 減衰閾値)) * 5
ENDIF
