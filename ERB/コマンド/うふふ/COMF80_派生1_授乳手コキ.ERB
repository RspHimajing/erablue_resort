
@COMNAME_S80_1
#FUNCTIONS
TSTR:コマンド名受渡 = 授乳手コキ


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_80_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = B
オートコマンド許可 = 3
反転オート許可 = 3


@COM_TOOLTIP_S80_1
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■授乳手コキする<br>
	LOCALS += "相手に自分の乳首を吸わせながら、相手のペニスを手で愛撫するコマンド。<br>授乳手コキモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。"
ELSE
	LOCALS = <br>■授乳手コキ<br>
	LOCALS += "相手の乳首に吸いつきながら、相手に手で自分のペニスを愛撫してもらうコマンド。<br>授乳手コキモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。"
ENDIF
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｂ・露出・接触・奉仕・不潔"
LOCALS += "<br>固有の取得経験：手淫経験(TARGET)・乳首開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S80_1
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_DELETE_ターゲット側("手コキ", PLAYER)
SIF MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
	CALL MODE_DELETE_プレイヤー側("乳首吸い", PLAYER)
CALL MODE_CHANGE("授乳手コキ", TARGET, PLAYER)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------



RCVAR:乳首開発フラグ = 2

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 200)
SIF TALENT:TARGET:器用な指
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 50)

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 250)


CALL SOURCE_CALC_不潔(TARGET, PLAYER, 100)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;手淫経験
EXP:手淫経験 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4

RETURN 1



@CAN_COM_S80_1(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 授乳手コキ(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 1
	PRINTFORM 精液中毒LV{ABL:精液中毒}({ABL:精液中毒 * 1})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 恥じらい(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 汚臭鈍感(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 3
	PRINT 汚臭敏感(3)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 献身的(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 快感の否定(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 親愛(5)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT ふたなり(8)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S80_1
;授乳手コキ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
SIF GLOBAL_COMABLE_S("80_1")
	RETURN RESULT
;手コキモードじゃない時はアウト
SIF !MODE_存在判定_完全一致("手コキ", TARGET, PLAYER)
	RETURN 0
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
;陥没乳首は乳首露出させてからでないと不可
SIF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	RETURN 0
IF !MODE_存在判定_完全一致("乳首吸い", PLAYER, TARGET)
	;口を何かに使ってるなら無理
	SIF TEQUIP:PLAYER:口
		RETURN 0
	;胸を何かに使ってるなら無理
	SIF TEQUIP:TARGET:胸
		RETURN 0
ENDIF
;上半身着衣
;鎧、差し込めないorはだけない服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S80_1
PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%
	IF RAND:2 == 0
		PRINT を膝に寝かせ、
	ELSE
		PRINT にしなだれかかり、
	ENDIF
SELECTCASE TALENT:TARGET:バストサイズ
	CASE -2
		PRINT 微かに膨らんだ胸にぽつんとある
	CASE -1
		PRINT 僅かな膨らみの
	CASE 1
		PRINT 大きな胸の
	CASE 2
		PRINT 豊かで柔らかな胸の
	CASE 3
		PRINT 顔が埋もれそうなほど大きな胸の
ENDSELECT
PRINTL 乳首を吸わせながらペニスに指を絡めて擦りあげている…


