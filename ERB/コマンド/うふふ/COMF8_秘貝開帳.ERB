;-------------------------------------------------
;秘貝開帳
;愛撫系コマンド、レベル2
;-------------------------------------------------
@COMNAME8
#FUNCTIONS
TSTR:コマンド名受渡 = 秘貝開帳
SIF GETBIT(FLAG:モード管理, モードビット_同時)
	TSTR:コマンド名受渡 = 同時に秘貝開帳



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_8
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = C_女
同時モード許可 = 999


@COM_TOOLTIP_8
LOCALS = <br>■秘貝開帳<br>
LOCALS += "相手に秘部を広げさせるコマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｃ・露出"
LOCALS += "<br>固有の取得経験：自慰経験(TARGET・一定条件時)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM8
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;処女、貞操観念
IF TALENT:処女 == 1
	CALL SOURCE_CALC_露出(TARGET, PLAYER, 500)
ELSE
	CALL SOURCE_CALC_露出(TARGET, PLAYER, 300)
ENDIF
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 40)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;露出癖Lv3以上で自慰経験加算
SIF ABL:露出癖 >= 3
	EXP:自慰経験 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

RETURN 1


@CAN_COM8(ARG)
#DIM CONST 必要実行値 = 18
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 秘貝開帳(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 3
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 3})
	＋表示フラグ = 1
ENDIF
;ABL:Ｖ感覚
IF ABL:Ｖ感覚
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:Ｖ感覚 * 2
	PRINTFORM Ｖ感覚LV{ABL:Ｖ感覚}({ABL:Ｖ感覚 * 2})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:露出癖
IF ABL:8
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 3
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 3})
	＋表示フラグ = 1
ENDIF
;ABL:自慰中毒
IF ABL:自慰中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:自慰中毒 * 3
	PRINTFORM 自慰中毒LV{ABL:自慰中毒}({ABL:自慰中毒 * 3})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 3
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 3})
	＋表示フラグ = 1
ENDIF

;PALAM:潤滑（の不足）
IF PALAM:潤滑 < PALAMLV:3
	PRINT  - 
	実行値 -= 5
	PRINT 潤滑不足(5)
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 3
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 3})
	＋表示フラグ = 1
ENDIF

;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 2
	PRINT 恥じらい(2)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 5
	PRINT 快感の否定(5)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && ((!TALENT:恋慕) || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 12
	PRINT 男嫌い(12)
	＋表示フラグ = 1
ENDIF

;処女
IF TALENT:処女 == 1
	PRINT  - 
	実行値 -= 20
	PRINT 処女(20)
	＋表示フラグ = 1
ELSEIF EXP:Ｖ経験 < EXPLV:2
	PRINT  - 
	実行値 -= 5
	PRINT Ｖ経験不足(5)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	PRINT  + 
	実行値 += 4
	PRINT ふたなり(4)
	＋表示フラグ = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 媚薬(6)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(18以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE8
;秘貝開帳実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(8)
	RETURN RESULT
;Vがない場合駄目
SIF TALENT:TARGET:Ｖ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;露出癖が2未満だとダメ
SIF ABL:露出癖< 2
	RETURN 0
;処女の場合、露出癖3以上ないとダメ
SIF TALENT:処女 && ABL:露出癖 < 3
	RETURN 0
;縄使用中は不可
; SIF TEQUIP:縄
; 	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツ、下着があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり
	RETURN 0

;オトコだとダメ
SIF !(TALENT:性別 & 1)
	RETURN 0
;お風呂場プレイ中はプレイマットが無いとダメ
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;指を何かに使ってるなら無理
SIF TEQUIP:TARGET:指
	RETURN 0
;膣を何かに使ってるなら無理
SIF TEQUIP:TARGET:膣
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM8
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%に命令され、
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT は揃って性器を拡げて見せた…
	RETURN 0
ENDIF

PRINTFORM %CALLNAME:TARGET%は
;露出癖
IF ABL:露出癖 == 0
	PRINTFORM 涙を浮かべてこちらを睨んだが、しぶしぶといった様子で自分の
	;小柄体型
	IF TALENT:種族 < 0
		PRINT 幼いワレメに
	ELSE
		PRINT クレヴァスに
	ENDIF
	PRINTL 指を挿し入れ、左右に拡げて見せた
	; SIF TCVAR:102
	; 	PRINTFORML さらけ出された膣口からは、さきほど射精した精液が、こぽこぽとこぼれてくる…
ELSEIF ABL:露出癖 == 1
	PRINTFORML 顔を真っ赤にしながらも、おずおずと、性器をはっきり拡げて見せた
	; SIF TCVAR:102
	; 	PRINTFORML さらけ出された膣口からは、さきほど射精した精液が、こぽこぽとこぼれてくる…
ELSEIF ABL:露出癖 == 2
	PRINTFORML 下腹部に手を置き、人差し指と中指でサインを作るように、%CALLNAME:PLAYER%に自分の秘所を曝した
	; IF TCVAR:102
	; 	PRINTFORML さらけ出された膣口からは、さきほど射精した精液が、こぽこぽとこぼれてくる…
	; ELSE
		PRINTFORML 広がった女陰からこちらを覗く膣口は、収めるべきモノを待ち望むかのように、愛液に濡れ、淫らに蠢いてる…
	; ENDIF
ELSEIF ABL:露出癖 == 3
	PRINTFORML 嬉々とした表情で、片手で陰唇を拡げ、空いた手で陰核を弄り始めた
	; IF TCVAR:102
	; 	PRINTFORML さらけ出された膣口は、さきほど射精したばかりの精液を、淫らに垂れ流している…
	; ELSE
		PRINTFORML %CALLNAME:TARGET%の手が陰核を刺激するたびに、膣口から愛液を滴らせている…
	; ENDIF
ELSEIF ABL:露出癖 == 4
	PRINTFORML 両手の指を使って陰唇だけでなく、膣口も同時に拡げた
	; IF TCVAR:102
	; 	PRINTFORML %CALLNAME:TARGET%の手が膣口をさらに左右に拡げると、さきほど射精した精液を垂れ流している…
	; ELSE
		PRINTFORML 膣内を見られることに感じているのか、%CALLNAME:TARGET%の頬は仄かに紅く染まっている
	; ENDIF
ELSEIF ABL:露出癖 >= 5 && TALENT:淫乱 == 0
	PRINTFORML 両手の指を陰唇にひっかけ、膣口ごと%CALLNAME:PLAYER%に見せつけるように大きく拡げた
	; IF TCVAR:102
	; 	PRINTFORML 大きく拡げられた膣口は、さきほど射精した精液を垂れ流している…
	; ELSE
		PRINTFORML %CALLNAME:TARGET%の手が膣内部まで拡げて見せると、膣壁がぱくぱくと動き、愛液をとめどなく溢れさせている…
	; ENDIF
;露出癖LV5以上＋淫乱or娼婦
ELSEIF ABL:露出癖 >= 5 && TALENT:淫乱
	PRINTFORML 両手の指を陰唇にひっかけ、膣口ごと%CALLNAME:PLAYER%に見せつけるように大きく拡げた
	; IF TCVAR:102
	; 	PRINTFORML うごめく膣内からは、さきほど射精した精液が、どろりと、だらしなくこぼれてきた…
	; ELSE
		PRINTFORML やわらかそうな膣内は、たっぷりと粘液で濡れ、こちらを誘う様に、ひくひくとうごめいている…
	; ENDIF
ENDIF
;調教者が恥じらい＋調教経験200以下
IF TALENT:PLAYER:羞恥心 > 0 && EXP:PLAYER:調教経験 < 200
	PRINTFORML 自分で命令を出しておきながらいざ%CALLNAME:TARGET%の痴態を目の当たりにした%CALLNAME:PLAYER%は
	PRINTL 微かな罪悪感と羞恥心を煽られ、朱に染まった顔を僅かにそむけてしまった
ENDIF

SIF TCVAR:TARGET:膣内精液量
	PRINTFORML %CALLNAME:TARGET%の膣口から、精液がどろりと垂れ落ちた…
