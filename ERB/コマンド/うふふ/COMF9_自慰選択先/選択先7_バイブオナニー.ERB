
@COMNAME_S9_7
#FUNCTIONS
TSTR:コマンド名受渡 = バイブオナニー

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_9_7
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
;自慰系オートコマンドは個別に禁止設定を行うため2を入れる
オートコマンド許可 = 2


@COM_TOOLTIP_S9_7
LOCALS = <br>■バイブオナニー<br>
LOCALS += "相手が見ている前でバイブを挿入し自慰を行うコマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：露出・快Ｂ・歓楽(一定条件時)・反感(一定条件時)"
LOCALS += "<br>固有の取得経験：自慰経験(自慰実行者)、調教自慰経験(自慰実行者)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S9_7
;弱点コマンドをバイブと共通させる
LOCAL = 弱点番号算出(PLAYER, "44")
IF LOCAL >= 0 || 追加弱点番号算出(PLAYER, "44") >= 0
	CALL 弱点の欠片取得処理("44")
	TCVAR:PLAYER:弱点コマンドフラグ = 1
	SIF 弱点看破:PLAYER:LOCAL == 0
		弱点看破:PLAYER:LOCAL = 1
ENDIF

;共通処理
CALL Ｖ自慰系共通処理
;モード遷移
CALL MODE_CHANGE("バイブ", MASTER, 自慰ターゲット保存)
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S9_7

IF FLAG:オートコマンド判定中フラグ
	;オート時のみ実行判定を行う
	CALL 自慰_ABLE_バイブオナニー
	RETURN RESULT
ELSE
	;選択から飛んでくるのでABLEは常に0
	RETURN 0
ENDIF

@自慰_ABLE_バイブオナニー
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("9_7")
	RETURN RESULT
;無知
SIF 知識素質:(自慰ターゲット保存):性知識 < 0 && EXP:(自慰ターゲット保存):自慰経験 < 1
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:4 == 0 && NOITEM == 0
	RETURN 0
;鎧があると駄目
SIF TEQUIP:(自慰ターゲット保存):下腹部前鎧あり
	RETURN 0
;服駄目
SIF TEQUIP:(自慰ターゲット保存):下半身服あり && !TEQUIP:(自慰ターゲット保存):スカートめくり可 && !TEQUIP:(自慰ターゲット保存):ずらし可
	RETURN 0
;タイツ駄目
SIF TEQUIP:(自慰ターゲット保存):下タイツあり && !TEQUIP:(自慰ターゲット保存):下タイツ破れ
	RETURN 0
;前張り駄目
SIF TEQUIP:(自慰ターゲット保存):下半身下着あり == 2
	RETURN 0
;存在しない場合駄目
SIF TALENT:(自慰ターゲット保存):Ｖ感度 == -2
	RETURN 0
;処女駄目
SIF TALENT:(自慰ターゲット保存):処女
	RETURN 0
;V感覚
SIF ABL:(自慰ターゲット保存):Ｖ感覚 < 3
	RETURN 0
;膣が無いとダメ
SIF GETBIT(TALENT:(自慰ターゲット保存):性別, 0) == 0
	RETURN 0
;使用中
SIF TEQUIP:(自慰ターゲット保存):膣
	RETURN 0
SIF TEQUIP:(自慰ターゲット保存):指
	RETURN 0

;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && OPTION変数:玩具プレイ禁止
	RETURN 0	
	
;関数実行のため、派生番号を返す
RETURN 7

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S9_7

PRINTFORM %CALLNAME:(自慰ターゲット保存)%は
IF TEQUIP:(自慰ターゲット保存):下半身服あり && TEQUIP:(自慰ターゲット保存):ずらし可
	PRINT 服をズラして
ELSEIF TEQUIP:(自慰ターゲット保存):下半身下着あり > 0
	PRINT 下着をズラして
ENDIF
PRINTL バイブを膣に入れ、スイッチを入れた…


@MESSAGE_COM_S9_7_非口上時
SIF (自慰ターゲット保存)== MASTER
	RETURN 0
SIF TFLAG:オートコマンドフラグ > 0
	RETURN 0

IF EXP:(自慰ターゲット保存):調教自慰経験 <= 10
	PRINTFORML 誰かに見られながらの自慰には慣れていないようで、%CALLNAME:(自慰ターゲット保存)%は顔を赤くして%CALLNAME:MASTER%を睨んでいる…
ELSEIF EXP:(自慰ターゲット保存):調教自慰経験 <= 50
	PRINTFORML 少し慣れてきたようだが、まだ%CALLNAME:MASTER%の視線が気になるようだ…
ELSE
	PRINTFORML %CALLNAME:(自慰ターゲット保存)%は%CALLNAME:MASTER%に見られながらの自慰に、妙な高揚感を覚えている…
ENDIF
