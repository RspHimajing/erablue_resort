;-------------------------------------------------
;ローター
;道具使用コマンド、レベル1
;-------------------------------------------------
@COMNAME40
#FUNCTIONS

	TSTR:コマンド名受渡 = ローター




;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_40
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = C
オートコマンド許可 = 1



@COM_TOOLTIP_40
LOCALS = <br>■ローター<br>
LOCALS += "ローターを使い、相手のクリトリスやペニスに刺激を与えるコマンド。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ｃ・露出・接触"
LOCALS += "<br>固有の取得経験：道具使用経験(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM40
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER,250)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 60)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE40
;ローター実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(40)
	RETURN RESULT
;クリがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:0 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧、めくれない服があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0

;使用者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;クリトリスを何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:クリトリス
	RETURN 0
CALL MODE_部位届く判定(部位_クリトリス, PLAYER, TARGET)
SIF RESULT == 0
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && OPTION変数:玩具プレイ禁止
	RETURN 0
	
;オートコマンド中の制限
SIF FLAG:オートコマンド判定中フラグ && TALENT:TARGET:性別 == 2 &&  OPTION変数:男性への愛撫禁止
	RETURN 0	
	
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM40
PRINTFORM %CALLNAME:PLAYER%は
IF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	PRINTFORM %CALLNAME:TARGET%の
	PRINTL 下半身にタイツの上から震えるローターを押し当てた…
	RETURN 0
ENDIF
IF TEQUIP:下半身下着あり > 0
	IF TALENT:恋慕
		PRINTFORM 自ら下着をずらして準備している
	ELSE
		PRINTFORM 下着をずらして露出させた
	ENDIF
ELSEIF TEQUIP:下半身服あり && TEQUIP:スカートめくり可
	IF TALENT:恋慕
		PRINTFORM 自ら服を捲り上げて準備している
	ELSE
		PRINTFORM 服を捲り上げて露出させた
	ENDIF
ELSEIF TEQUIP:下半身服あり && TEQUIP:ずらし可
	IF TALENT:恋慕
		PRINTFORM 自ら服をずらして準備している
	ELSE
		PRINTFORM 服をずらして露出させた
	ENDIF
ENDIF
PRINTFORM %CALLNAME:TARGET%の
;オトコ＋欲情のPALAMLV3以上
IF !(TALENT:性別 & 1) && (PALAM:欲情 >= PALAMLV:3)
	PRINTL 硬くそそり勃ったペニスに震えるローターを押し当てた…
;オトコ
ELSEIF !(TALENT:性別 & 1)
	PRINTL ペニスに震えるローターを押し当てた…
;ふたなり＋欲情のPALAMLV3以上
ELSEIF (TALENT:性別 & 3) == 3 && PALAM:欲情 >= PALAMLV:3
	PRINTL ぬかるんだ秘唇に震えるローターを押し当てた…
;ふたなり
ELSEIF (TALENT:性別 & 3) == 3
	PRINTL 秘唇に震えるローターを押し当てた…
;欲情のPALAMLV3以上
ELSEIF PALAM:欲情 >= PALAMLV:3
	PRINTL 硬く尖ったクリトリスに震えるローターを押し当てた…
ELSE
	PRINTL クリトリスに震えるローターを押し当てた…
ENDIF

