;-------------------------------------------------
;パンツ脱衣
;-------------------------------------------------
@COMNAME207
#FUNCTIONS
IF TEQUIP:下半身下着あり == 2
	TSTR:コマンド名受渡 = 前張りを剥がす
ELSEIF TEQUIP:下半身下着あり == 3
	TSTR:コマンド名受渡 = ふんどしを脱がす
ELSEIF TEQUIP:下半身下着あり == 4
	TSTR:コマンド名受渡 = リボンをほどく
ELSEIF TEQUIP:下半身下着あり == -2
	TSTR:コマンド名受渡 = 性器ピアスを外す
ELSEIF TEQUIP:下半身下着あり == -3
	TSTR:コマンド名受渡 = クリから名札を外す
ELSE
	TSTR:コマンド名受渡 = 下着を脱がす
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_207
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
オートコマンド許可 = 3

@COM_TOOLTIP_207
LOCALS = <br>■下着を脱がす<br>
LOCALS += "相手の下半身の下着を脱がせるコマンド。<br>下半身の下着が前張りの場合、[前張りを剥がす]に変化。<br>下半身の下着がふんどしの場合、[ふんどしを脱がす]に変化。<br>下半身下着抜き取り可の場合、下半身の服があっても実行出来る。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM207
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:下半身下着あり = 0
ENDIF

;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM207(ARG)
#DIM CONST 必要実行値 = 20
#DIM 実行値
#DIM ＋表示フラグ

SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 下半身下着脱衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:露出癖
IF ABL:露出癖
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:露出癖 * 10
	PRINTFORM 露出癖LV{ABL:露出癖}({ABL:露出癖 * 10})
	＋表示フラグ = 1
ENDIF
IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 10)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 10) * 5
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 10)}({GETPALAMLV(PALAM:欲情, 10) * 5})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 恥じらい(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT セフレ(3)
	＋表示フラグ = 1
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		PRINT  - 
		実行値 -= 10
		PRINT 野外露出(10)
		＋表示フラグ = 1
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(20以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE207
;下半身下着脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(207)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:下半身下着あり
	RETURN 0
;服がある
IF TEQUIP:下半身服あり && !TEQUIP:下半身下着抜き取り可
	;前張りならめくりorずらし可でOK
	SIF TEQUIP:下半身下着あり != 2 || (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
		RETURN 0
ENDIF
;タイツがある
SIF TEQUIP:下タイツあり
	RETURN 0
;バイブ以外で膣を使ってるなら無理
SIF TEQUIP:TARGET:膣 && !MODE_存在判定_ターゲット側("バイブ", TARGET)
	RETURN 0
;前張り以外
IF TEQUIP:下半身下着あり != 2
	;バイブとビーズ以外でアナルを使ってるなら無理
	SIF TEQUIP:TARGET:アナル && !MODE_存在判定_ターゲット側("アナルバイブ", TARGET) && !MODE_存在判定_ターゲット側("アナルビーズ", TARGET)
		RETURN 0
	;ペニスを使ってると駄目
	SIF TEQUIP:TARGET:ペニス
		RETURN 0
	;足コキ中は駄目
	SIF MODE_存在判定_プレイヤー側("足コキ", TARGET)
		RETURN 0
ENDIF

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM207
IF TEQUIP:下半身下着あり == 2
	PRINTFORML %CALLNAME:TARGET%の前張りを剥がした…
ELSEIF TEQUIP:下半身下着あり == 3
	PRINTFORML %CALLNAME:TARGET%のふんどしを脱がせた…
ELSEIF TEQUIP:下半身下着あり == 4
	PRINTFORML %CALLNAME:TARGET%のリボンラッピングをほどいた…
ELSEIF TEQUIP:下半身下着あり == -2
	PRINTFORML %CALLNAME:TARGET%の性器を飾り立てるピアスを外した…
ELSEIF TEQUIP:下半身下着あり == -3
	PRINTFORML %CALLNAME:TARGET%のクリトリスから卑猥な表記の名札を外した…
ELSE
	PRINTFORML %CALLNAME:TARGET%の下着を脱がせた…
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 人目のあるところで脱いだことで、%CALLNAME:TARGET%はどこか落ち着かないようだ…
ENDIF

