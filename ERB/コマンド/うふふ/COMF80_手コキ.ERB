;-------------------------------------------------
;手淫
;奉仕系コマンド、レベル1
;-------------------------------------------------
@COMNAME80
#FUNCTIONS
TSTR:コマンド名受渡 = 手コキ


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_80
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ&奉仕")
;同時モードはムリじゃねえかなコレ
オートコマンド許可 = 3
反転オート許可 = 3


@COM_TOOLTIP_80
IF RFLAG:奉仕逆転時ツールチップフラグ
	LOCALS = <br>■手コキする<br>
	LOCALS += "自分が手で相手のペニスを愛撫するコマンド。<br>手コキモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。"
ELSE
	LOCALS = <br>■手コキ<br>
	LOCALS += "相手に手で自分のペニスを愛撫してもらうコマンド。<br>手コキモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。"
ENDIF
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：露出・接触・奉仕・不潔"
LOCALS += "<br>固有の取得経験：手淫経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM80
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_CHANGE("手コキ", TARGET, PLAYER)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------




CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 200)
SIF TALENT:TARGET:器用な指
	CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 50)

CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 250)
CALL SOURCE_CALC_不潔(TARGET, PLAYER, 100)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;手淫経験
EXP:手淫経験 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4

RETURN 1


@CAN_COM80(ARG)
#DIM CONST 必要実行値 = 14
#DIM 実行値
#DIM ＋表示フラグ

SKIPDISP ARG
PRINTFORML 手コキ(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
実行値 = 0
＋表示フラグ = 0

;すべての命令に共通の要素を考慮
CALL COM_ORDER
実行値 = RESULT
＋表示フラグ = RESULT:1

;ABL:欲望
IF ABL:欲望
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:欲望 * 1
	PRINTFORM 欲望LV{ABL:欲望}({ABL:欲望 * 1})
	＋表示フラグ = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:奉仕精神 * 4
	PRINTFORM 奉仕精神LV{ABL:奉仕精神}({ABL:奉仕精神 * 4})
	＋表示フラグ = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += ABL:精液中毒 * 1
	PRINTFORM 精液中毒LV{ABL:精液中毒}({ABL:精液中毒 * 1})
	＋表示フラグ = 1
ENDIF

IF MARK:快楽刻印
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += MARK:快楽刻印 * 1
	PRINTFORM 快楽刻印LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	＋表示フラグ = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情, 5)
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += GETPALAMLV(PALAM:欲情, 5) * 1
	PRINTFORM 欲情LV{GETPALAMLV(PALAM:欲情, 5)}({GETPALAMLV(PALAM:欲情, 5) * 1})
	＋表示フラグ = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	実行値 -= 1
	PRINT 恥じらい(1)
	＋表示フラグ = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 1
	PRINT 汚臭鈍感(1)
	＋表示フラグ = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	実行値 -= 3
	PRINT 汚臭敏感(3)
	＋表示フラグ = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 6
	PRINT 献身的(6)
	＋表示フラグ = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	実行値 -= 1
	PRINT 快感の否定(1)
	＋表示フラグ = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER)
	PRINT  - 
	実行値 -= 5
	PRINT 男嫌い(5)
	＋表示フラグ = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 5
	PRINT 恋慕(5)
	＋表示フラグ = 1
ENDIF
;セフレ
IF TALENT:セフレ
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 3
	PRINT セフレ(3)
	＋表示フラグ = 1
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 8
	PRINT ふたなり(8)
	＋表示フラグ = 1
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF ＋表示フラグ
		PRINT  + 
	実行値 += 999
	PRINT あなたの言いなり(999)
	＋表示フラグ = 1
ENDIF

;合計を表示(14以上で実行)
PRINTFORM  = {実行値}

SIF 実行値 < 必要実行値
	PRINT  < 
SIF 実行値 == 必要実行値
	PRINT  = 
SIF 実行値 > 必要実行値
	PRINT  > 
PRINTFORM 実行値{必要実行値}

WAIT
SKIPDISP 0

;実行できない
SIF 実行値 < 必要実行値
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE80
;手淫実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
SIF GLOBAL_COMABLE(80)
	RETURN RESULT
;ちんこがない場合駄目
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;縄使用中は不可
; SIF TEQUIP:縄
; 	RETURN 0
;ペニスを何かに使ってるなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
;指を何かに使ってるなら無理
SIF TEQUIP:TARGET:指
	RETURN 0
;下半身着衣
;前鎧、タイツ、前張りがあると無理
SIF TEQUIP:PLAYER:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:下タイツあり && !TEQUIP:PLAYER:下タイツ破れ
	RETURN 0
SIF TEQUIP:PLAYER:下半身下着あり == 2
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM80
PRINTFORM %CALLNAME:TARGET%は
;恋慕か親愛
IF TALENT:恋慕 && PLAYER == MASTER
	PRINT 愛しむような表情で、
	;淫乱か娼婦
	SIF TALENT:淫乱
		PRINT 自分から
ELSEIF 知識素質:性知識 < 0
	PRINTFORM %CALLNAME:PLAYER%に言われるがまま、
ENDIF
PRINTFORML %CALLNAME:PLAYER%のペニスに指を絡めて擦りあげている…


