;-------------------------------------------------
;美乳オイルエステ
;-------------------------------------------------
@MASSAGE_COMNAME21
#FUNCTIONS
TSTR:コマンド名受渡 = 美乳オイルエステ
SIF TALENT:TARGET:バストサイズ <= -1
	TSTR:コマンド名受渡 = 豊胸オイルエステ

;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_21
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_21
IF TALENT:TARGET:バストサイズ <= -1
	LOCALS = <br>■豊胸オイルエステ<br>
	LOCALS += "マッサージオイルを使って乳房を揉みほぐす。慎ましい胸にお悩みのあなたへ。"
	LOCALS += "<br>美肌にハリが出るほか、バストアップにも効果がある…ということになっている。"
	LOCALS += "<br>気持ちよくなればなるほど効果があるってことですからね～。声は我慢しなくて大丈夫ですからね～。"
	LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ｂ・露出・接触・歓楽・不快(警戒下)・反感(警戒下)"
	LOCALS += "<br>固有の取得経験：なし<br>"
	TSTR:ツールチップ受渡 = %LOCALS%
ELSE
	LOCALS = <br>■美乳オイルエステ<br>
	LOCALS += "マッサージオイルを使って乳房を揉みほぐす。"
	LOCALS += "<br>リンパの流れを良くすることで美乳・美肌効果が得られる…ということになっている。"
	LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ｂ・露出・接触・歓楽・不快(警戒下)・反感(警戒下)"
	LOCALS += "<br>固有の取得経験：なし<br>"
	TSTR:ツールチップ受渡 = %LOCALS%
ENDIF

@MASSAGE_COM21
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;Ｂ愛撫より全体的に強め。勿論です、プロですから。
;性交ではなくマッサージの中での行為のため露出強め。エロマッサージなので歓楽は通常より弱め
CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 150)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 100)

;胸を揉むため性欲を煽る。
BASE:TARGET:性欲 += 80

;警戒は理性の倍回復する。性欲の値で減少したりする。


IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -60)
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -60)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 900
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒140で理性が70回復するも、煽られる性欲と与えられる刺激で理性が80削られて理性変動は-10。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒56で理性28回復、そこから理性80低下でこの数字となる。理性と警戒の合計値はほんの少しだけ上がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -52)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 56)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF BASE:TARGET:理性 <= 200
;理性がグズグズに煮詰まっちゃってると、警戒及び理性の回復は半分になる。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 35)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 70)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF TALENT:TARGET:バストサイズ <= -1
;相手が貧乳以下の場合、実行可能であるならば「おっぱい大きくするためだし……」ってしぶしぶ受け入れるイメージ。
;育ててくれるからちょっとだけ友情も上がるし、理性と警戒の回復も抑えめになる。いっぱい揉んで育ててあげよう。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 40)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 80)
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)

ELSEIF TALENT:TARGET:バストサイズ > -1 && (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1100
;普乳以上かつ理性と警戒の削りが足りていないと、一気に警戒度が上がり、危機感で理性も回復してしまう。不快、反感も増える。
;理性200以下と共存しちゃうけど、この場合理性200以下のほうが優先となる。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 150)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 300)
	CALL SOURCE_CALC_不快(TARGET, PLAYER, 200)
	CALL SOURCE_CALC_反感(TARGET, PLAYER, 200)

ELSE
;基本値 警戒が1100～900
;反感まではいかないが、確実に回復してくる。このまま黒閃で反転術式の出力を戻されたらふりだしまで一直線なのでもっと理性を削りましょう。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 50)
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 140)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 80)
ENDIF


;施術着を着ている場合、25%で脱がす。ここに関してはプレイヤーが選択できたほうがいいかも？
IF TEQUIP:上半身下着あり && RAND:4 == 0
	TEQUIP:上半身下着あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF

IF TALENT:TARGET:バストサイズ <= -1
	CALL バスト変動処理(TARGET, "愛撫", 1)
ELSE
	CALL バスト変動処理(TARGET, "愛撫", -1)
ENDIF

IF TARGET == MASTER
	CALL ちんイラ増加処理(PLAYER, 50)
ELSE
	CALL ちんイラ増加処理(TARGET, 100)
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE21
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(21)
	RETURN RESULT

;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0

;相手に胸がないならダメ
SIF TALENT:TARGET:性別 == 2
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;実行するだけならそこそこ緩い。削りが足りてないと反感食らうし警戒されるけど。
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1400
	RETURN 0


RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM21
PRINTFORM %CALLNAME:PLAYER%は
	IF RFLAG:コマンド結果受渡し変数 == 1
	;施術着を脱がせたとき
		PRINTFORM %CALLNAME:TARGET%が着ている施術着%TEXTR("の胸元をずらして乳房を露出させ/の胸元をたくし上げて乳房を露出させ/を「施術のため」と言い張って剥ぎ取って")%、
	ELSEIF GROUPMATCH(TEQUIP:TARGET:上半身下着あり, 1, 3)
		PRINTFORM %CALLNAME:TARGET%が着ている施術着の%TEXTR("胸元に手を差し込んで/中へと手を滑り込ませて/上から")%、
	ENDIF
PRINTFORM %TEXTR("マッサージオイルをつけた手で/マッサージという建前で")%、%CALLNAME:TARGET%の
	IF TALENT:TARGET:バストサイズ == 3
	;魔乳
	;巨乳くらいまでは「デカパイ」とかそんな感じのワードでもいいかなと思ったんだけど、サイズ感の判別しっかりしてるほうがいいかもってことでTEXTRにはせず素質表記
	;魔乳とか「ただのデカパイじゃないぜ？ド級のデカパイ、『ドデカパイ』だ！」にしてもよかった気はしなくもない。笑っちゃわなければだけど
		PRINTFORM ずっしりと重い魔乳を
	ELSEIF TALENT:TARGET:バストサイズ == 2
	;爆乳
		PRINTFORM たわわに実った爆乳を
	ELSEIF TALENT:TARGET:バストサイズ == 1
	;巨乳
		PRINTFORM 豊満に育った巨乳を
	ELSEIF TALENT:TARGET:バストサイズ == 0
	;普乳
		PRINTFORM 手のひらから少しだけ溢れるサイズの%TEXTR("おっぱい/美乳")%を
	ELSEIF TALENT:TARGET:バストサイズ == -1
	;貧乳
		PRINTFORM つんとハリのある%TEXTR("ちっぱい/微乳")%を
	ELSEIF TALENT:TARGET:バストサイズ == -2
	;絶壁
	;フォリア様に「絶壁」だの「胸板」だの言うのは不敬！没収(コンフィスケイション)！死刑(デスペナルティ)！！
		PRINTFORM 胸板と言っていいほど平坦でありながら女性らしい柔らかさのある微乳を
	ENDIF
PRINTFORML %TEXTR("丁寧に揉みほぐした/たっぷりと揉みしだいた/ねっとりと揉みしだき堪能した")%。


;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 900
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%は%TEXTR("とろんと目元を蕩けさせ/蕩けた顔でだらしなく口をあけて/法悦に目元を蕩けさせて")%、
	CASE 1
		PRINTFORM %CALLNAME:TARGET%はだらしなく緩んだ口から%TEXTR("あられもない声/快楽に染まった嬌声/淫らな声")%を%TEXTR("あげて/こぼして/こぼしながら")%、
	ENDSELECT
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("の『マッサージ』/の手技/が与える快楽")%%TEXTR("に乳房を差し出してしまっている/に酔いしれている/にされるがままになっている/を受け入れている/を甘受している")%…
ENDIF
