;-------------------------------------------------
;大殿筋骨盤マッサージ
;-------------------------------------------------
@MASSAGE_COMNAME13
#FUNCTIONS
TSTR:コマンド名受渡 = 大殿筋骨盤マッサージ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_13
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("微エロマッサージ")

@MASSAGE_COM_TOOLTIP_13
LOCALS = <br>■大殿筋骨盤マッサージ<br>
LOCALS += "手のひら全体を使って大殿筋、つまりお尻を揉みほぐす。"
LOCALS += "<br>血行促進効果によりむくみが取れ、美尻になる効果がある。"
LOCALS += "<br>COMタイプ：微エロマッサージ<br>取得ソース：快Ａ・接触・露出・歓楽・不快(一定条件時)・反感(一定条件時)"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM13
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

IF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -50)
	BASE:TARGET:性欲 += 60
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -70)
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 67)
ELSE
;気持ちいいから理性は下がるし、性欲も煽られるが、ケツを揉むため少し警戒される。
;でも普通に真っ当な施術であり、卑猥が一切ないのは確定的に明らか。
	CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 67)
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -50)
	BASE:TARGET:性欲 += 60
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 20)
ENDIF

;セクハラ尻揉みとだいたい同じ。快Ａは丁寧にやる分でちょい強め。
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 80)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 200)

IF GETBIT(OPTION変数:愛撫スリーサイズ成長, 5)
	BASE:TARGET:変動ヒップ -= 1 * (1 + あなた特殊能力:肉体改造術 * 9) * (1 + GETBIT(TALENT:TARGET:スリーサイズ変動, 5))
ENDIF

IF TARGET == MASTER
	CALL ちんイラ増加処理(PLAYER, 35)
ELSE
	CALL ちんイラ増加処理(TARGET, 70)
ENDIF

;下半身服がある場合は……どうしよ？服着てても揉めるけど、オイルも使う。でも施術着だから服の上からオイル使っても問題はなさそう。
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE13
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(13)
	RETURN RESULT

;仰向けだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 0
	RETURN 0

;警戒が800以下でないとダメ
SIF BASE:TARGET:警戒 > 800
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM13
IF TALENT:TARGET:性別 == 2
	PRINTFORM %CALLNAME:PLAYER%はマッサージオイルをつけた手のひらで%CALLNAME:TARGET%の
		IF TALENT:TARGET:体格 >= 1
		;長身以上
			PRINTFORM みっちりと肉の張った
		ELSEIF TALENT:TARGET:体格 <= -1
		;小柄以下
			PRINTFORM 肉付きの薄い華奢な
		ENDIF
	PRINTFORM お尻の筋肉を丁寧に揉みほぐした。
ELSE
	PRINTFORM %CALLNAME:PLAYER%はマッサージオイルを垂らし、手のひら全体を使って%CALLNAME:TARGET%の、
		IF 下半身体型(TARGET) >= 1
		;肉感的以上
			PRINTFORM %TEXTR("むっちりとした/もっちりとした/むっちりと肉の乗った/もっちりと肉感的な")%
		ELSEIF 下半身体型(TARGET) <= -2
		;細身以下
			PRINTFORM 肉付きの薄い華奢な
		ENDIF
		SIF CFLAG:TARGET:発情期フラグ < 0
			PRINTFORM %TEXTR("発情期の/発情した/発情に火照った")%
		IF TALENT:TARGET:恋人持ち == 2
			PRINTFORM 人妻の
		ELSEIF TALENT:TARGET:恋人持ち == 3
			PRINTFORM 未亡人の
		ENDIF
	PRINTFORML %TEXTR("尻たぶ/お尻/尻肉")%を丁寧に揉みしだき、堪能した。
		IF PALAM:欲情  >= PALAMLV:2
			PRINTL
			PRINTFORML %CALLNAME:PLAYER%が揉みこむたびに、愛液で濡れた施術着が小さく水音をたてるのを、%CALLNAME:TARGET%は熱く息をしながら恥ずかしそうに聴いている…
		ENDIF
ENDIF
