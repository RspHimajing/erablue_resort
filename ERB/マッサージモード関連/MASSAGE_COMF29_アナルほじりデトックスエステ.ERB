;-------------------------------------------------
;アナルほじりデトックスエステ
;-------------------------------------------------
@MASSAGE_COMNAME29
#FUNCTIONS
TSTR:コマンド名受渡 = アナルほじりデトックスエステ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_29
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_29
LOCALS = <br>■アナルほじりデトックスエステ<br>
LOCALS += "お尻の穴に指を入れ、直腸から刺激を与えるマッサージ。"
LOCALS += "<br>直腸刺激はデトックス効果があり、ハリのある美尻になる…ということにしている。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ａ・露出・接触・歓楽・液体"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM29
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる


CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 170)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

;ケツ穴スボスボなのでＡ感1以下は苦痛。
SIF ABL:TARGET:Ａ感覚 <= 1
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 40)

;Ａ感0はさらに苦痛。
SIF ABL:TARGET:Ａ感覚 == 0
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 30)

;ローションは別のコマンドになるっぽいので気持ち程度
CALL SOURCE_CALC_液体(TARGET, PLAYER, 1000)

;下半身下着がある場合は脱がす。
IF TEQUIP:下半身下着あり > 0
	TEQUIP:下半身下着あり = 0
	TEQUIP:下半身服あり = 0
	SIF TEQUIP:上下一体服
		TEQUIP:上半身服あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF
IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 150
	BASE:TARGET:警戒 += 警戒変動値算出(TARGET, -40)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合でも「流石にこれはダメなやつなのでは？」と思いはじめる。が、時既に遅し。
;警戒90で理性が45上がり、そこから理性が160下がるイメージでこの数値。ここにきて未だに警戒90なのが無知シチュの『味』。
;ケツ穴での性感に？？！？！となっているのでちょっとだけ性欲抑えめ。シヴァさん？？？？
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -115)
	BASE:TARGET:性欲 += 160
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 90)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSEIF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 400
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒250で理性が125回復するも、煽られる性欲と与えられる刺激で理性が200削られて理性変動は-75。
;……というのが平常時における挙動だが、ここまで削られていると与えられる快感に警戒心が塗りつぶされてしまう。
;よって警戒100で理性50回復、そこから理性200低下でこの数字となる。理性と警戒の合計値は少しだけ下がり、そのまま次のコマンドへ。
	BASE:TARGET:理性 += 理性変動値算出(TARGET, -150)
	BASE:TARGET:性欲 += 200
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 100)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
ELSE
;実行に際し十分な理性・警戒削りがなされていない場合。
;性欲は煽れるが、警戒と理性が跳ねる。警戒心がビンっ！ってなるので、煽られる性欲の理性低下を撥ね退けてしまう。
	BASE:TARGET:理性 +=理性変動値算出(TARGET, 125)
	BASE:TARGET:性欲 += 150
	BASE:TARGET:警戒 +=警戒変動値算出(TARGET, 250)
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ENDIF

BASE:TARGET:ちんイラ = MAX(0, BASE:TARGET:ちんイラ - 300)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE29
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

SIF GLOBAL_MASSAGE_COMABLE(29)
	RETURN RESULT

;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0

;仰向けだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 0
	RETURN 0

;理性警戒の合計が800以下じゃないとダメ
;実行自体はできるが、この時点だと警戒と理性が跳ねる。されるがままになっちゃうのは合計値400以下くらいを想定。
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 800
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM29

PRINTFORM %CALLNAME:PLAYER%は、

	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM %CALLNAME:TARGET%の
			;Ｖがあり欲情している場合
			SIF TALENT:TARGET:Ｖ感度 >= -1 && PALAM:欲情  >= PALAMLV:3
				PRINTFORM ぐっしょりと愛液を吸った
		PRINTFORM 施術着を脱がし、
	ENDIF
PRINTFORM オイルでたっぷり濡らした指を%CALLNAME:TARGET%の
	IF ABL:TARGET:Ａ感覚 >= 3
		PRINTFORM %TEXTR("こなれた/よくほぐれた")%%TEXTR("アナル/アヌス/ケツ穴/ケツまんこ")%
	ELSE
		PRINTFORM %TEXTR("締め付けの強い/キツキツの")%%TEXTR("アナル/尻の穴/肛門")%
	ENDIF
	
	PRINTFORM に挿し入れて、
	IF TALENT:TARGET:性別 == 2 && RAND:3 == 0
	;男かつ33％。前立腺は男にしか存在しないので、これはこの上なく男らしい行為のはずである。
		PRINTFORML 直腸の内壁から前立腺を%TEXTR("捏ねるようにマッサージした/指圧でぐりぐりと刺激した")%。
	ELSE
		PRINTFORML %TEXTR("内側から揉みほぐすようにねっとりとかき混ぜた/ぐちゃぐちゃと水音を立てながらかき回した/直腸の内壁を指の腹で押し込むように刺激した")%
	ENDIF
	
	
;削りが十分な時は追加のメッセージ
IF (BASE:TARGET:理性 + BASE:TARGET:警戒) < 400
	PRINTW 
	SELECTCASE RAND:2
	CASE 0
		PRINTFORM %CALLNAME:TARGET%は%TEXTR("とろんと目元を蕩けさせ/蕩けた顔でだらしなく口をあけて/法悦に目元を蕩けさせて")%、
	CASE 1
		PRINTFORM %CALLNAME:TARGET%はだらしなく緩んだ口から%TEXTR("あられもない声/快楽に染まった嬌声/淫らな声")%を%TEXTR("あげて/こぼして/こぼしながら")%、
	ENDSELECT
	PRINTFORML %CALLNAME:PLAYER%%TEXTR("の『マッサージ』/の手技/が与える快楽")%%TEXTR("にアナルを差し出してしまっている/に酔いしれている/にされるがままになっている/を受け入れている/を甘受している")%…
ENDIF
