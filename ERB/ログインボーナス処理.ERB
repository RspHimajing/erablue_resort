

;-------------------------------------------------------------------------------------------
;リアル時間で一日一回限定の処理
;ロード時に発生
;-------------------------------------------------------------------------------------------

@ログインボーナス処理

CUSTOMDRAWLINE ＊
PRINTL 
PRINTL ログインボーナス！
PRINTL 
CUSTOMDRAWLINE ＊
PRINTL 
PRINTL 
WAIT
ログイン累計日数 += 1

CALL 特殊ログボ処理

;以下通常ログボ
PRINTL 【通常ログインボーナス】
PRINTFORM {ログイン累計日数}日目のログインとして、
SELECTCASE ログイン累計日数 % 7
	CASE 1
		PRINTL 「5ZP」を手に入れた！
		FLAG:ZP所持量 += 5
	CASE 2
		PRINTL 「１万ルピ」を手に入れた！
		MONEY += 10000
	CASE 3
		SELECTCASE RAND:4
			CASE 0
				PRINTL 「eraの友情アニマ」を３個手に入れた！
				ITEM:eraの友情アニマ += 3
			CASE 1
				PRINTL 「eraの信頼アニマ」を３個手に入れた！
				ITEM:eraの信頼アニマ += 3
			CASE 2
				PRINTL 「eraの恋心アニマ」を３個手に入れた！
				ITEM:eraの恋心アニマ += 3
			CASE 3
				PRINTL 「eraの性愛アニマ」を３個手に入れた！
				ITEM:eraの性愛アニマ += 3
		ENDSELECT
	CASE 4
		PRINTL 「輸入品リロールチケット」を１枚手に入れた！
		ITEM:輸入品リロールチケット += 1
	CASE 5
		PRINTL 「eraの恋慕アニマ」を３個手に入れた！
		ITEM:eraの恋慕アニマ += 3
	CASE 6
		PRINTL 「３万ルピ」を手に入れた！
		MONEY += 30000
	CASE 0
		PRINTL 「30ZP」を手に入れた！
		FLAG:ZP所持量 += 30
ENDSELECT

FORCEWAIT



@特殊ログボ処理

IF ログイン累計日数 == 1
	PRINTL ガチャ◯◯：
	PRINTW 「こーんにちはー！　ガチャ◯◯です！
	PRINTW 　erablue_resortをプレイしてくれてありがとう！
	PRINTW 　このログインボーナスは、ロード時にいろんなアイテムが貰えるお得なシステムだよ。
	PRINTW 　一日に一回貰えるから忘れずにログインしてね！」
	PRINTL 
	PRINTL ※ログインボーナスはセーブデータごとに別管理されます。
	PRINTW ※一日の切り替わりタイミングはAM 5時です。
	PRINTL 
	PRINTL ガチャ◯◯：
	PRINTW 「今日は初日だから特別に、「eraの友情アニマ」を追加で３つプレゼント！
	PRINTW 　使った相手の好感度をたくさん上げる凄いアイテムだから、ぜひ使ってみてね！
	PRINTW 　それじゃあ、erablue_resortをお楽しみください！　ばいばーい！」
	PRINTL 
	PRINTW 「eraの友情アニマ」を３個手に入れた！
	PRINTL 
	PRINTL 
	ITEM:eraの友情アニマ += 3
ENDIF



