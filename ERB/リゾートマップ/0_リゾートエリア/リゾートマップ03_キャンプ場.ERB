

@マップ紹介_3
;とりあえず5行
PRINTFORML ※紹介文募集中※
PRINTFORML 
PRINTFORML 
PRINTFORML 
PRINTFORML 


@MAP_RESORT_0_3(AA)
#DIMS REF AA, 0


AA:00 = 山山山山山山山木木木木川川川木木木木木木山山山山山木木木木木木木木木木木木木
AA:01 = 山山山山山木木木木　　川川川　　　　木木木山山山木木木木木木　　　　　木木木
AA:02 = 山山木木木木木　　　川川川川　　　　　　木木木木木　　　　　　　　　　　　木
AA:03 = 山木木木木木　　　　川川川　　　　　　　　　木木　　　　　　　　　　　　　
AA:04 = 木木木木　　　　　　川川川──┐　　　　　　　　　　　　　┌─────┐　→02
AA:05 = 木木木　　　　　　川川川川　　│　　　　　　　　　　　　　│　　　　　│
AA:06 = 木木　　　　　　　川川川　　　│　　　　　　　　　　　　　│　　03　　│
AA:07 = 木　　　　　　　　川川川　05　│　　　　　　　　　　　　　│　　　　　│
AA:08 = 木　　　　　　　　川川川　　　│　　　　　　　　　　　　　└─────┘
AA:09 = 木　　　　　　　　川川川───┘　　　　　　　　　　　　　　　　　　　　　木
AA:10 = 　　　　　　　　　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木
AA:11 = 　　　　　　　　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木木
AA:12 = 　　　　　　　　川川川　　　　　　　　　　　　　┌─┐　　　　　　　　　木木
AA:13 = 　　　　　　　　川川川　　　　　　　　　　　　　│07│　　　　　　　　　　木
AA:14 = 　　　　　　　川川川川川　　　　　　　　　　　　└─┘　　　　　　　　　　木
AA:15 = 　　　　　湖湖湖湖湖湖湖湖湖　　┌─┐
AA:16 = 　　　湖湖湖湖湖湖湖湖湖湖湖湖湖│06│
AA:17 = 　　　湖湖湖湖湖湖湖湖湖湖湖湖湖└─┘
AA:18 = 　　　湖湖湖湖湖湖湖湖湖湖湖湖湖
AA:19 = 　　　　湖湖湖湖湖湖湖湖湖湖湖　　　　　　　┌─────────┐
AA:20 = 　　　　　湖湖湖湖湖湖湖湖湖　　　　　　　　│　　　　　　　　　│　　　　木
AA:21 = 木　　　　　　湖湖湖湖湖　　　　　　　　　　│　　　　04　　　　│　　　　木
AA:22 = 木　　　　　　　川川川　　　　　　　　　　　│　　　　　　　　　│　　　木木
AA:23 = 木　　　　　　川川川川　　　　　　　　　　　└─────────┘　　木木木
AA:24 = 木木　　　　　川川川　　　　　　　　　　　　　　　　　　　　　　木木木木木木
AA:25 = 木木木　　　川川川川　　　　　　　　　　　　　　　　　　　　　木木木木木木木
AA:26 = 
AA:27 = 二：ロビーへ　　　　　三：メインテント　　　四：バーベキューエリア
AA:28 = 五：渓流エリア　　　　六：サウナテント　　　七：キャンプファイアー

IF 施設改造度:3:8 > 0
	AA:10 = 　┌────┐　　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木
	AA:11 = 　│　　　　│　川川川　　　　　　　　　　　　　　　　　　　　　　　　　木木
	AA:12 = 　│　08　　│　川川川　　　　　　　　　　　　　┌─┐　　　　　　　　　木木
	AA:13 = 　│　　　　│　川川川　　　　　　　　　　　　　│07│　　　　　　　　　　木
	AA:14 = 　└────┘川川川川川　　　　　　　　　　　　└─┘　　　　　　　　　　木

	AA:29 = 八：訓練場
ENDIF

@GETPLACENAME_0_3(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 302
		TSTR:場所名受渡 = ロビー
	CASE 303
		TSTR:場所名受渡 = メインテント
	CASE 304
		TSTR:場所名受渡 = バーベキューエリア
	CASE 305
		TSTR:場所名受渡 = 渓流エリア
	CASE 306
		TSTR:場所名受渡 = サウナテント
	CASE 307
		TSTR:場所名受渡 = キャンプファイアー
	CASE 308
		IF 施設改造度:3:8 > 0
			TSTR:場所名受渡 = 訓練場
		ENDIF
ENDSELECT


@DRAW_MAP_BG_0_3(SLICED, 表示文字列)
#DIMS REF 表示文字列
#DIMS SLICED
SELECTCASE SLICED
;色つけたいときはこんな感じ
CASE "木", "竹"
	表示文字列 += @"<font color='#90EE90'>%SLICED%</font>"
CASE "山"
	表示文字列 += @"<font color='#5C985C'>%SLICED%</font>"
CASE "川", "湖"
	表示文字列 += @"<font color='#5C5CFF'>%SLICED%</font>"
;違う文字を表示させる例
;下部一覧の数字を三桁表示しつつボタン化させる
CASE "二"
	表示文字列 += @"<button value='302'>302</button>"
CASE "三"
	表示文字列 += @"<button value='303'>303</button>"
CASE "四"
	表示文字列 += @"<button value='304'>304</button>"
CASE "五"
	表示文字列 += @"<button value='305'>305</button>"
CASE "六"
	表示文字列 += @"<button value='306'>306</button>"
CASE "七"
	表示文字列 += @"<button value='307'>307</button>"
CASE "八"
	表示文字列 += @"<button value='308'>308</button>"
;ふつうの
CASEELSE
	表示文字列 += @"%SLICED%"
ENDSELECT

;サウナテントにいる場合の特殊イベント
@COM_END_PLACEEVENT_0_306

;別の場所からサウナに来たならまず熱気ゲージリセット
IF CFLAG:MASTER:前ターン位置 != CFLAG:MASTER:現在位置
	PRINTL サウナの中からはむわっとした熱気が漂ってくる
	PRINTW あまり長い間、留まることは出来ないだろう
	熱気ゲージ = 0
	RETURN 0
ENDIF

;特定のコマンドの場合実行しない（移動、覗く、川で冷やした場合）
SIF SELECTCOM == 400 || SELECTCOM == 404 || SELECTCOM == 380
	RETURN 0

;オートコマンド時は発動しない
SIF TFLAG:オートコマンドフラグ
	RETURN 0

;サウナにいるキャラ全員の歓楽と露出と苦痛を少量増加させる。水で冷やしたターンは増加しない。
;その場に居るTARGET全員で回す
IF 熱気ゲージ >= 0
	LOCAL:1 = TARGET
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):睡眠
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):隠密
			CONTINUE
		TARGET = TARGET:LOCAL
		
		CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 70)
		CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
	NEXT
	TARGET = LOCAL:1
ENDIF

;熱気ゲージを増加させ、値により反応を返す。水で冷やしたターンは増加しない。
;具体的な数値は表示しない隠しパラメータとする

熱気ゲージ += RAND:15 + 20
DRAWLINE
SELECTCASE 熱気ゲージ
	CASE IS < 20
		;0～19
		PRINTL まだまだ余裕でサウナに入っていられそうだ。
		RETURN 0
	CASE IS < 40
		;20～39
		PRINTL 体がいい感じに温まってきた。
		RETURN 0
	CASE IS < 60
		;40～59
		PRINTL 熱くなってきた。そろそろ冷やした方がいいかもしれない。
		RETURN 0
	CASE IS < 80
		;60～79
		PRINTL かなり熱い！
		PRINTL 頭がぼーっとしてきた…
		RETURN 0
	CASE IS < 100
		;80～99
		;サウナに弱いだとギブアップ
		IF TALENT:MASTER:サウナ耐性 == -1
		ELSE
			PRINTL 限界ギリギリだ！
			PRINTL 体を冷やせるものはないだろうか？
			RETURN 0
		ENDIF
	CASEELSE
		;100～
		;通常時ギブアップ
		IF TALENT:MASTER:サウナ耐性 == 1 && 熱気ゲージ < 120
			PRINTL 限界ギリギリだ！
			PRINTL 体を冷やせるものはないだろうか？
			RETURN 0
		ENDIF
ENDSELECT


PRINTL もうだめだ！
PRINTFORML %CALLNAME:MASTER%は熱さに耐え兼ね、サウナから飛び出した！
PRINTL 別の場所でしばらく涼み、ようやく体の火照りが収まってきた…

TIME += 30
CFLAG:MASTER:現在位置 = 303
;隠密時解除
CFLAG:MASTER:隠密 = 0
CFLAG:TARGET:隠密 = 0
CALL うふふ終了処理("サウナ限界")
RETURN 1


@庭系列改造メニュー()
#FUNCTIONS
;100~199と999は大本のメニュー操作で使うため、使用しないこと

LOCALS = リゾートハウスから通じる野外周辺を改装します。<br><br>
IF 施設改造度:3:0 < 1
	LOCALS += @"■<button value='200'>[キャンプ地改造]</button> Lv{施設改造度:3:0}>{施設改造度:3:0 + 1} （必要:150,000ルピ）<br>"
ELSE
	LOCALS += @"■[キャンプ地改造] Lv{施設改造度:3:0}：上限<br>"
ENDIF
IF 施設改造度:3:0 > 0 && 施設改造度:3:8 < 1
	LOCALS += @"└■<button value='201'>[訓練場を増築する]</button> （必要:80,000ルピ）<br>"
ELSEIF 施設改造度:3:8 > 0
	LOCALS += @"└■[訓練場を増築する] 増築済<br>"
ENDIF

RETURNF LOCALS

@庭系列改造実行関数(ARG)

SELECTCASE ARG
	CASE 200
		DRAWLINE
		PRINTL 庭がキャンプ場になり、以下の施設が導入されます。
		PRINTBUTTON "■キャンプ場個別マップの解放", 300
		PRINTL 
		PRINTBUTTON "■メインテントの導入", 301
		PRINTL 
		PRINTBUTTON "■バーベキューエリアの導入", 302
		PRINTL 
		PRINTBUTTON "■渓流エリアの導入", 303
		PRINTL 
		PRINTBUTTON "■サウナテントの導入", 304
		PRINTL 
		PRINTBUTTON "■キャンプファイアーの導入", 305
		PRINTL 
		PRINTL 
		PRINTL 庭をキャンプ場に改造しますか？（必要:150,000ルピ）
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		BINPUT
		SELECTCASE RESULT
			CASE 0
				IF MONEY < 150000
					PRINTW ルピが足りません。
					RETURN
				ENDIF
				施設改造度:3:0 = 1
				MONEY -= 150000
				PRINTL 庭をキャンプ場に改造しました！
				PRINTW (滞在キャラ上限＋５)
				CALL 滞在可能キャラ数再計算
				CALL RESET_GRAPH_0
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"庭をキャンプ場Lv1に改造した","実績")
				RETURN
			CASE 1
				RETURN
			CASE 300
				DRAWLINE
				PRINTL マップの解放により、キャンプ場に移動すると個別のマップへと移動するようになります。
				PRINTL 改造が進むと更にマップが拡張され、内部の施設を強化出来るようになります。（未実装です）
				PRINTW
			CASE 301
				DRAWLINE
				PRINTL キャンプの拠点となる大きなテントです。
				PRINTL 焚火を囲みながらのんびりと過ごすことが出来ます。
				PRINTL また、キャンプ中のキャラクターは夜間ここで睡眠するようになります。
				PRINTW
			CASE 302
				DRAWLINE
				PRINTL 肉や魚などが用意されたバーベキューエリアです。
				PRINTL 料理や飲酒が可能なため、キャンプ中のキャラクターは昼食・夕食をここで取るようになります。
				PRINTL また、夜間はここで宴会をするキャラクターもいるでしょう。（未実装です）
				PRINTW 
			CASE 303
				DRAWLINE
				PRINTL 綺麗な川で遊ぶことが出来る渓流エリアです。
				PRINTL ゆっくりと釣りを嗜んだり、水遊びで楽しく過ごすことが出来ます。
				PRINTW 
			CASE 304
				DRAWLINE
				PRINTL サウナを楽しめるテント施設です。
				PRINTL 飛び込むことの出来る冷たい湖に併設されています。
				PRINTW 
			CASE 305
				DRAWLINE
				PRINTL 非常に大きく燃え上がる炎と、それの骨組みです。
				PRINTL 18時より前は炎は付いていません。
				PRINTL 夜になれば多くのキャラクターがここに集まるでしょう。
				PRINTW 
		ENDSELECT
		RESTART
	CASE 201
		DRAWLINE
		PRINTL キャンプ場の外れに訓練場が導入されます。
		PRINTL 訓練場を導入することで、長期雇用をしているキャラクターが一日の終わりに経験値を得るようになります。
		PRINTL また従業員の仕事割り振りに「訓練」が追加され、一日の終わりに経験値を得ることができるようになります。
		PRINTL ただし、どちらも「全てのキャラクターのうち最も高いレベル」までしか上昇しません。
		PRINTL 
		PRINTL 訓練場を導入しますか？（必要:80,000ルピ）
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		INPUT
		SELECTCASE RESULT
			CASE 0
				IF MONEY < 80000
					PRINTW ルピが足りません。
					RETURN
				ENDIF
				施設改造度:3:8 = 1
				MONEY -= 80000
				PRINTL 訓練場を導入しました！
				PRINTW (滞在キャラ上限＋５)
				CALL 滞在可能キャラ数再計算
				CALL RESET_GRAPH_0
				RETURN
			CASE 1
				RETURN
		ENDSELECT
		RESTART
ENDSELECT







