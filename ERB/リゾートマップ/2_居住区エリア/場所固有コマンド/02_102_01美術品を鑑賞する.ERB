
;-------------------------------------------------
;美術館で美術品を鑑賞する
;-------------------------------------------------
@COMNAME_PLACE_380_2_102
#FUNCTION
TSTR:コマンド名受渡 = 美術品を鑑賞する
RETURNF 1
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_2_102
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")
		
@COM380_2_102
#DIM DYNAMIC 順番シャッフル用配列, キャラクタ数上限
#DIM シャッフル数

IF 知識素質:TARGET:絵師 != 0 && !TCVAR:TARGET:美術館鑑賞フラグ
	;ターゲットが作品ある場合、一日1回は絶対それを見る
	RFLAG:コマンド結果受渡し変数 = TARGET
	RETURN 1
ENDIF

;仮置きなので適当に定住者からランダムに選ぶ
IF RAND:2
	;キャラの作品
	CALL キャラ番号配列_初期化_シャッフル(順番シャッフル用配列, 0)
	シャッフル数 = RESULT

	FOR LOCAL, 0, シャッフル数
		;定住でも従業員でもないキャラはスルー
		SIF !TALENT:(順番シャッフル用配列:LOCAL):従業員 && !TALENT:(順番シャッフル用配列:LOCAL):定住者
			CONTINUE
		;定住でも従業員でもないキャラはスルー
		SIF !TALENT:(順番シャッフル用配列:LOCAL):従業員 && !TALENT:(順番シャッフル用配列:LOCAL):定住者
			CONTINUE
		;絵師が０だとスルー（マイナスはOK）
		SIF 知識素質:(順番シャッフル用配列:LOCAL):絵師 == 0
			CONTINUE
		;一番最初に見つけたキャラを登録しておしまい
		RFLAG:コマンド結果受渡し変数 = 順番シャッフル用配列:LOCAL
		BREAK
	NEXT
	RETURN 1
ENDIF

;キャラ作品ではない時は汎用
RFLAG:コマンド結果受渡し変数 = -1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_2_102
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_2_102

IF RFLAG:コマンド結果受渡し変数 == -1
	PRINTFORM %CALLNAME:PLAYER%は
	PRINTDATA
		DATA 古物商から買い取った
		DATA ダンジョンで見つけた
		DATA よろず屋から売り込まれた
		DATA 流浪の芸術家から譲り受けた
	ENDDATA
	PRINTDATA
		DATA 絵画
		DATA 彫像
		DATA 壺
		DATA 宝刀
	ENDDATA
	PRINTL を眺めている
	PRINTDATAL
		DATA 審美眼が磨かれたような気がする！
		DATA 静かに心が落ち着いていくようだ……
		DATA 実に興味深い作品であると感じたようだ
		DATA 見る角度を変えれば、乳房のように見える部位を発見した！
	ENDDATA
ELSE
	;ここに口上用の分岐いれるけど今はまだ空っぽ
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:(RFLAG:コマンド結果受渡し変数)%が描いた
	PRINTDATA
		DATA 風景画
		DATA イラスト
		DATA 油絵
		DATA 人物画
		DATA 似顔絵
		DATA 魔物の絵
	ENDDATA
	PRINTL を眺めている
	SELECTCASE 知識素質:(RFLAG:コマンド結果受渡し変数):絵師
		CASE -1
			PRINTDATAL
				DATAFORM これは……なんだろうか。猫？　犬？　タイトルを見ると、%CALLNAME:PLAYER%と書かれていた……
				DATA 名状し難い絵だ。今にも動き出しそうな迫力はあるが……いや、実際に動いている？　未知の生命が生まれてないか？
				DATA 騙し絵のように見ているだけで不安定になってくる。心を揺さぶる、という意味では名作の範疇に入るかもしれない
			ENDDATA
		CASE 1
			PRINTDATAL
				DATA 荒削りながらも意思を感じる良い絵だ
				DATA プロには敵わないが、これはこれで味のある良い絵だろう
				DATA なかなか上手な絵だ。見る人が見れば才能を感じさせるだろう
			ENDDATA
		CASE 2
			PRINTDATAL
				DATA 筆致の一つ一つに意図が感じられる良い絵だ
				DATA 全体の構図に素晴らしい技術を感じる、計算された絵だ
				DATA 斬新な色使いを見事にまとめた挑戦的な絵だ
			ENDDATA
		CASE 3
			PRINTDATAL
				DATA ……世界に誇れるほどの出来だ！　これ単体で観光客を呼べるほどだろう
				DATA まさに名作だ！　値段を付ければ何億ルピになるだろうか……
				DATA 美を極めていると言っても過言ではない。見ているだけで心が澄んでいくようだ
			ENDDATA
	ENDSELECT
ENDIF
