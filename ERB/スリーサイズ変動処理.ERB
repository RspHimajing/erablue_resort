@バスト変動処理(対象キャラ, 変動理由, 変動数値)
#DIMS 変動理由
#DIM 変動数値
#DIM 対象キャラ

SIF 変動数値 > 0 && !GETBIT(OPTION変数:愛撫スリーサイズ成長, 0)
	RETURN 0
SIF 変動数値 < 0 && !GETBIT(OPTION変数:愛撫スリーサイズ成長, 1)
	RETURN 0

RESULT = 0
TRYCALLFORM バスト変動固有処理_{NO:対象キャラ}(対象キャラ, 変動理由, 変動数値)
;RESULTが0なら通常処理へ、非0ならここで返す
SIF RESULT
	RETURN 0

SIF TALENT:対象キャラ:体型固定
	RETURN 0

SIF あなた特殊能力:肉体改造術 > 0
	変動数値 = 変動数値 + 10

SELECTCASE 変動理由
	CASE "妊娠"
		BASE:対象キャラ:変動バスト += 変動数値
	CASE "愛撫"
		BASE:対象キャラ:変動バスト += 変動数値
	CASE "食事"
		IF TALENT:対象キャラ:太りやすい == -2
			;太らない
			SIF 変動数値 > 0
				変動数値 = 変動数値 * 2 / 5
		ENDIF
		BASE:対象キャラ:変動バスト += 変動数値
	CASEELSE
		BASE:対象キャラ:変動バスト += 変動数値
ENDSELECT

@ウエスト変動処理(対象キャラ, 変動理由, 変動数値)
#DIMS 変動理由
#DIM 変動数値
#DIM 対象キャラ


SIF 変動数値 > 0 && !GETBIT(OPTION変数:愛撫スリーサイズ成長, 2)
	RETURN 0
SIF 変動数値 < 0 && !GETBIT(OPTION変数:愛撫スリーサイズ成長, 3)
	RETURN 0

RESULT = 0
TRYCALLFORM ウエスト変動固有処理_{NO:対象キャラ}(対象キャラ, 変動理由, 変動数値)
;RESULTが0なら通常処理へ、非0ならここで返す
SIF RESULT
	RETURN 0

SIF TALENT:対象キャラ:体型固定
	RETURN 0

SIF あなた特殊能力:肉体改造術 > 0
	変動数値 = 変動数値 + 10

SELECTCASE 変動理由
	CASE "妊娠"
		BASE:対象キャラ:変動ウエスト += 変動数値
	CASE "愛撫"
		BASE:対象キャラ:変動ウエスト += 変動数値
	CASE "食事"
		SELECTCASE TALENT:対象キャラ:太りやすい
			CASE 1
				;太りやすい
				IF 変動数値 > 0
					変動数値 = 変動数値 * 3 / 2
				ELSE
					変動数値 = 変動数値 / 2
				ENDIF
			CASE -1
				;太りにくい
				IF 変動数値 > 0
					変動数値 = 変動数値 / 2
				ELSE
					変動数値 = 変動数値 * 3 / 2
				ENDIF
			CASE -2
				;太らない
				SIF 変動数値 > 0
					RETURN 0 
		ENDSELECT
		BASE:対象キャラ:変動ウエスト += 変動数値
	CASEELSE
		BASE:対象キャラ:変動ウエスト += 変動数値
ENDSELECT

@ヒップ変動処理(対象キャラ, 変動理由, 変動数値)
#DIMS 変動理由
#DIM 変動数値
#DIM 対象キャラ


SIF 変動数値 > 0 && !GETBIT(OPTION変数:愛撫スリーサイズ成長, 4)
	RETURN 0
SIF 変動数値 < 0 && !GETBIT(OPTION変数:愛撫スリーサイズ成長, 5)
	RETURN 0

RESULT = 0
TRYCALLFORM ヒップ変動固有処理_{NO:対象キャラ}(対象キャラ, 変動理由, 変動数値)
;RESULTが0なら通常処理へ、非0ならここで返す
SIF RESULT
	RETURN 0

SIF TALENT:対象キャラ:体型固定
	RETURN 0

SIF あなた特殊能力:肉体改造術 > 0
	変動数値 = 変動数値 + 10

SELECTCASE 変動理由
	CASE "妊娠"
		BASE:対象キャラ:変動ヒップ += 変動数値
	CASE "愛撫"
		BASE:対象キャラ:変動ヒップ += 変動数値
	CASE "食事"
		SELECTCASE TALENT:対象キャラ:太りやすい
			CASE 1
				;太りやすい
				IF 変動数値 > 0
					変動数値 = 変動数値 * 4 / 3
				ELSE
					変動数値 = 変動数値 * 2 / 3
				ENDIF
			CASE -1
				;太りにくい
				IF 変動数値 > 0
					変動数値 = 変動数値 * 2 / 3
				ELSE
					変動数値 = 変動数値 * 4 / 3
				ENDIF
			CASE -2
				;太らない
				SIF 変動数値 > 0
					変動数値 = 変動数値 * 2 / 5
		ENDSELECT
		BASE:対象キャラ:変動ヒップ += 変動数値
	CASEELSE
		BASE:対象キャラ:変動ヒップ += 変動数値
ENDSELECT

@補正込みバストサイズ(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 補正後数値

補正後数値 = BASE:対象キャラ:バスト + MAX(0, BASE:対象キャラ:変動バスト / 10)

RETURNF 補正後数値

@補正込みウエストサイズ(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 補正後数値

補正後数値 = BASE:対象キャラ:ウエスト

SELECTCASE PREGNANCY_SIZE(対象キャラ)
	CASE 2
		補正後数値 = (補正後数値 * 105 / 100) * (102 - (CFLAG:対象キャラ:人物番号 % 5)) / 100
	CASE 3
		補正後数値 = (補正後数値 * 115 / 100) * (102 - (CFLAG:対象キャラ:人物番号 % 5)) / 100
	CASE 4
		補正後数値 = (補正後数値 * 140 / 100) * (102 - (CFLAG:対象キャラ:人物番号 % 5)) / 100
	CASE 5
		補正後数値 = (補正後数値 * 165 / 100) * (102 - (CFLAG:対象キャラ:人物番号 % 5)) / 100
	CASE 6
		補正後数値 = (補正後数値 * 180 / 100) * (102 - (CFLAG:対象キャラ:人物番号 % 5)) / 100
ENDSELECT

補正後数値 = 補正後数値 + MAX(0, BASE:対象キャラ:変動ウエスト / 10)

RETURNF 補正後数値

@補正込みヒップサイズ(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 補正後数値

補正後数値 = BASE:対象キャラ:ヒップ + MAX(0, BASE:対象キャラ:変動ヒップ / 10)

RETURNF 補正後数値

@スリーサイズ変動適用(対象キャラ)
#DIM 対象キャラ

;成長したスリーサイズでTALENTをセットしなおす作業
CALL RESET_BUSTSIZE(対象キャラ)
CALL RESET_WAISTSIZE(対象キャラ)
CALL RESET_HIPSIZE(対象キャラ)
