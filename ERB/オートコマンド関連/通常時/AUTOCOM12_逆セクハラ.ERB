;------------------------------------------------------------------------------
;通常時、逆セクハラ
;------------------------------------------------------------------------------
@AUTOCOM12
#DIM セクハラ部位
#DIM セクハラ快楽量
CALL SOURCE_CALC_歓楽(AUTOCOM_実行番号保存, PLAYER, 500)
CALL SOURCE_CALC_好感度要素_性愛度UP(AUTOCOM_実行番号保存, PLAYER, 250)

;快楽計算
;部位決定
セクハラ部位 = WEIGHTED_RANDOM_PART(PLAYER)

;快楽量決定、基本値から諸々補正
SELECTCASE 知識素質:AUTOCOM_実行番号保存:性知識
	CASE IS < 0
		セクハラ快楽量 = 30
	CASE 0
		セクハラ快楽量 = 100
	CASE 1
		セクハラ快楽量 = 200
	CASE 2
		セクハラ快楽量 = 300
	CASE 3
		セクハラ快楽量 = 500
ENDSELECT

セクハラ快楽量 += ABL:AUTOCOM_実行番号保存:技巧 * 30
セクハラ快楽量 += TALENT:AUTOCOM_実行番号保存:経験豊富 * 50
セクハラ快楽量 += TALENT:AUTOCOM_実行番号保存:器用な指 * 20

SELECTCASE セクハラ部位
	CASE 0
		CALL SOURCE_CALC_快Ｃ(PLAYER, AUTOCOM_実行番号保存, セクハラ快楽量)
	CASE 1
		CALL SOURCE_CALC_快Ｖ(PLAYER, AUTOCOM_実行番号保存, セクハラ快楽量)
	CASE 2
		CALL SOURCE_CALC_快Ａ(PLAYER, AUTOCOM_実行番号保存, セクハラ快楽量)
	CASE 3
		CALL SOURCE_CALC_快Ｂ(PLAYER, AUTOCOM_実行番号保存, セクハラ快楽量)
ENDSELECT

;口上用に数値受け渡し
RFLAG:コマンド結果受渡し変数 = セクハラ部位
RFLAG:コマンド結果受渡し変数２ = セクハラ快楽量

TIME_逆セクハラ:AUTOCOM_実行番号保存:0 = TIME

RETURN 1

@AUTOCOM_ABLE12(キャラ番号)
#DIM キャラ番号
;あなたが必要
SIF GETBIT(AUTOCOM_相手判別関数(キャラ番号), 0) == 0
	RETURN 0
;うふふ中ではない
SIF CFLAG:キャラ番号:うふふ
	RETURN 0
;性感帯無し時は駄目
SIF !HAS_EROGENOUS_ZONE(PLAYER)
	RETURN 0
;フィールド展開中
SIF フィールド展開:逆セクハラ == 0
	RETURN 0
;恋愛対象外
SIF 恋愛対象外チェック(キャラ番号, MASTER)
	RETURN 0
;うふふしたことがある
SIF 初体験日取得("初うふふ", キャラ番号, MASTER) == 0
	RETURN 0
;２時間に１回のみ発動
SIF TIME - TIME_逆セクハラ:キャラ番号:0 < 120
	RETURN 0
;無関心、恥じらいは性欲必要
SIF (TALENT:キャラ番号:無関心 > 0 || TALENT:キャラ番号:羞恥心 > 0) && BASE:キャラ番号:性欲 < 1500
	RETURN 0

RETURN 1

@MESSAGE_AUTOCOM12
PRINTFORM %CALLNAME:AUTOCOM_実行番号保存%は
SIF BASE:AUTOCOM_実行番号保存:性欲 > 1500
	PRINT 興奮した様子で
PRINTFORM %CALLNAME:PLAYER%の
SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE 0
		IF TEQUIP:PLAYER:下腹部前鎧あり
			PRINT 鎧の下に手を伸ばし、
		ELSEIF TEQUIP:PLAYER:スカートめくり可
			PRINT 服の裾から手を差し入れ、
			IF TEQUIP:PLAYER:下タイツあり
				PRINT タイツの上から
			ELSEIF TEQUIP:PLAYER:下半身下着あり == 0
				PRINT 直接
			ELSE
				PRINT 下着の上から
			ENDIF
		ELSEIF TEQUIP:PLAYER:ずらし可
			PRINT 服の裾をズラし、
			IF TEQUIP:PLAYER:下タイツあり
				PRINT タイツの上から
			ELSEIF TEQUIP:PLAYER:下半身下着あり == 0
				PRINT 直接
			ELSE
				PRINT 下着の上から
			ENDIF
		ELSE
			PRINT 服の上から、
		ENDIF
		IF GETBIT(TALENT:PLAYER:性別, 1)
			IF BASE:PLAYER:勃起 < 500
				PRINT まだ柔らかな
			ELSEIF BASE:PLAYER:勃起 < 1000
				PRINT 半勃ちの
			ELSE
				PRINT すっかり硬くなった
			ENDIF
			PRINTL 陰茎に手を這わせてきた…
		ELSE
			PRINTL 股間に手を這わせてきた…
		ENDIF
	CASE 1
		IF TEQUIP:PLAYER:下腹部前鎧あり
			PRINT 鎧の下に手を伸ばし、
		ELSEIF TEQUIP:PLAYER:スカートめくり可
			PRINT 服の裾から手を差し入れ、
			IF TEQUIP:PLAYER:下タイツあり
				PRINT タイツの上から
			ELSEIF TEQUIP:PLAYER:下半身下着あり == 0
				PRINT 直接
			ELSE
				PRINT 下着の上から
			ENDIF
		ELSEIF TEQUIP:PLAYER:ずらし可
			PRINT 服の裾をズラし、
			IF TEQUIP:PLAYER:下タイツあり
				PRINT タイツの上から
			ELSEIF TEQUIP:PLAYER:下半身下着あり == 0
				PRINT 直接
			ELSE
				PRINT 下着の上から
			ENDIF
		ELSE
			PRINT 服の上から、
		ENDIF
		PRINTFORML 膣口に指を這わせてきた…
	CASE 2
		IF TEQUIP:PLAYER:下腹部後鎧あり
			PRINT 鎧の下に手を伸ばし、
		ELSEIF TEQUIP:PLAYER:スカートめくり可
			PRINT 服の裾から手を差し入れ、
			IF TEQUIP:PLAYER:下タイツあり
				PRINT タイツの上から
			ELSEIF TEQUIP:PLAYER:下半身下着あり == 0
				PRINT 直接
			ELSE
				PRINT 下着の上から
			ENDIF
		ELSE
			PRINT 服の上から、
		ENDIF
		PRINTFORML 尻をゆっくりと揉んできた…
	CASE 3
		IF TEQUIP:PLAYER:上半身鎧あり
			PRINT 鎧の下に手を伸ばし、
		ELSEIF TEQUIP:PLAYER:手差し込み可
			PRINT 服の裾から手を差し入れ、
			IF TEQUIP:PLAYER:上タイツあり
				PRINT タイツの上から
			ELSEIF TEQUIP:PLAYER:上半身下着あり < 1
				PRINT 直接
			ELSE
				PRINT 下着の上から
			ENDIF
		ELSE
			PRINT 服の上から、
		ENDIF
		PRINTFORML 胸をゆっくりと揉んできた…
ENDSELECT

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:AUTOCOM_実行番号保存%の
IF TALENT:AUTOCOM_実行番号保存:経験豊富
	PRINT 手慣れた様子の
ELSEIF TALENT:AUTOCOM_実行番号保存:器用な指
	PRINT 器用な手付きの
ENDIF
PRINT セクハラで、
SELECTCASE PALAM:PLAYER:(RFLAG:コマンド結果受渡し変数)
	CASE IS < 1000
		PRINTFORML 少しムラムラしてきている…
	CASE IS < 3000
		PRINTFORML 快楽に少し声を出してしまった…
	CASE IS < 6000
		PRINTFORML 強い快感を感じている…
	CASEELSE
		PRINTFORML 今にも絶頂してしまいそうだ。
ENDSELECT
