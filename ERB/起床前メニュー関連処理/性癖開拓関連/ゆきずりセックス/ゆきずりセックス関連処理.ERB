;==================================================================================================
@ゆきずりセックス判定(ARG)
;-------------------------------------------------
;ARG番のキャラがゆきずりセックスを実行するかを返す関数
;実行する場合フラグ立てまで行う
;開始時, 実行中には1を返す
;終了時, 開始しない時, 途中終了時には0を返す
;--------------------------------------------------------------------------------------------------
#DIM 実行閾値
;--------------------------------------------------------------------------------------------------

;既に実行中の場合は継続判定に移行する。直前の行動が「覗く」ならとりあえず継続処理
IF IS_ゆきずりセックス中(ARG) || TCVAR:ARG:ゆきずりセックス_覗きフラグ
	CALL ゆきずりセックス継続判定(ARG)
	RETURN RESULT
ENDIF

;▼ゆきずりセックス判定▼

;睡眠中は素直に寝てなさい
SIF CFLAG:ARG:睡眠
	RETURN 0
;ダウン中も素直に寝てなさい
SIF BASE:ARG:体力 < 1
	RETURN 0
;性知識が無い場合は誘わない
SIF 知識素質:ARG:性知識 < 0
	RETURN 0
;性感帯無い場合も駄目
SIF !HAS_EROGENOUS_ZONE(ARG)
	RETURN 0
;同行中はやんないよ
SIF CFLAG:ARG:同行
	RETURN 0
SIF CFLAG:ARG:一緒に遊ぶ
	RETURN 0
SIF CFLAG:ARG:デート
	RETURN 0
SIF CFLAG:ARG:うふふ
	RETURN 0
SIF MODE_存在判定_いずれか側("同行系", ARG)
	RETURN 0
SIF MODE_存在判定_いずれか側("キス系", ARG)
	RETURN 0
SIF MODE_存在判定_ターゲット側("全裸散歩", ARG)
	RETURN 0
SIF MODE_存在判定_いずれか側("ハーヴィンオナホ系", ARG)
	RETURN 0

;既に誰かが同じ場所で隠密してるなら発生しない
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == ARG
		CONTINUE
	SIF IS_SAME_ROOM(ARG, LOCAL) && CFLAG:LOCAL:隠密
		RETURN 0
NEXT

;興味の場合は4h、愛好の場合は2h、中毒の場合は1h毎にそれぞれ判定を発生させる
SELECTCASE 性癖素質:ARG:ゆきずりセックス
	CASE 1
		SIF TCVAR:ARG:ゆきずりセックス_終了時刻 + 4 * 60 > NOW()
			RETURN 0
	CASE 2
		SIF TCVAR:ARG:ゆきずりセックス_終了時刻 + 2 * 60 > NOW()
			RETURN 0
	CASE 3
		SIF TCVAR:ARG:ゆきずりセックス_終了時刻 + 1 * 60 > NOW()
			RETURN 0
ENDSELECT

;現在の性欲が性癖レベルによる割合以下(1:80%,2:60%,3:40%)なら発生しない
SIF BASE:ARG:性欲 < (MAXBASE:ARG:性欲 * ( 10 - (性癖素質:ARG:ゆきずりセックス * 2) ) / 10 )
	RETURN 0

;実行閾値の設定

;閾値初期化
実行閾値 = 2

;性癖レベル分確率が上がる
実行閾値 += 性癖素質:ARG:ゆきずりセックス

;発情期中である
SIF CFLAG:ARG:発情期フラグ
	実行閾値 ++
;素質[淫乱]所持している
SIF TALENT:ARG:淫乱
	実行閾値 ++

;自ら誘うのかNPCから誘われたのかを半々で決める(0:誘い、1:誘われ)
TCVAR:ARG:ゆきずりセックス_誘い = RAND:2
IF TCVAR:ARG:ゆきずりセックス_誘い
	;●キャラが誘う側の補正
	
	;素質[性欲強い]所持している
	SIF TALENT:ARG:性欲
		実行閾値 ++
	
	;素質[経験豊富]所持している
	SIF TALENT:ARG:経験豊富
		実行閾値 ++
		
ELSE
	;○キャラが誘われる側の補正
	
	;素質[押しに弱い]所持している
	SIF TALENT:ARG:マイペース < 0
		実行閾値 ++
	
	;素質[献身的]所持している
	SIF TALENT:ARG:献身的
		実行閾値 ++
ENDIF

;乱数が実行閾値を超えた場合は発生しない
SIF RAND:10 > 実行閾値
	RETURN 0

;▼ゆきずりセックス承認▼

;同室なら物陰に行く描写を行う
IF CFLAG:ARG:同室
	SETCOLOR カラーパレット("えっちな色")
	IF TCVAR:ARG:ゆきずりセックス_誘い
		;NPCに誘われる
		PRINTFORML %CALLNAME:ARG%は観光客から性行為を求められ物陰へ移動した……
	ELSE
		;NPCを誘う
		PRINTFORML %CALLNAME:ARG%は近くにいた観光客を誘って物陰へ移動した……
	ENDIF
	RESETCOLOR
ENDIF

;ゆきずりセックス回数のカウントアップ
IF CFLAG:ARG:リゾート内ゆきずりセックス回数 < 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めてリゾート内で[ゆきずりセックス]をした</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内ゆきずりセックス回数 == 9
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[ゆきずりセックス]をした回数が１０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内ゆきずりセックス回数 == 49
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[ゆきずりセックス]をした回数が５０回に到達した</font>","うふふ")
ELSEIF CFLAG:ARG:リゾート内ゆきずりセックス回数 == 99
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[ゆきずりセックス]をした回数が１００回に到達した</font>","うふふ")
ENDIF
CFLAG:ARG:リゾート内ゆきずりセックス回数 ++

;プレイ内容の決定
CALL ゆきずりセックス内容決定(ARG)

;残り時間と絶頂値を初期化
TCVAR:ARG:ゆきずりセックス_残時間 = 90
TCVAR:ARG:ゆきずりセックス_絶頂値 = 100

;隠密状態にする
CFLAG:ARG:隠密 = 1

RETURN 1


;==================================================================================================
@ゆきずりセックス継続判定(ARG)
;-------------------------------------------------
;ARG番のキャラがゆきずりセックスを継続するか返す関数
;継続する場合には1を返し、終了する場合には0を返す
;--------------------------------------------------------------------------------------------------

;--------------------------------------------------------------------------------------------------

;対象の隠密状態を継続
CFLAG:ARG:隠密 = 1

;時間が経過しないコマンドの場合は処理をスキップ
IF TIME_PROGRESS(TFLAG:行動前時刻)
	
	;残時間の減少処理
	TCVAR:ARG:ゆきずりセックス_残時間 = MAX(0, TCVAR:ARG:ゆきずりセックス_残時間 - TIME_PROGRESS(TFLAG:行動前時刻))

	;直前の行動で絶頂値とパラメータの変動処理を行っているならそれらの処理をスキップ
	IF !TCVAR:ARG:ゆきずりセックス_覗きフラグ
		CALL ゆきずりセックス_絶頂値更新(ARG, TIME_PROGRESS(TFLAG:行動前時刻))
		CALL ゆきずりセックス_パラメータ変動処理(ARG, TIME_PROGRESS(TFLAG:行動前時刻))
	ENDIF

ENDIF

;覗きフラグを切っておく
TCVAR:ARG:ゆきずりセックス_覗きフラグ = 0

;行為中にあなたが物陰に入ってきた処理
CALL ゆきずりセックス_遭遇処理(ARG)


;継続確認
IF IS_ゆきずりセックス中(ARG)
	RETURN 1
ELSE
	
	;同じ部屋にいるなら移動先PRINT
	IF CFLAG:ARG:同室
		SETCOLOR カラーパレット("えっちな色")
		PRINTFORML 顔を赤らめた%CALLNAME:ARG%が見知らぬ男と物陰から出てきた……
		RESETCOLOR
	ENDIF
	
	;各関連変数を初期化
	TCVAR:ARG:ゆきずりセックス_体位 = 0
	TCVAR:ARG:ゆきずりセックス_残時間 = 0
	TCVAR:ARG:ゆきずりセックス_絶頂値 = 0
	
	;終了時間を記録
	TCVAR:ARG:ゆきずりセックス_終了時刻 = NOW()

	;覗きバレフラグを閉じておく
	TCVAR:ARG:覗きバレ = 0
	
	;隠密を解除
	CFLAG:ARG:隠密 = 0

	RETURN 0
ENDIF


;==================================================================================================
@ゆきずりセックス_絶頂値更新(ARG, 経過時間)
;-------------------------------------------------
;絶頂値の変動を行う。
;変動量は対象の欲望と技巧によって増加し、10分を基準として経過時間によって補正がかかる。
;(5分なら50%、20分なら200%)
;--------------------------------------------------------------------------------------------------
#DIM 経過時間
#DIM 変動値
;--------------------------------------------------------------------------------------------------
IF TCVAR:ARG:ゆきずりセックス_絶頂値 > 0
	変動値 = 20
	変動値 += ABL:ARG:欲望
	変動値 += ABL:ARG:技巧
	
	;10分をベースに、経過時間変動値を補正
	変動値 *= 経過時間
	変動値 /= 10
	
	TCVAR:ARG:ゆきずりセックス_絶頂値 = MAX(0, TCVAR:ARG:ゆきずりセックス_絶頂値 - 変動値)
	
ELSE
	TCVAR:ARG:ゆきずりセックス_絶頂値 = 100
ENDIF


;==================================================================================================
@ゆきずりセックス_パラメータ変動処理(ARG, 経過時間=1)
;-------------------------------------------------
;各種パラメータの増加、性欲の減少などの変動を行う
;変動量は
;--------------------------------------------------------------------------------------------------
#DIM 絶頂閾値
#DIM 経過時間
#DIM ループ変数
#DIM 絶頂数
#DIM 性欲減少量
;--------------------------------------------------------------------------------------------------

;絶頂数を初期化
絶頂数 = 0
絶頂閾値 = 20

;絶頂閾値に対応感度を補正
SELECTCASE TCVAR:ARG:ゆきずりセックス_体位
	CASE 1 ;V
		絶頂閾値 -= TALENT:ARG:Ｖ感度
	CASE 2 ;A
		絶頂閾値 -= TALENT:ARG:Ａ感度
ENDSELECT

;対象キャラの絶頂判定
; …経過時間を閾値で割った数だけ処理を繰り返す。感度がデフォなら10分で1回、20分で2回。
FOR ループ変数, 0, 経過時間/絶頂閾値
	CALL ゆきずりセックス_絶頂加算(ARG)
	絶頂数 ++
NEXT
;上の端数は確率で絶頂判定。感度がデフォなら15分で1回の絶頂と50%の確率でさらに絶頂
IF 経過時間%絶頂閾値 > RAND:絶頂閾値
	CALL ゆきずりセックス_絶頂加算(ARG)
	絶頂数 ++
ENDIF

;絶頂数が1以上あるならZP取得処理
IF 絶頂数
	CALL ZP_取得処理(ARG, 絶頂数, 0)
	
	;絶頂出来たら性欲を20%程発散する
	性欲減少量 = MAXBASE:ARG:性欲 / 5
	BASE:ARG:性欲 = MAX(BASE:ARG:性欲 - 性欲減少量, 0)
ENDIF

;==================================================================================================
@ゆきずりセックス_絶頂加算(ARG)
;-------------------------------------------------
;絶頂判定を経て絶頂した時の処理
;--------------------------------------------------------------------------------------------------
;部位によって絶頂箇所を加算
SELECTCASE TCVAR:ARG:ゆきずりセックス_体位
	CASE 1 ;V
		EXP:ARG:Ｖ絶頂経験 += 1
		CALL TAKE_SPERM(ARG, -2, 25)
	CASE 2 ;A
		EXP:ARG:Ａ絶頂経験 += 1
		CALL TAKE_SPERM_A(ARG, -2, 25)
ENDSELECT
EXP:ARG:絶頂経験 += 1


;==================================================================================================
@ゆきずりセックス_遭遇処理(ARG)
;-------------------------------------------------
;「物陰に隠れる」で行為中のキャラに遭遇してしまったときの処理
;--------------------------------------------------------------------------------------------------
#DIM 経過時間
;--------------------------------------------------------------------------------------------------

IF CFLAG:PLAYER:隠密 && IS_SAME_ROOM(ARG)

	DRAWLINE
	
	;ここで行為中のキャラとばったり遭遇
	
	;いずれ「そのまま見られながら行為を続行」とかそういうのを付けたい
	
	PRINTFORML だれか先客がいるようだ……。
	PRINTFORMW %CALLNAME:PLAYER%は先客の邪魔にならないよう静かにその場を離れた。
	
	DRAWLINE
	
	
	;あなたと同行者の隠密を解除する
	CFLAG:PLAYER:隠密 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF CFLAG:(TARGET:LOCAL):同行 || CFLAG:(TARGET:LOCAL):うふふ
			CFLAG:(TARGET:LOCAL):隠密 = 0
	NEXT
	
ENDIF

;==================================================================================================
@ゆきずりセックス_覗く(ARG)
;-------------------------------------------------
;「COMF404_覗く」からゆきずりセックスを覗き見る処理
;--------------------------------------------------------------------------------------------------
#DIM 経過時間
;--------------------------------------------------------------------------------------------------

;覗きフラグを立てる
TCVAR:ARG:ゆきずりセックス_覗きフラグ = 1

;経過時間を加算
TIME += 12

;絶頂値の変動 15分は決め打ち(覗き見3分+このイベント経過時間の12分)
CALL ゆきずりセックス_絶頂値更新(ARG, 15)

;共通文章
PRINTFORM 物陰を覗き見ると、
SELECTCASE TCVAR:ARG:ゆきずりセックス_体位
	CASE 1 ;Ｖ性交
		PRINTFORML 見知らぬ男に膣穴を貫かれ喘がされている%CALLNAME:ARG%の姿を目撃した。
	CASE 2 ;Ａ性交
		PRINTFORML 見知らぬ男の男根を尻穴で咥え込んでいる%CALLNAME:ARG%の姿を目撃した。
	CASEELSE ;その他
		PRINTFORML %CALLNAME:ARG%が愛おしそうに見知らぬ男のペニスに奉仕している姿を目撃した。
ENDSELECT
PRINTFORMW

;お相手の絶頂とかち合ったかで分岐
IF TCVAR:ARG:ゆきずりセックス_絶頂値 > 0
	;通常時
	IF !TCVAR:ARG:覗きバレ
		;まだバレてない
		
		;覗きバレ判定を行う
		IF TCVAR:ARG:使い魔覗き中
			;使い魔覗き中は相手の魔法知識Lvによってバレる可能性がある
			IF RAND:10 < 知識素質:ARG:魔法知識*2
				;覗きバレフラグを立てる
				TCVAR:ARG:覗きバレ = 1
				
			ENDIF
		ENDIF
		;どちらにせよ一定の確率でバレちゃう(大体10%ぐらい)
		SIF RAND:10 < 1
			TCVAR:ARG:覗きバレ = 1
		
		IF TCVAR:ARG:覗きバレ
			PRINTFORML その光景を堪能していると、%CALLNAME:ARG%がちらりとこちらに目を向けた。
			PRINTFORML 目線は間違いなくこちらを捕らえている。これは偶然ではない、バレている……!!
			SIF TCVAR:ARG:使い魔覗き中 && 知識素質:ARG:魔法知識 >= 1
				PRINTFORML 魔法知識に長けた%CALLNAME:ARG%に対して使い魔を差し向けたのは悪手だったかもしれない……
			PRINTFORML 
			SELECTCASE RAND:3
				CASE 0
					PRINTFORM しかし、%CALLNAME:ARG%はこちらに不敵な笑みを向けると、
				CASE 1
					PRINTFORM しかし、%CALLNAME:ARG%はこちらにウインクすると、
				CASE 2
					PRINTFORM しかし、%CALLNAME:ARG%は悪戯っぽくシーッと口に指をあてると、
			ENDSELECT
			PRINTFORML 先ほどよりも激しく行為に勤しみだした。
			PRINTFORML それはこちらへの当てつけなのか、それとも「見られている」という事に興奮しているのかは分からないが……
			PRINTFORMW どちらにせよ、より淫靡な姿を見せる%CALLNAME:ARG%の姿にこちらも目を離せずにいた……
		ELSE
			;ばれてない
			PRINTFORML 目の前の雄と雌は覗かれている事に気づかず、互いに性的な快楽を貪っている……
		ENDIF
	ELSE
		;すでにバレている
		PRINTFORML %CALLNAME:ARG%はたまにちらっとこちらに目線を向け、%CALLNAME:PLAYER%がまだいる事を確認すると
		PRINTFORML わざと聞こえるように喘ぎ声を上げたり、激しさを増したりと、こちらの情欲を煽ってくる。
		PRINTFORMW こちらを愉しませてくれる健気な姿にほくそ笑みながら、%CALLNAME:PLAYER%は%CALLNAME:ARG%の情事をゆっくり堪能した……
	ENDIF
	
ELSE
	;お相手側の絶頂とかち合っちゃった
	
	PRINTFORM 丁度男が絶頂を迎えたタイミングだったらしく、
	SELECTCASE TCVAR:ARG:ゆきずりセックス_体位
		CASE 1 ;Ｖ性交
			PRINTFORML %CALLNAME:ARG%は快楽に身を震わせながら放たれた白濁を膣内で受けとめていた。
			PRINTFORML 長い射精を終えた男がペニスを抜くと、収まり切れなかった精液が溢れ、ボトボトと床を汚す。
			IF TCVAR:ARG:覗きバレ
				PRINTFORML %CALLNAME:ARG%は女陰を指で広げ、しっかり種付けされたことを男に…あるいはその奥で覗いている%CALLNAME:PLAYER%に見せつけた。
			ENDIF
		CASE 2 ;Ａ性交
			PRINTFORML %CALLNAME:ARG%は快楽に身を震わせながら放たれた白濁を尻穴で受けとめていた。
			PRINTFORML 長い射精を終えた男がペニスを抜くと、収まり切れなかった精液が溢れ、ボトボトと床を汚す。
			IF TCVAR:ARG:覗きバレ
				PRINTFORML %CALLNAME:ARG%は尻を両手で広げ、精液が溢れている尻穴を男に…あるいはその奥で覗いている%CALLNAME:PLAYER%に見せつけた。
			ENDIF
		CASEELSE ;奉仕
			PRINTFORML 男の精液が奉仕している%CALLNAME:ARG%に降りかかった。
			IF TCVAR:ARG:覗きバレ
				PRINTFORML %CALLNAME:ARG%は自身に向けて大量に出された精液を男に…あるいはその奥で覗いている%CALLNAME:PLAYER%に見せつけた。
			ENDIF
	ENDSELECT
	PRINTFORML 
	PRINTFORML ………………………
	PRINTFORMW
	 
	IF TCVAR:ARG:ゆきずりセックス_残時間
		;延長戦
		PRINTFORML 行為後の淫靡な%CALLNAME:ARG%の姿に当てられた男のペニスは再びムクムクと硬さを帯び、語ることなく%CALLNAME:ARG%に処理を催促する。
		PRINTFORMW 満足に至っていない%CALLNAME:ARG%も突き出されたペニスをうっとりとした表情を見せ、ゆきずりの性行為を再開しはじめた……
		
	ELSE
		;終わり
		PRINTFORML 互いに満足した二人はしばらく余韻を浸ったのち、そそくさと服の乱れを直し始めた。
		PRINTFORMW このままでは鉢合わせてしまう……%CALLNAME:PLAYER%は急いでその場を離れた。
	ENDIF
ENDIF

;パラメータ更新
CALL ゆきずりセックス_パラメータ変動処理(ARG, 15)


;==================================================================================================
@ゆきずりセックス途中終了(ARG)
;-------------------------------------------------
;ARG番のキャラがゆきずりセックスを何らかの理由で途中終了した際に動かす関数
;フラグ周りを初期化する
;--------------------------------------------------------------------------------------------------
TCVAR:ARG:ゆきずりセックス_残時間 = 0
TCVAR:ARG:ゆきずりセックス_絶頂値 = 100
RETURN 0


;==================================================================================================
@IS_ゆきずりセックス中(ARG)
#FUNCTION
;-------------------------------------------------
;ARG番のキャラがゆきずりセックス中であるかを判定する関数
;残時間が0以下で、相手が絶頂したタイミングで終了(0)を返す。
;--------------------------------------------------------------------------------------------------
#DIM 行為中判定 = 1
;--------------------------------------------------------------------------------------------------
IF !TCVAR:ARG:ゆきずりセックス_残時間  && !TCVAR:ARG:ゆきずりセックス_絶頂値
	RETURNF 0
ELSE
	RETURNF 1
ENDIF


;==================================================================================================
@ゆきずりセックス内容決定(ARG)
;-------------------------------------------------
;各感覚と技巧を基に行為の内容を決定する。(Ｖ性交、Ａ性交、Ｂ性交、その他)
;基本的にどれも最低でも1の重みを持ち、オプションの設定次第では初体験を捧げる可能性を持つ
;--------------------------------------------------------------------------------------------------
#DIMS プレイ内容
#DIM Ｖ性交重み
#DIM Ａ性交重み
#DIM 奉仕重み
#DIM 重み合計
;--------------------------------------------------------------------------------------------------

;プレイ内容の重み決定
;Ｖ性交 … 「Ｖなし」ではなく、「純潔設定あり」で「処女」以外の場合のみ実行
SIF TALENT:ARG:Ｖ感度 > -2 && !(OPTION変数:夜這い処女・童貞 && TALENT:ARG:処女)
	;Ｖ感覚に影響する
	Ｖ性交重み += MAX(1, ABL:ARG:Ｖ感覚)

;Ａ性交 … 「Ａなし」ではなく、「純潔設定あり」で「Ａ処女」以外の場合のみ実行
SIF TALENT:ARG:Ａ感度 > -2 && !(OPTION変数:夜這い処女・童貞 && TALENT:ARG:Ａ処女)
	;Ａ感覚に影響する
	Ａ性交重み += MAX(1, ABL:ARG:Ａ感覚)

;奉仕 … NPCから誘われた時に重みを得る。
SIF TCVAR:ARG:ゆきずりセックス_誘い
	;奉仕精神と素質[献身的]に影響する。
	奉仕重み += MAX(1, ABL:ARG:奉仕精神) + (TALENT:ARG:献身的*2)

;重み合計を算出
重み合計 = Ｖ性交重み+Ａ性交重み+奉仕重み

;プレイ内容の決定
IF 重み合計
	;重み合計を基に乱数でプレイ内容を決定
	SELECTCASE RAND:重み合計
		;=========================================================
		; ●奉仕性交
		;=========================================================
		;基本的にはキャラの性欲は満たされず絶頂しない(NPCは満足する)
		CASE IS < 奉仕重み
			;挿入以外の性交…フェラチオ、手コキ
			
			;どのプレイでも奉仕快楽経験を積む
			EXP:ARG:奉仕快楽経験 ++
			
			SELECTCASE RAND:3
				CASE 0
					プレイ内容 = フェラチオ
					EXP:ARG:口淫経験 ++
					
				CASE 1
					プレイ内容 = 手コキ
					EXP:ARG:手淫経験 ++
			ENDSELECT
			
			
		;=========================================================
		; ●Ｖ性交
		;=========================================================
		CASE 奉仕重み TO 奉仕重み+Ｖ性交重み-1
			;Ｖ性交
			SELECTCASE RAND:12
				CASE 0
					プレイ内容 = 正常位
				CASE 1
					プレイ内容 = 後背位
				CASE 2
					プレイ内容 = 寝バック
				CASE 3
					プレイ内容 = 対面座位
				CASE 4
					プレイ内容 = 背面座位
				CASE 5
					プレイ内容 = 騎乗位
				CASE 6
					プレイ内容 = 側位
				CASE 7
					プレイ内容 = 屈曲位
				CASE 8
					プレイ内容 = 駅弁
				CASE 9
					プレイ内容 = 背面駅弁
				CASE 10
					プレイ内容 = 対面立位
				CASE 11 
					プレイ内容 = 立ちバック
			ENDSELECT
			
			;部位を設定
			TCVAR:ARG:ゆきずりセックス_体位 = 1
			
			;初めて判定
			IF EXP:ARG:Ｖ性交経験 == 0
				CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>出会ったばかりの相手に自ら処女を捧げてしまった</font><img src='えっちハート'>","うふふ")
			ENDIF
			TALENT:ARG:処女 = 0
			
			;経験回数
			EXP:ARG:Ｖ経験 ++
			EXP:ARG:Ｖ性交経験 ++
			
			
		;=========================================================
		; ●Ａ性交
		;=========================================================
		CASE 奉仕重み+Ｖ性交重み TO 奉仕重み+Ｖ性交重み+Ａ性交重み-1
			;Ａ性交
			SELECTCASE RAND:12
				CASE 0
					プレイ内容 = Ａ正常位
				CASE 1
					プレイ内容 = Ａ後背位
				CASE 2
					プレイ内容 = Ａ寝バック
				CASE 3
					プレイ内容 = Ａ対面座位
				CASE 4
					プレイ内容 = Ａ背面座位
				CASE 5
					プレイ内容 = Ａ騎乗位
				CASE 6
					プレイ内容 = Ａ側位
				CASE 7
					プレイ内容 = Ａ屈曲位
				CASE 8
					プレイ内容 = Ａ駅弁
				CASE 9
					プレイ内容 = Ａ背面駅弁
				CASE 10
					プレイ内容 = Ａ対面立位
				CASE 11 
					プレイ内容 = Ａ立ちバック
			ENDSELECT
			
			;体位を設定
			TCVAR:ARG:ゆきずりセックス_体位 = 2
			
			;初めて判定
			IF EXP:ARG:Ａ性交経験 == 0
				CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>出会ったばかりの相手に自ら後ろの初めてを捧げてしまった</font><img src='えっちハート'>","うふふ")
			ENDIF
			TALENT:ARG:Ａ処女 = 0
			
			;経験回数
			EXP:ARG:Ａ経験 ++
			EXP:ARG:Ａ性交経験 ++
			
	ENDSELECT
ELSE
	;全ての重みが0の時は「手コキ」にする。
	プレイ内容 = 手コキ
	EXP:ARG:手淫経験 ++
	
ENDIF

;プレイ内容を保管
CSTR:ARG:ゆきずりセックス_プレイ内容 = %プレイ内容%


