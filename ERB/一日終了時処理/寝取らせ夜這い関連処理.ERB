@寝取らせ夜這い判定()
#DIM ピックアップ配列, 5000
#DIM 島内配列, 5000
#DIM 主導キャラ配列, 5000
#DIM 主導キャラ
#DIM 候補カップリング配列, 5000, 2
#DIM 候補数
#DIM RAND値
#DIM ピックアップ数
#DIM カップリングキャラ１
#DIM カップリングキャラ２
#DIMS カップリング関係性
#DIM DT_ID
#DIM 選択結果
#DIM 対象キャラ
#DIM L_CNT

SIF OPTION変数:夜這い全部禁止
	RETURN 0

;気になってどういうものか見に来たあなたに向けて簡単な仕様説明
;
;・性癖開拓システムでリゾート客(仮にＡとする)にエロ本を押し付けてＡの脳を破壊し、性癖素質[寝取られ(興味)]を与えます。
;・縁結びシステムを使って、Ａと他のキャラ(仮にＢとする)をくっつけます。関係性は恋慕でもセフレでもＯＫ。
;・あなたとＡの「信頼度」を、一定以上にします。その際あなたが「遊び人」として極まってきているなら、このボーダーはちょっとだけ下がります。Ａが「従業員」であるなら、このボーダーは劇的に下がります。
;上記のみっつの条件を、すべて満たせば準備完了！(別にＡもＢも両方寝取られ性癖持ってても大丈夫だよ！)
;
;「ＡとＢがどちらも島にいる」状態で、あなたが「自室」で「就寝」する際、「他に同衾するキャラがいなければ(一人寝であるなら)」確率でイベントが発生。
;一日の終わりに、度し難い性癖に脳を破壊されたＡがあなたの部屋を訪ねてきて、「Ｂを抱いてくれ」とお願いされます。性癖名は「寝取られ」だけど、Ａがやる実際のプレイは「寝取らせ」が正しいわけですな。
;いやぁ、頼まれちゃったからなぁ～！しょうがないなぁ～～～！！ほな、いただきま～～～っす！！
;
;毎日来られても困るし特別感もないので、発生確率は低め。「こんなもんかな～」で適当に決めてるので、低すぎる！と思ったら本スレとかDiscordで教えてください。調整します。
;実際問題、発生に一人寝を要求すること自体がプレイに対する一つの縛りになっちゃってるから、もうちょい高くてもいいかもな～とは書いた本人も思ってます。
;抽出処理とタイミングの関係で、条件を満たしたカップルが増えるほどに発生確率は平均化されちゃうし、狙った組み合わせも出づらくなるし。
;あなたの「遊び人」が５とかだと確率がちょっと上がる。理由は確率処理んとこに書いた。
;
;さいごに
;エロ本ばら撒きには要注意。みんながみんな性癖持ちだと、「男あなたにグラン(♂)を差し出してくるジータ(寝取られ性癖持ち)」とかそういう事故が起きます。
;まぁ、意図して除外処理しなかったんですけども。だ、だってそういう需要もあるかもだし……。

;補正値の減衰
;・毎日減衰させたいので各種判定前に実行
FOR 対象キャラ, 1, CHARANUM
	; 1日ごとに補正が10％づつ増加(上限0％)
	SIF 寝取らせ_発生率補正:対象キャラ:0
		寝取らせ_発生率補正:対象キャラ:0 = MIN(寝取らせ_発生率補正:対象キャラ:0 + 10, 0)
NEXT

;あなたが自室に居ない場合はダメ
SIF CFLAG:PLAYER:現在位置 != CFLAG:PLAYER:自室位置
	RETURN 0
SIF CFLAG:PLAYER:現在マップ種別 != 0
	RETURN 0

;同衾キャラが居る場合はダメ
SIF TARGET:1 > 0
	RETURN 0
SIF FLAG:挿入睡眠相手記録 > 0
	RETURN 0

VARSET ピックアップ配列
;恋慕の組み合わせピックアップ処理
;まず恋慕orセフレで、組み合わせにあなたが入っていないカップリングを割り出す
ピックアップ数 = DT_SELECT("関係性データベース", @"対象キャラ１ <> {CFLAG:MASTER:人物番号} And 対象キャラ２ <> {CFLAG:MASTER:人物番号} And (関係性種別 = '恋慕' Or 関係性種別 = 'セフレ')", ,ピックアップ配列)
;この命令でピックアップ数には「該当するカップリングの数」が
;ピックアップ配列には「該当するデータベースID」が順番に入っている

;例外フォロー忘れずに
SIF ピックアップ数 < 1
	RETURN 0

;組み合わせが島内にいるかどうかを見る
VARSET 島内配列
VARSET 主導キャラ配列
候補数 = 0
FOR L_CNT, 0, ピックアップ数
	DT_ID = ピックアップ配列:L_CNT
	カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
	カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))

	;どちらかが滞在していない場合はダメ
	SIF CFLAG:カップリングキャラ１:滞在期間 < 1
		CONTINUE
	SIF CFLAG:カップリングキャラ２:滞在期間 < 1
		CONTINUE

	;どちらかが寝取られ素質を持っているならOK
	SIF 性癖素質:カップリングキャラ１:寝取られ <= 0 && 性癖素質:カップリングキャラ２:寝取られ <= 0
		CONTINUE

	;- 主導/献上品が関係ない判定
	SIF !寝取らせ夜這い判定_キャラ状態判定(カップリングキャラ１) || !寝取らせ夜這い判定_キャラ状態判定(カップリングキャラ２)
		CONTINUE
	
	SIF !寝取らせ夜這い判定_身体条件判定(カップリングキャラ１) || !寝取らせ夜這い判定_身体条件判定(カップリングキャラ２)
		CONTINUE

	;1カップリングごとに主導キャラを入れ替えて2回判定する
	FOR 主導キャラ, 1, 2 + 1
		IF 主導キャラ == 1
			寝取らせ_主導キャラ   = カップリングキャラ１
			寝取らせ_献上品キャラ = カップリングキャラ２
		ELSE
			寝取らせ_主導キャラ   = カップリングキャラ２
			寝取らせ_献上品キャラ = カップリングキャラ１
		ENDIF
		;主導側が寝取られ素質を持っていない場合はダメ
		SIF 性癖素質:寝取らせ_主導キャラ:寝取られ <= 0
			CONTINUE
		;献上品側があなたと同性で同性愛を持っていない場合はダメ
		SIF 同性愛性癖判定(寝取らせ_献上品キャラ, PLAYER) && 性癖素質:寝取らせ_献上品キャラ:同性愛 < 1
			CONTINUE
		;主導側の信頼度が足りない場合はダメ
		SIF !寝取らせ夜這い判定_信頼度判定(寝取らせ_主導キャラ)
			CONTINUE
		;主導側の確率判定
		SIF !寝取らせ夜這い判定_確率判定(寝取らせ_主導キャラ)
			CONTINUE
		;候補に登録
		候補カップリング配列:候補数:0 = DT_ID
		候補カップリング配列:候補数:1 = 主導キャラ
		候補数 ++
	NEXT
NEXT

;例外フォロー忘れずに
SIF 候補数 < 1
	RETURN 0

;イベント本文
DRAWLINE
PRINTL
PRINTFORMW ──深夜、%CALLNAME:PLAYER%の部屋のドアがノックされた。
PRINTL
PRINTFORML こんな時間に誰だろう……。
PRINTFORM 予期せぬ来客に%CALLNAME:PLAYER%がドアを開けると、
IF 候補数 < 寝取らせ_対象選択化しきい値
	; 候補が少ないときは1組だけ選出
	RAND値 = RAND:候補数
	DT_ID      = 候補カップリング配列:RAND値:0
	主導キャラ = 候補カップリング配列:RAND値:1

	;該当のIDから対象キャラを持ってきてキャラ番号を取る
	カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
	カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))
	カップリング関係性  '= DT_CELL_GETS("関係性データベース", DT_ID, "関係性種別", 1)

	;寝取らせの主導側と差し出される側を決める
	IF 主導キャラ == 1
		寝取らせ_主導キャラ   = カップリングキャラ１
		寝取らせ_献上品キャラ = カップリングキャラ２
	ELSE
		寝取らせ_主導キャラ   = カップリングキャラ２
		寝取らせ_献上品キャラ = カップリングキャラ１
	ENDIF
	PRINTFORML そこには%CALLNAME:寝取らせ_主導キャラ%と%CALLNAME:寝取らせ_献上品キャラ%の姿があった。
ELSE
	; 候補が多いときは複数選出してそこから選択
	PRINTFORML そこには…
	; シャッフル
	FOR L_CNT, 0, 候補数
		RAND値 = RAND:候補数
		SWAP 候補カップリング配列:L_CNT:0, 候補カップリング配列:RAND値:0
		SWAP 候補カップリング配列:L_CNT:1, 候補カップリング配列:RAND値:1
	NEXT
	; シャッフルしたリストから表示人数分だけ表示
	FOR L_CNT, 0, MIN(候補数, 寝取らせ_選択表示人数)
		DT_ID      = 候補カップリング配列:L_CNT:0
		主導キャラ = 候補カップリング配列:L_CNT:1

		;該当のIDから対象キャラを持ってきてキャラ番号を取る
		カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
		カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))
		カップリング関係性  '= DT_CELL_GETS("関係性データベース", DT_ID, "関係性種別", 1)
		IF 主導キャラ == 1
			寝取らせ_主導キャラ   = カップリングキャラ１
			寝取らせ_献上品キャラ = カップリングキャラ２
		ELSE
			寝取らせ_主導キャラ   = カップリングキャラ２
			寝取らせ_献上品キャラ = カップリングキャラ１
		ENDIF
		PRINTS "　"
		PRINTBUTTON @"[{L_CNT, 2}]%CALLNAME:寝取らせ_主導キャラ%と%CALLNAME:寝取らせ_献上品キャラ%の姿があった", L_CNT
		PRINTL
	NEXT
	PRINTS "　"
	PRINTBUTTON "[-1]誰も居なかった", -1
	PRINTL
	BINPUT
	選択結果 = RESULT
	IF 選択結果 == -1
		; キャンセル
		PRINTL
		PRINTFORML …ドアを開けた先には誰も居なかった。どうやら空耳だったようだ。
		PRINTFORMW %CALLNAME:PLAYER%はドアを閉めて部屋に戻った。
		PRINTL
		RETURN 1
	ENDIF
	; 選択したカップリングの情報を取得
	DT_ID      = 候補カップリング配列:選択結果:0
	主導キャラ = 候補カップリング配列:選択結果:1
	カップリングキャラ１ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ１", 1))
	カップリングキャラ２ = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", DT_ID, "対象キャラ２", 1))
	カップリング関係性  '= DT_CELL_GETS("関係性データベース", DT_ID, "関係性種別", 1)
	IF 主導キャラ == 1
		寝取らせ_主導キャラ   = カップリングキャラ１
		寝取らせ_献上品キャラ = カップリングキャラ２
	ELSE
		寝取らせ_主導キャラ   = カップリングキャラ２
		寝取らせ_献上品キャラ = カップリングキャラ１
	ENDIF
	PRINTL
ENDIF
PRINTL 

RESULT = 0
TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_訪問_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
SIF RESULT > 0
	GOTO 訪問ラベル

PRINTFORM 欲望の宿った%CALLNAME:寝取らせ_主導キャラ%の瞳は廊下の夜闇に爛々と輝くようで、
;訪問キャラふたりの間では事前に話がついているので、これからの事に対する反応
IF EXP:寝取らせ_献上品キャラ:貸出経験 >= 3
	PRINTFORMW 連れだった%CALLNAME:寝取らせ_献上品キャラ%も頬を染め、これから味わうだろう不貞快楽への期待に胸を高鳴らせている様子だ。
ELSEIF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
	PRINTFORMW それとは対照的に、連れだった%CALLNAME:寝取らせ_献上品キャラ%は不安と恐怖でいっぱいの様子だ。
ELSEIF TALENT:寝取らせ_献上品キャラ:羞恥心 == 1 || TALENT:寝取らせ_献上品キャラ:貞操 == 1
	PRINTFORMW 連れだった%CALLNAME:寝取らせ_献上品キャラ%は恥じらうように俯いている。
ELSEIF TALENT:寝取らせ_献上品キャラ:経験豊富
	PRINTFORMW 連れだった%CALLNAME:寝取らせ_献上品キャラ%もニヤニヤと艶の乗った笑みを浮かべている。
ELSE
	PRINTFORMW その反面、連れだった%CALLNAME:寝取らせ_献上品キャラ%はうつむき加減で所在なさげに佇んでいる。
ENDIF
PRINTL
PRINTFORML 要件が何であるにせよ、こんなところで話すのもなんだ。
PRINTFORMW %CALLNAME:PLAYER%は二人を部屋の中へと招き入れた……
PRINTL
PRINTFORML ────────
PRINTW
PRINTFORM ……興奮した%CALLNAME:寝取らせ_主導キャラ%の要件とはつまり、%CALLNAME:PLAYER%に
IF EXP:寝取らせ_主導キャラ:寝取られ経験 >= 5
	PRINTFORM 、いつものように
ELSEIF EXP:寝取らせ_主導キャラ:寝取られ経験 >= 2
	PRINTFORM 、また前回のように
ENDIF
PRINTFORMW %CALLNAME:寝取らせ_献上品キャラ%%TEXTR("のことを抱いて/とセックスして")%ほしいということらしかった。
PRINTL
PRINTFORML こうして%CALLNAME:PLAYER%の部屋へと来る前に、二人のあいだで話は済ませてあるらしい。
;寝取られ中毒彼氏クンの手によって、セクシーランジェリーで飾りたてられているかもしれない？
IF 知識素質:寝取らせ_主導キャラ:性知識 > 1 && 性癖素質:寝取らせ_主導キャラ:寝取られ == 3
		SIF CSTR:寝取らせ_献上品キャラ:コスプレ前の服 == "" && CSTR:寝取らせ_献上品キャラ:表示グラフィックカテゴリ != ""
			CSTR:寝取らせ_献上品キャラ:コスプレ前の服 '= CSTR:寝取らせ_献上品キャラ:表示グラフィックカテゴリ
	LOCALS '= CSTR:寝取らせ_献上品キャラ:脱ぐ前の服
	CALL CLOTHES_CHANGE(寝取らせ_献上品キャラ, "セクシーランジェリー")
	CSTR:寝取らせ_献上品キャラ:脱ぐ前の服 '= LOCALS
	PRINTFORMW %CALLNAME:PLAYER%の興奮をより煽るためか、%CALLNAME:寝取らせ_主導キャラ%は%CALLNAME:寝取らせ_献上品キャラ%にわざわざ情欲を煽る下着まで着させて来ている……。
ENDIF
PRINTL
IF EXP:寝取らせ_献上品キャラ:貸出経験 >= 1
	IF EXP:寝取らせ_献上品キャラ:貸出経験 <= 2
		;「今回が三回目」まで
		PRINTFORM %CALLNAME:PLAYER%が「またよろしくね」と囁くと、
		IF TALENT:寝取らせ_献上品キャラ:経験豊富
			PRINTFORML %CALLNAME:寝取らせ_献上品キャラ%は妖艶に微笑み返してきた。
		ELSE
			PRINTFORML 前回のことを思い出したのか、頬を染めた%CALLNAME:寝取らせ_献上品キャラ%は俯き加減に頷いた。
		ENDIF
	ELSEIF ABL:寝取らせ_献上品キャラ:マゾ性感 >= 2
		PRINTFORML %CALLNAME:PLAYER%が「何か言うことは？」と%TEXTR("告げる/高圧的に告げる/告げ、乳房を乱暴に掴む")%と、自らの立場を%TEXTR("わきまえた/思い出した")%%CALLNAME:寝取らせ_献上品キャラ%はぞくぞくと身を震わせ、
		IF ABL:寝取らせ_献上品キャラ:マゾ性感 >= 3
			IF 知識素質:寝取らせ_献上品キャラ:高貴
				;全裸土下座はこのあとの服の扱いが大変なのでパス。いや「全裸土下座フラグが1なら全裸スタート」とかできなくはないだろうけども……
				;手の甲へのキスは「敬愛」「忠誠」、足の甲へのキスは「服従」の意味を持つ。高貴だから詳しいんだ！
				PRINTFORM %TEXTR("その場に膝をつき美しい土下座を披露して/その場で三つ指をつき額を床に擦り付けて/その場に跪きあなたの手の甲に口づけて/その場に跪きあなたの足の甲に口づけて")%、
			ELSE
				;「深々と頭を下げる」は微妙に状況にそぐわない気がしたので、とりあえず男だけで
				;土下座は……高貴ゆえに美しい土下座ができる方々と比べるとなんかこう、格が落ちるというか、下位互換的な被りかたっていうか……
				IF TEQUIP:スカートめくり可 == 1
					PRINTFORM スカートをたくし上げながら、
				ELSEIF TALENT:寝取らせ_献上品キャラ:性別 == 1
					PRINTFORM %CALLNAME:PLAYER%の手を取って自らの胸を揉ませると、
				ELSE
					PRINTFORM 深々と頭を下げて、
				ENDIF
			ENDIF
			IF RISKY_DAY(寝取らせ_献上品キャラ) >= 1 && GETBIT(TALENT:PLAYER:性別, 1)
				PRINTFORM 「今日は危険日なので、あなたの%TEXTR("子種を恵んでください/子を産ませてください/赤ちゃんを孕ませてください/精液をください")%
				CALL PRINT_STR("……_H_」")
				PRINTFORMW と種乞いした。
			ELSE
				;できるだけ男女兼用できる文面に
				SELECTCASE RAND:4
				CASE 0
					PRINTFORM 「また今夜も、%CALLNAME:寝取らせ_主導キャラ%とじゃできない『本当のセックス』を教えてください
				CASEELSE
					PRINTFORM 「また今夜も%TEXTR("躾けてください/たくさん可愛がってください/、いっぱい『遊んで』ください/たくさん気持ちよくしてください")%
				ENDSELECT
				CALL PRINT_STR("……_H_」")
				PRINTFORMW と懇願した。
			ENDIF
		ELSE
			;「深々と頭を下げる」は微妙に状況にそぐわない気がしたので、とりあえず男だけで
			;土下座は……高貴ゆえに美しい土下座ができる方々と比べるとなんかこう、格が落ちるというか、下位互換的な被りかたっていうか……
			IF TEQUIP:スカートめくり可 == 1
				PRINTFORM スカートをたくし上げながら、
			ELSEIF TALENT:寝取らせ_献上品キャラ:性別 == 1
				PRINTFORM %CALLNAME:PLAYER%の手を取って自らの胸を揉ませると、
			ELSE
				PRINTFORM 深々と頭を下げて、
			ENDIF
			IF TALENT:寝取らせ_献上品キャラ:プライド == 1
				PRINTFORM 屈辱に震える声で
			ELSEIF TALENT:寝取らせ_献上品キャラ:羞恥心 == 1 || TALENT:寝取らせ_献上品キャラ:貞操 == 1 || TALENT:寝取らせ_献上品キャラ:陽気／陰気 == -1
				PRINTFORM 躊躇いがちに震える声で
			ENDIF
			;マゾ性感3の時よりも語彙と丁寧さを落とした感じ
			;できるだけ男女兼用できる文面に
			PRINTFORM 「また今夜も%TEXTR("躾けてください/たくさん可愛がってください/たくさん気持ちよくしてください")%
			CALL PRINT_STR("……_H_」")
			PRINTFORMW と懇願した。
		ENDIF
	ELSE
	;マゾ性感１以下かつ４回目以降。慣れで雑な扱いされてもよくなってしまう態度の変化
		PRINTFORM %CALLNAME:PLAYER%が「またよろしくね」と%TEXTR("挨拶がわりに胸を揉む/挨拶がわりに尻を揉む/挨拶がわりに乳首をつねる/挨拶がわりに乳首をひっぱる/肩を抱く/腰を抱き寄せる/肩に手をまわして胸をもむ/挨拶がわりに見せつけるように唇を奪う")%と、
		PRINTFORMW %CALLNAME:寝取らせ_献上品キャラ%は甘く吐息を漏らし、期待通りのその姿に%CALLNAME:寝取らせ_主導キャラ%はごくりと喉を鳴らした。
	ENDIF
ELSE
	;はじめて「貸し出される」場合
	PRINTFORM %CALLNAME:PLAYER%が「本当にいいのか」と尋ねると
	IF TALENT:寝取らせ_献上品キャラ:処女 || TALENT:寝取らせ_献上品キャラ:陽気／陰気 == -1 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
		PRINTFORMW 、%CALLNAME:寝取らせ_献上品キャラ%は声を震わせながらも、「%CALLNAME:寝取らせ_主導キャラ%のためだから」と健気に頷いてみせた。
	ELSEIF TALENT:寝取らせ_献上品キャラ:羞恥心 == 1 || TALENT:寝取らせ_献上品キャラ:貞操 == 1
		PRINTFORMW 、%CALLNAME:寝取らせ_献上品キャラ%は俯きながらも、「%CALLNAME:寝取らせ_主導キャラ%のためだから」と健気に頷いてみせた。
	ELSEIF TALENT:寝取らせ_献上品キャラ:経験豊富
		PRINTFORMW 、%CALLNAME:寝取らせ_献上品キャラ%は%CALLNAME:PLAYER%の腕に胸を押し当てるように抱き着き、「%CALLNAME:寝取らせ_主導キャラ%が悪いんだから」と%CALLNAME:寝取らせ_主導キャラ%に流し目を送ってみせた。
	ELSEIF TALENT:寝取らせ_献上品キャラ:プライド == 1
		PRINTFORMW %CALLNAME:寝取らせ_献上品キャラ%は、%CALLNAME:寝取らせ_主導キャラ%によって%CALLNAME:PLAYER%に差し出される屈辱に顔をゆがめながらも渋々といった感じで頷いてみせた。
	ELSE
		PRINTFORMW 、%CALLNAME:寝取らせ_献上品キャラ%は俯きながらも、「%CALLNAME:寝取らせ_主導キャラ%のためだから」と強がってぎこちなく笑ってみせた。
	ENDIF
ENDIF
$訪問ラベル
PRINTL 
PRINTFORML ────さて、どうしようか？
DRAWLINE
PRINTBUTTON "[0] 受け入れる", 0
PRINTL
PRINTBUTTON "[1] 断る", 1
PRINTL
PRINTL
PRINTBUTTON "[100] 選択肢について説明", 100

BINPUT
;口上用に選択肢を記録
RFLAG:コマンド結果受渡し変数２ = RESULT
選択結果 = RESULT
SELECTCASE 選択結果
	CASE 0
		RESULT = 0
		TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_受諾_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
		SIF RESULT != 0
			GOTO 寝取らせ夜這い受諾_テキスト表示スキップ
		
		;貸出回数で分岐
		IF EXP:寝取らせ_献上品キャラ:貸出経験 >= 1
			PRINTFORML せっかく%CALLNAME:寝取らせ_主導キャラ%が用意してくれた据え膳だ。断る理由もない。
			PRINTL 
			PRINTFORML 以前%TEXTR("たっぷりと/心ゆくまで")%%TEXTR("味わった/堪能させてもらった/楽しんだ")%%CALLNAME:寝取らせ_献上品キャラ%の抱き心地を思い出しながら、%CALLNAME:PLAYER%は%CALLNAME:寝取らせ_献上品キャラ%に手を伸ばした……。
		ELSE
			PRINTFORML %CALLNAME:寝取らせ_主導キャラ%のたっての頼みだ。合意の上でのことであるなら、何を遠慮することもない。
			PRINTL 
			PRINTFORML ……それに、%CALLNAME:寝取らせ_献上品キャラ%と%CALLNAME:寝取らせ_主導キャラ%が今の関係になれたのだって、%CALLNAME:PLAYER%の仲立ちあってこそだ。
			PRINTFORMW %CALLNAME:PLAYER%にはこの、「正当な報酬」を受け取る権利があるだろう。
			PRINTL 
			PRINTFORML ──今はただ、楽しめばいい。思う存分、欲望のままに。
			PRINTFORMW 倒錯的な性癖に破壊された脳で%CALLNAME:寝取らせ_献上品キャラ%を差し出してくれる%CALLNAME:寝取らせ_主導キャラ%の姿に内心でほくそ笑みながら、%CALLNAME:PLAYER%は%CALLNAME:寝取らせ_献上品キャラ%を抱き寄せた……
		ENDIF
		;関係性で分岐
		IF カップリング関係性 != "セフレ"
		;なんかあなたとの関係性も参照されちゃったっぽくて恋慕じゃ発生しなかったので
			PRINTL 
			;ふたりの関係が恋慕で、差し出される側が浮気を持っていない場合。必然、献上品にとってはこれが初めての貸出になるはず
			IF TALENT:寝取らせ_献上品キャラ:浮気 == 0
				IF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
					PRINTFORM 愛する%CALLNAME:寝取らせ_主導キャラ%にまだ処女も捧げられていないのに、%CALLNAME:寝取らせ_主導キャラ%が見守るその目の前で、%CALLNAME:寝取らせ_献上品キャラ%は望まぬ
				ELSE
					PRINTFORM 愛する%CALLNAME:寝取らせ_主導キャラ%が見守るその目の前で、%CALLNAME:寝取らせ_献上品キャラ%は望まぬ
				ENDIF
				SETCOLOR カラーパレット("えっちな色")
				PRINT [浮気]
				RESETCOLOR
				PRINTFORMW へと足を踏み入れさせられてしまった……
			ELSE
				PRINTL
				PRINTFORM 愛する%CALLNAME:寝取らせ_主導キャラ%が見守るその目の前でまた、%CALLNAME:寝取らせ_献上品キャラ%の望まぬ
				SETCOLOR カラーパレット("えっちな色")
				PRINT [浮気]
				RESETCOLOR
				PRINTFORMW の幕が上がる……
			ENDIF
		ENDIF
		PRINTL 
		PRINTFORM %CALLNAME:寝取らせ_主導キャラ%の願いを叶えるために、%CALLNAME:寝取らせ_献上品キャラ%はこの一晩%CALLNAME:PLAYER%の
		SETCOLOR カラーパレット("えっちな色")
		PRINT [言いなり]
		RESETCOLOR
		PRINTFORMW だ。望めばなんだってしてくれるだろう……
		PRINTL 
		$寝取らせ夜這い受諾_テキスト表示スキップ
		
		RESULT = 0
		TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_履歴登録_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
		SIF RESULT != 0
			GOTO 寝取らせ夜這い受諾_履歴登録スキップ
		IF カップリング関係性 != "セフレ"
			;ふたりの関係が恋慕で、差し出される側が浮気を持っていない場合。必然、献上品にとってはこれが初めての貸出になるはず
			IF TALENT:寝取らせ_献上品キャラ:浮気 == 0
				IF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
					CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%からの願いで、まだ処女だった%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>まだ処女だったのに、恋人の%CALLNAME:寝取らせ_主導キャラ%のお願いで%CALLNAME:PLAYER%と[浮気]えっちさせられてしまった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に%CALLNAME:寝取らせ_献上品キャラ%の処女を差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
				ELSE
					CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%の願いで%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>恋人の%CALLNAME:寝取らせ_主導キャラ%にお願いされて、%CALLNAME:PLAYER%と[浮気]えっちさせられてしまった</font><img src='えっちハート'>","うふふ")
					CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に、恋人の%CALLNAME:寝取らせ_献上品キャラ%の自分ではまだ触れたこともない体を差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
				ENDIF
			ENDIF
		ELSEIF EXP:寝取らせ_献上品キャラ:貸出経験 == 0
			;はじめて貸し出される時
			IF TALENT:寝取らせ_献上品キャラ:処女 && GETBIT(TALENT:寝取らせ_献上品キャラ:性別, 0)
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%からの願いで、まだ処女だった%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>まだ処女だったのに、%CALLNAME:寝取らせ_主導キャラ%のお願いで、%CALLNAME:PLAYER%とセックスさせられてしまった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に、%CALLNAME:寝取らせ_献上品キャラ%の処女を差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
			ELSE
				CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%の願いで%CALLNAME:寝取らせ_献上品キャラ%を抱いてやった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_献上品キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:寝取らせ_主導キャラ%のお願いで、%CALLNAME:PLAYER%とセックスさせられてしまった</font><img src='えっちハート'>","うふふ")
				CALL 履歴データベース登録(CFLAG:寝取らせ_主導キャラ:人物番号, @"<img src='えっちハート'><font color='#%カラーパレット_HTML("赤ピンク")%'>%CALLNAME:PLAYER%に%CALLNAME:寝取らせ_献上品キャラ%を初めて差し出し、寝取ってもらった</font><img src='えっちハート'>","うふふ")
			ENDIF
		ENDIF
		$寝取らせ夜這い受諾_履歴登録スキップ
		
		SIF TALENT:寝取らせ_献上品キャラ:浮気 == 0
			TALENT:寝取らせ_献上品キャラ:浮気 = 1
		EXP:寝取らせ_主導キャラ:寝取られ経験 += 1
		EXP:寝取らせ_献上品キャラ:貸出経験 += 1
		TCVAR:寝取らせ_献上品キャラ:言いなり = 1
		;これが事実上の夜這いなので、あなたへの夜這いを抑制
		TCVAR:PLAYER:夜這い済み = 2
		;全員寝かせる
		;・日中に寝た時用処理。陥落済みのキャラが起きていると終了時に呼ぶ選択肢が出るため
		FOR 対象キャラ, 1, CHARANUM
			SIF !CFLAG:対象キャラ:睡眠
				CFLAG:対象キャラ:睡眠 = 1
		NEXT
		; 対象キャラを夜這い済みに設定
		TCVAR:寝取らせ_主導キャラ:夜這い済み = 2
		TCVAR:寝取らせ_献上品キャラ:夜這い済み = 2
		; 献上品キャラのうふふ用設定
		CFLAG:寝取らせ_献上品キャラ:睡眠 = 0
		CFLAG:寝取らせ_献上品キャラ:現在位置 = CFLAG:PLAYER:現在位置
		CFLAG:寝取らせ_献上品キャラ:現在マップ種別 = CFLAG:PLAYER:現在マップ種別
		CFLAG:寝取らせ_献上品キャラ:同室 = 1
		;あなたのうふふ用設定
		CFLAG:MASTER:うふふ = 1
		; 時間固定処理
		FLAG:イベントTIME記録 = TIME
		;処理の誤爆を防ぐためオートコマンド周りリセット
		TFLAG:オートコマンドフラグ = 0
		AUTOCOM_実行番号保存 = -1
		AUTOCOM_対象番号保存 = -1
		自慰ターゲット保存 = -1
		; 献上品キャラのうふふ用設定2
		CALL うふふ開始キャラ処理(寝取らせ_献上品キャラ, "寝取らせ")
		;TFLAG:寝取らせ夜這いうふふ済み
		;うふふ開始
		CALL TRAIN_イベントうふふ_LOOP()
		;うふふ終了後の処理
		CALL 寝取られオナニー処理(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
	CASE 1
		RESULT = 0
		TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_拒否_{NO:寝取らせ_主導キャラ}(寝取らせ_主導キャラ, 寝取らせ_献上品キャラ)
		IF RESULT == 0
			PRINTFORMW %CALLNAME:PLAYER%が今日は気分じゃないと伝えると、%CALLNAME:寝取らせ_主導キャラ%は残念そうな顔で%CALLNAME:寝取らせ_献上品キャラ%と一緒に帰っていった……
		ENDIF
		;断られるとしばらく対象キャラが主導の寝取らせ発生率が減少
		;・3日は発生しない値を設定
		;　補正値が残ってる状態で断られた場合は加算
		寝取らせ_発生率補正:寝取らせ_主導キャラ:0 += (寝取らせ夜這い判定_発生率取得(寝取らせ_主導キャラ) + (10 * 30)) * -1
	CASE 100
		;選択肢の説明
		DRAWLINE
		PRINTFORML 受け入れる：%CALLNAME:寝取らせ_主導キャラ%の性癖を叶えるために、%CALLNAME:寝取らせ_献上品キャラ%とうふふモードに突入します。
		PRINTFORML 　　　　　　%CALLNAME:寝取らせ_主導キャラ%のためなので、実行不可を除く全てのコマンドに、%CALLNAME:寝取らせ_献上品キャラ%は抵抗できません。
		PRINTFORML 　　　　　　ソースや好感度変化は通常通り行われるほか、%CALLNAME:寝取らせ_主導キャラ%の寝取られ経験と%CALLNAME:寝取らせ_献上品キャラ%の貸出経験がそれぞれ+１されます。
		PRINTFORML なお、%CALLNAME:寝取らせ_主導キャラ%はいないものとして扱います(自室か隠密状態で過酷なオナニーしてるイメージ)
		PRINTFORML ※このうふふモード中は時間が進行しません
		PRINTFORML 
		PRINTFORML 断る：断ります。%CALLNAME:寝取らせ_主導キャラ%はがっかりして帰り、寝取られ性癖が満たされ悪化することはありません。
		PRINTFORML 　　　%CALLNAME:寝取らせ_献上品キャラ%はたぶんほっとしてます。
		PRINTFORML 　　　しばらくの間、%CALLNAME:寝取らせ_主導キャラ%が寝取らせを提案しなくなります。
		WAIT
		GOTO 訪問ラベル
ENDSELECT
;寝取らせイベント用の変数をリセット
寝取らせ_主導キャラ = -1
寝取らせ_献上品キャラ = -1
寝取らせ_嘘告白特殊イベント発生済 = 0
RETURN 1


;寝取らせ_主導キャラがシコってる描写とかそこから生み出されるZPとか

@寝取られオナニー処理(オナニーキャラ, 寝取られキャラ)
#DIM オナニーキャラ
#DIM 寝取られキャラ
#DIM オナニー回数
#DIM オナニー絶頂回数
#DIM オナニー射精回数
#DIMS 射精語

PRINTL 

RESULT = 0
TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_寝取られオナニー口上_{NO:オナニーキャラ}(オナニーキャラ, 寝取られキャラ)
SIF RESULT != 0
	GOTO 寝取らせ夜這い_寝取られオナニーテキスト表示スキップ

PRINTFORM %CALLNAME:PLAYER%に抱かれて乱れ、
IF EXP:寝取られキャラ:貸出経験 >= 4
	SELECTCASE RAND:3
	CASE 0
		PRINTFORM 嬌声に紛れて無意識に%CALLNAME:PLAYER%への好意を垂れ流してしまっている
	CASE 1
		PRINTFORM 犯されながら%CALLNAME:PLAYER%%TEXTR("への感謝を口にさせられている/に愛の告白を強要されている")%
	CASE 2
		PRINTFORM %CALLNAME:オナニーキャラ%と比較するよう言いつけられるとたいした葛藤もなく%CALLNAME:PLAYER%のほうを選ぶ
	ENDSELECT
ELSEIF ABL:PLAYER:技巧 >= 4
	PRINTFORM 自分とのセックスでは聞いたことのないような%TEXTR("甘く蕩けた/甘く濁った/ケモノのような/だらしなく蕩けた")%嬌声をあげる
ELSE
	PRINTFORM すっかり%CALLNAME:PLAYER%の性技の虜にされてしまった
ENDIF
PRINTFORMW %CALLNAME:寝取られキャラ%の姿を隠れて見ていた%CALLNAME:オナニーキャラ%は、寝取られオナニーがとてもはかどったようだ……
PRINTL 
$寝取らせ夜這い_寝取られオナニーテキスト表示スキップ

RESULT = 0
TRYCALLFORM KOJO_EVENT_寝取らせ夜這い_寝取られオナニー実処理_{NO:オナニーキャラ}(オナニーキャラ, 寝取られキャラ)
SIF RESULT != 0
	GOTO 寝取らせ夜這い_寝取られオナニー処理スキップ

IF TALENT:オナニーキャラ:性別 == 2
;Ｖなしのオトコなら、絶頂は射精のみ
;経験加算値はなんとなくで決定したので、もっと説得力あるのがあればそっちに書き換えてください
	IF TALENT:オナニーキャラ:男性器サイズ <= -1
	;短小以下
		射精語 '= TEXTR("射精/ぴゅっぴゅ")
		SELECTCASE RAND:2
		CASE 0
			オナニー回数 = 5
			オナニー絶頂回数 = 2
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		CASE 1
			オナニー回数 = 6
			オナニー絶頂回数 = 3
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		ENDSELECT
	ELSEIF TALENT:オナニーキャラ:男性器サイズ == 0
	;普通
		射精語 '= "射精"
		SELECTCASE RAND:2
		CASE 0
			オナニー回数 = 6
			オナニー絶頂回数 = 3
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		CASE 1
			オナニー回数 = 7
			オナニー絶頂回数 = 4
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		ENDSELECT
	ELSEIF TALENT:オナニーキャラ:男性器サイズ >= 1
	;巨根以上
		射精語 '= "射精"
		SELECTCASE RAND:2
		CASE 0
			オナニー回数 = 8
			オナニー絶頂回数 = 4
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		CASE 1
			オナニー回数 = 9
			オナニー絶頂回数 = 5
			CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
			オナニー射精回数 = RESULT
		ENDSELECT
	ENDIF
	;リザルト
	SELECTCASE オナニー射精回数
	CASE 0
		PRINTFORM %CALLNAME:オナニーキャラ%は%TOFULL(TOSTR(オナニー回数))%回のオナニーで%TOFULL(TOSTR(オナニー絶頂回数))%回の絶頂を迎えたが、その
		IF TALENT:オナニーキャラ:男性器サイズ <= -2
			PRINT 粗末なおちんちん
		ELSEIF TALENT:オナニーキャラ:男性器サイズ <= -1
			PRINT 小さなちんちん
		ELSEIF TALENT:オナニーキャラ:男性器サイズ == 0
			PRINT 肉棒
		ELSE
			PRINT 見掛け倒しの大きな肉棒
		ENDIF
		PRINTFORMW から精液が出ることは１回もなかった……
	CASE 1
		PRINTFORMW １回%射精語%できたようだ！もっとがんばろう！
	CASE 2
		PRINTFORMW なんと、２回も%射精語%できたようだ！よかったね！
	CASE 3
		PRINTFORMW なんと、３回も%射精語%できたようだ！やったね！
	CASE 4
		PRINTFORMW なんと！４回も%射精語%できたようだ！すごいね！！
	CASE 5
		PRINTFORMW なんと！５回も%射精語%できたようだ！！がんばったね！！
	ENDSELECT
	EXP:オナニーキャラ:自慰経験 += オナニー回数
	PRINTFORML 自慰経験 + {オナニー回数}
	EXP:オナニーキャラ:絶頂経験 += オナニー絶頂回数
	PRINTFORML 絶頂経験 + {オナニー絶頂回数}
	EXP:オナニーキャラ:Ｃ絶頂経験 += オナニー絶頂回数
	PRINTFORML Ｃ絶頂経験 + {オナニー絶頂回数}
	IF オナニー射精回数 > 0
		EXP:オナニーキャラ:射精経験 += オナニー射精回数
		PRINTFORML 射精経験 + {オナニー射精回数}
	ENDIF
	FLAG:ZP所持量 += オナニー絶頂回数
	PRINTFORML ZP所持量 + {オナニー絶頂回数}

ELSE
;Ｖありなら射精以外でも絶頂できるから回数いっぱい。でもあなたほどいっぱいイけるわけじゃない
	;Ｃ絶頂
	IF GETBIT(TALENT:オナニーキャラ:性別, 1)
		;ちんちんある場合
		SELECTCASE RAND:2
			CASE 0
				EXP:オナニーキャラ:Ｃ絶頂経験 += 3
				PRINTFORML Ｃ絶頂経験 + 3
				オナニー回数 = 5
				オナニー絶頂回数 = 3
				CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
				オナニー射精回数 = RESULT
			CASE 1
				EXP:オナニーキャラ:Ｃ絶頂経験 += 2
				PRINTFORML Ｃ絶頂経験 + 2
				オナニー回数 = 5
				オナニー絶頂回数 = 2
				CALL コマンド外精力消費処理(オナニーキャラ, オナニー絶頂回数)
				オナニー射精回数 = RESULT
		ENDSELECT
	ELSE
		;クリの場合
		SELECTCASE RAND:3
			CASE 0
				EXP:オナニーキャラ:Ｃ絶頂経験 += 3
				PRINTFORML Ｃ絶頂経験 + 3
				オナニー回数 = 5
				オナニー絶頂回数 = 3
			CASE 1
				EXP:オナニーキャラ:Ｃ絶頂経験 += 4
				PRINTFORML Ｃ絶頂経験 + 4
				オナニー回数 = 6
				オナニー絶頂回数 = 4
			CASE 2
				EXP:オナニーキャラ:Ｃ絶頂経験 += 5
				PRINTFORML Ｃ絶頂経験 + 5
				オナニー回数 = 7
				オナニー絶頂回数 = 5
		ENDSELECT
	ENDIF
	;Ｂ絶頂
	SELECTCASE RAND:3
		CASE 0
			EXP:オナニーキャラ:Ｂ絶頂経験 += 3
			PRINTFORML Ｂ絶頂経験 + 3
			オナニー回数 += 5
			オナニー絶頂回数 += 3
		CASE 1
			EXP:オナニーキャラ:Ｂ絶頂経験 += 4
			PRINTFORML Ｂ絶頂経験 + 4
			オナニー回数 += 6
			オナニー絶頂回数 += 4
		CASE 2
			EXP:オナニーキャラ:Ｂ絶頂経験 += 5
			PRINTFORML Ｂ絶頂経験 + 5
			オナニー回数 += 7
			オナニー絶頂回数 += 5
	ENDSELECT
	IF TALENT:オナニーキャラ:処女 == 0
	;処女でないならＶでもオナニー
		SELECTCASE RAND:3
			CASE 0
				EXP:オナニーキャラ:Ｖ絶頂経験 += 3
				PRINTFORML Ｖ絶頂経験 + 3
				オナニー回数 += 5
				オナニー絶頂回数 += 3
			CASE 1
				EXP:オナニーキャラ:Ｖ絶頂経験 += 4
				PRINTFORML Ｖ絶頂経験 + 4
				オナニー回数 += 6
				オナニー絶頂回数 += 4
			CASE 2
				EXP:オナニーキャラ:Ｖ絶頂経験 += 5
				PRINTFORML Ｖ絶頂経験 + 5
				オナニー回数 += 7
				オナニー絶頂回数 += 5
		ENDSELECT
	ENDIF
	;リザルト
	PRINTFORM %CALLNAME:オナニーキャラ%は
	SELECTCASE RAND:3
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER%のおかげで、{オナニー絶頂回数}回も絶頂できたようだ……
		CASE 1
			PRINTFORMW たくさんオナニーして、{オナニー絶頂回数}ZPも稼いでくれたようだ……
		CASE 2
			PRINTFORMW {オナニー回数}回のオナニーで{オナニー絶頂回数}回もイけたようだ……
	ENDSELECT
	EXP:オナニーキャラ:自慰経験 += オナニー回数
	PRINTFORML 自慰経験 + {オナニー回数}
	EXP:オナニーキャラ:絶頂経験 += オナニー絶頂回数
	PRINTFORML 絶頂経験 + {オナニー絶頂回数}
	IF オナニー射精回数 > 0
		EXP:オナニーキャラ:射精経験 += オナニー射精回数
		PRINTFORML 射精経験 + {オナニー射精回数}
	ENDIF
	FLAG:ZP所持量 += オナニー絶頂回数
	PRINTFORML ZP所持量 + {オナニー絶頂回数}
ENDIF
$寝取らせ夜這い_寝取られオナニー処理スキップ
IF CFLAG:オナニーキャラ:リゾート内オナニー回数 < 1
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めてリゾート内で[オナニー]をした</font>","うふふ")
ELSEIF CFLAG:オナニーキャラ:リゾート内オナニー回数 == 9
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１０回に到達した</font>","うふふ")
ELSEIF CFLAG:オナニーキャラ:リゾート内オナニー回数 == 49
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が５０回に到達した</font>","うふふ")
ELSEIF CFLAG:オナニーキャラ:リゾート内オナニー回数 == 99
	CALL 履歴データベース登録(CFLAG:オナニーキャラ:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>リゾート内で[オナニー]をした回数が１００回に到達した</font>","うふふ")
ENDIF
CFLAG:オナニーキャラ:リゾート内オナニー回数 ++
PRINTL

@寝取らせ夜這い判定_キャラ状態判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
;酔い潰れや体力０の時は発生しない
;泥酔眠姦が実装されたら献上品の酔いすぎは条件から外してもいいと思う
SIF CFLAG:対象キャラ:酔いすぎ
	RETURNF 0
SIF CFLAG:対象キャラ:体力限界
	RETURNF 0
RETURNF 1

@寝取らせ夜這い判定_身体条件判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
;どちらかが「ＣもＶもないキャラ」だった場合発生しない
SIF TALENT:対象キャラ:Ｖ感度 == -2 && TALENT:対象キャラ:Ｃ感度 == -2
	RETURNF 0
RETURNF 1


@寝取らせ夜這い判定_信頼度判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
;とりあえず寝取らせ_主導キャラの信頼が親友以上から発生するようにしてみる。献上品の信頼は不問
;ただしあなたの遊び人が４以上の竿役適任つよつよオスである場合、ハードルがワンランク下がる。

IF TALENT:対象キャラ:従業員
	;寝取らせ_主導キャラが従業員である場合、「気にくわない上司に寝取られる」的なプレイを望むかも、みたいなフレーバーで信頼度要求を劇的に下げてみる。
	;これはこれで個人的に癖なので、思いついちゃったし仕方ないよね！縁結びでくっつける手間もあるわけだし……まぁヨシ！
	SIF CFLAG:対象キャラ:信頼度 < 経験値テーブル:(関係閾値:1)
		RETURNF 0
ELSEIF あなた特殊能力:遊び人 >= 4
	SIF CFLAG:対象キャラ:信頼度 < 経験値テーブル:(関係閾値:3)
		RETURNF 0
ELSE
	SIF CFLAG:対象キャラ:信頼度 < 経験値テーブル:(関係閾値:4)
		RETURNF 0
ENDIF
RETURNF 1

@寝取らせ夜這い判定_発生率取得(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 発生率
;夜這いは性欲条件だけど、寝取られは何を条件にするのが適切なのかわからない。
;ので、普通に確率制御にしてみる。とりあえず性癖開拓のとこから処理持ってきていじってみる

;基礎確率は寝取られ性癖ごとに8%。きついのは初回だけでイベントが引ければ即2になるので、実質基礎16%
;ここで最大24%(Lv3×8)盛って、あとはおまけ程度の補正にする
SIF 性癖素質:対象キャラ:寝取られ > 0
	発生率 = 性癖素質:対象キャラ:寝取られ * 8

;あなたへの信頼。おまけ
SELECTCASE CFLAG:対象キャラ:信頼度
	CASE IS >= 経験値テーブル:(関係閾値:4)
		発生率 += 2
	CASEELSE
		;その他は補正なし
		発生率 += 0
ENDSELECT

;あなたの遊び人素質による加算。最大10%
;女の扱いがうまいと噂がたっていれば、性癖を満たすに丁度いい竿役と認識してもらえる可能性が高くなると思われるため
発生率 += あなた特殊能力:遊び人 * 2

;最低確率は5%。一応設定だけしとく。
発生率 = MAX(発生率, 5)

;これで最大確率36%。毎晩出てこられても困るだろうし、こんなもんじゃない？
RETURNF 発生率

@寝取らせ夜這い判定_確率判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 発生率

;発生率取得
発生率 = 寝取らせ夜這い判定_発生率取得(対象キャラ)

;補正
;・過去に断られたことによる発生率減少
;　この補正による発生率の最低確率を超えて0％にする(断られてからしばらくは絶対に発生しない)
発生率 = MAX(発生率 + 寝取らせ_発生率補正:対象キャラ:0, 0)

;確率判定
SIF RAND:100 < 発生率
	RETURNF 1
RETURNF 0

